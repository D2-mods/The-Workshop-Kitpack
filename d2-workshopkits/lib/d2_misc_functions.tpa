//
//

/*functions:
d2_portrait_icon              ACTION
d2_subspell_remove_effects    PATCH
d2_remove_level_headers       PATCH
d2_subspell_remove_visuals    PATCH
d2_remove_effects_on_cast     PATCH
d2_protect_on_castequip       PATCH
d2_no_casting                 PATCH
d2_no_fatigue                 PATCH
block_death_gaze              PATCH
update_op324_restrictions     PATCH
d2_remove_spelltypes          PATCH
d2_mindamageboost             PATCH
d2_addhitstun                 PATCH
castsound_to_opcode           PATCH
d2_immunity_spl_block         PATCH
read_or_set_scriptname        PATCH
read_or_set_areascript        PATCH
*/

////////////////////////////////////////////////////////////

//Adds new portrait icon to statdesc.2da
//Will use existing icon with same name and modder prefix
//NOTE: no limit to number of icons that can be added

/*example
LAF d2_portrait_icon INT_VAR name = RESOLVE_STR_REF (@99) STR_VAR bam = ~icon99~ RET icon END

"icon" is number for related opcodes
*/

DEFINE_ACTION_FUNCTION d2_portrait_icon
  INT_VAR name=0
  STR_VAR bam=none prefix=d2
  RET icon
BEGIN

OUTER_SET icon = ~-1~                 // default is no change
ACTION_IF !(GAME_IS ~pstee~) BEGIN    // not used for pstee

  COPY_EXISTING ~STATDESC.2DA~ ~override~

    // Set some variables
    GET_STRREF name iconname
    READ_2DA_ENTRIES_NOW ~statdesc_table~ 1
    COUNT_2DA_ROWS 1 rows
    SET row_count = (rows - 1)    // count starts at 0

    // uses existing icon if found (checks modder prefix)
    FOR (r = 3; r < rows; ++r) BEGIN
      READ_2DA_ENTRY_FORMER ~statdesc_table~ r 1 stringref
      GET_STRREF stringref string
      PATCH_IF (~%string%~ STRING_EQUAL_CASE ~%iconname%~) BEGIN    // if icon name is already used
        READ_2DA_ENTRY_FORMER ~statdesc_table~ r 2 resource
        PATCH_IF !(~%resource%~ STRING_MATCHES_REGEXP ~^%prefix%.*~) BEGIN    // if resource DOES start with "prefix"
          READ_2DA_ENTRY_FORMER ~statdesc_table~ r 0 icon
          SET icon = icon    // RET variable
        END
      END
    END

    // Otherwise, create new icon
    PATCH_IF (icon = ~-1~) BEGIN
      READ_2DA_ENTRY_FORMER ~statdesc_table~ row_count 0 icon    // get last icon number, then add +1
      SET icon += 1    // RET variable
      INSERT_2DA_ROW rows 1 ~%icon% %name% %bam%~    // add new line with variables
      PRETTY_PRINT_2DA
    END
  IF_EXISTS BUT_ONLY

END
END

////////////////////////////////////////////////////////////

//prep for subspells
//remove casting effects, set level/power/speed to 0, no projectile

DEFINE_PATCH_FUNCTION d2_subspell_remove_effects INT_VAR globals=1 BEGIN

  WRITE_LONG  0x8 ~-1~
  WRITE_ASCII 0x10 ~None~ #8
  WRITE_SHORT 0x1c 0
  WRITE_SHORT 0x22 0
  WRITE_BYTE  0x25 0
  WRITE_BYTE  0x27 0
  WRITE_LONG  0x34 0
  LPF ALTER_SPELL_HEADER INT_VAR target=1 range=1 speed=0 projectile=1 END
  LPF ALTER_EFFECT INT_VAR power=0 END

  PATCH_IF (globals > 0) BEGIN
    LPF DELETE_EFFECT INT_VAR check_headers=0 END
  END

END

////////////////////////////////////////////////////////////

//Removes spell headers except the top one
//header is set to 1 (called "melee", but also used for spells)

DEFINE_PATCH_FUNCTION d2_remove_level_headers

BEGIN

  LPF ALTER_SPELL_HEADER INT_VAR header=1 new_header_type=0 END
  LPF DELETE_SPELL_HEADER INT_VAR header_type=1 END
  LPF DELETE_SPELL_HEADER INT_VAR header_type=2 END
  LPF DELETE_SPELL_HEADER INT_VAR header_type=3 END
  LPF ALTER_SPELL_HEADER INT_VAR new_header_type=1 min_level=1 END

END

////////////////////////////////////////////////////////////

//Removes visual and audio effects (not casting effects)
//Removes portrait icon and dialogue strings

DEFINE_PATCH_FUNCTION d2_subspell_remove_visuals

  INT_VAR visual   = 1    // 1 = remove, 0 = not remove
          sound    = 1
          icon     = 1
          string   = 1

BEGIN

  PATCH_IF (visual > 0) BEGIN
    LPF DELETE_EFFECT INT_VAR match_opcode=8 END
    LPF DELETE_EFFECT INT_VAR match_opcode=9 END
    LPF DELETE_EFFECT INT_VAR match_opcode=50 END
    LPF DELETE_EFFECT INT_VAR match_opcode=61 END
    LPF DELETE_EFFECT INT_VAR match_opcode=141 END
    LPF DELETE_EFFECT INT_VAR match_opcode=215 END
  END

  PATCH_IF (sound > 0) BEGIN
    LPF DELETE_EFFECT INT_VAR match_opcode=174 END
  END

  PATCH_IF (icon > 0) BEGIN
    LPF DELETE_EFFECT INT_VAR match_opcode=142 END
  END

  PATCH_IF (string > 0) BEGIN
    LPF DELETE_EFFECT INT_VAR match_opcode=139 END
  END

END

////////////////////////////////////////////////////////////

//simplifies adding op321 to spells/items
//if no variables are set, will remove effects of current resource on cast/equip
//can optionally set cre_type to check splprot stat (cre_special sets parameter1, if needed)

/*326override
replaces op321 with op326 (i.e. cast spell with list of op321's)
This option is ignored if resource is set to current file
*/

/*defaults
resource - file being patched
spl files - skips globals
itm files - skips headers
check all header types (always for SPL, can set to specific for ITM)
matches first effect in stack (can set opcode to match)
adds as first effect in stack
*/

DEFINE_PATCH_FUNCTION d2_remove_effects_on_cast
  INT_VAR globals=~-1~ headers=~-1~ header_type=~-1~ match_opcode=~-1~ probability=~-1~
          cre_type=0 cre_special=0 timing=1 duration=0 326override=0 overwrite=1
  STR_VAR resource=~~ insert=first
BEGIN

  //prep
  PATCH_IF (~%resource%~ STR_EQ ~~) BEGIN  TEXT_SPRINT  resource ~%DEST_RES%~ END

  SET opcode = 321
  PATCH_IF (326override > 0) BEGIN
    PATCH_IF !(~%resource%~ STR_EQ ~%DEST_RES%~) BEGIN
      PATCH_IF (overwrite > 0) BEGIN
        LPF DELETE_EFFECT INT_VAR match_opcode=326 match_parameter2=cre_type STR_VAR match_resource=EVAL ~%resource%~ END
      END
      SET opcode = 326
    END
  END

  PATCH_IF (opcode = 321) BEGIN
    PATCH_IF (overwrite > 0) BEGIN
      LPF DELETE_EFFECT INT_VAR match_opcode=321 STR_VAR match_resource=EVAL ~%resource%~ END
    END
    PATCH_IF ((cre_type < 0) OR (cre_type > 2)) BEGIN
      SET cre_type = 0
    END
  END

  PATCH_IF (globals < 0) BEGIN  PATCH_IF (~%DEST_EXT%~ STR_EQ ~spl~) BEGIN
      SET globals = 0
    END ELSE BEGIN
      SET globals = 1
    END 
  END

  PATCH_IF (headers < 0) BEGIN  PATCH_IF (~%DEST_EXT%~ STR_EQ ~itm~) BEGIN
      SET headers = 0
    END ELSE BEGIN
      SET headers = 1
    END
  END

  PATCH_IF (~%DEST_EXT%~ STR_EQ ~spl~) BEGIN  SET header_type = ~-1~ END

  //add effect
  LPF CLONE_EFFECT 
    INT_VAR check_globals=globals check_headers=headers header_type=header_type match_opcode=match_opcode
            silent=1 multi_match=1 opcode=opcode parameter1=cre_special parameter2=cre_type timing=timing duration=duration
            probability1=probability probability2=0 dicenumber=0 dicesize=0 savingthrow=0 special=0
    STR_VAR resource=EVAL ~%resource%~  insert=EVAL ~%insert%~
  END

END

////////////////////////////////////////////////////////////

//variation of above function (protect instead of remove effect)
//more general version of d2_safe_spell (can protect from itself or other spells)
//can optionally set 'cre_type' variable to affect specific creature type

/*opcode and timing
opcode must be 318 or 324 (defaults to 318 if not)
Default for spells is duration 1 round
ITM equipped effects are automatically set to timing=2 (equipped)
CRE effects are automatically set to timing=9 (permanent)
*/

/*defaults
resource - file being patched
spl files - skips globals
itm files - skips headers
check all header types (always for SPL, can set to specific for ITM)
matches first effect in stack (can set opcode to match)
adds as LAST effect in stack
*/

DEFINE_PATCH_FUNCTION d2_protect_on_castequip
  INT_VAR globals=~-1~ headers=~-1~ header_type=~-1~ match_opcode=~-1~ probability=~-1~
          opcode=318 timing=0 duration=6 cre_type=0 cre_special=0 overwrite=1
  STR_VAR resource=~~ insert=last
BEGIN

  //prep
  PATCH_IF (~%resource%~ STR_EQ ~~) BEGIN  TEXT_SPRINT  resource ~%DEST_RES%~ END
  PATCH_IF (overwrite > 0) BEGIN
    LPF DELETE_EFFECT INT_VAR match_opcode=318 match_parameter2=cre_type STR_VAR match_resource=EVAL ~%resource%~ END
    LPF DELETE_EFFECT INT_VAR match_opcode=324 match_parameter2=cre_type STR_VAR match_resource=EVAL ~%resource%~ END
  END
  PATCH_IF (opcode != 318 AND opcode != 324) BEGIN  SET opcode = 318 END

  PATCH_IF (globals < 0) BEGIN  PATCH_IF (~%DEST_EXT%~ STR_EQ ~spl~) BEGIN
      SET globals = 0
    END ELSE BEGIN
      SET globals = 1
    END 
  END

  PATCH_IF (headers < 0) BEGIN  PATCH_IF (~%DEST_EXT%~ STR_EQ ~itm~) BEGIN
      SET headers = 0
    END ELSE BEGIN
      SET headers = 1
    END
  END

  PATCH_IF (~%DEST_EXT%~ STR_EQ ~spl~) BEGIN  SET header_type = ~-1~ END

  PATCH_IF (timing=1 OR timing=9) BEGIN  SET duration=0 END
  SET htiming=timing SET hduration=duration
  SET gtiming=timing SET gduration=duration
  PATCH_IF (~%DEST_EXT%~ STR_EQ ~itm~) BEGIN  SET gtiming=2 SET gduration=0 END
  PATCH_IF (~%DEST_EXT%~ STR_EQ ~cre~) BEGIN  SET gtiming=9 SET gduration=0 END

  //add effect
  PATCH_IF (globals > 0) BEGIN
    LPF CLONE_EFFECT 
      INT_VAR check_globals=1 check_headers=0 header_type=header_type match_opcode=match_opcode
              silent=1 multi_match=1 opcode=opcode parameter1=cre_special parameter2=cre_type timing=gtiming duration=gduration
              probability1=probability probability2=0 dicenumber=0 dicesize=0 savingthrow=0 special=0
      STR_VAR resource=EVAL ~%resource%~  insert=EVAL ~%insert%~
    END
  END

  PATCH_IF (headers > 0) BEGIN
    LPF CLONE_EFFECT 
      INT_VAR check_globals=0 check_headers=1 header_type=header_type match_opcode=match_opcode
              silent=1 multi_match=1 opcode=opcode parameter1=cre_special parameter2=cre_type timing=htiming duration=hduration
              probability1=probability probability2=0 dicenumber=0 dicesize=0 savingthrow=0 special=0
      STR_VAR resource=EVAL ~%resource%~  insert=EVAL ~%insert%~
    END
  END

END

////////////////////////////////////////////////////////////

//remove casting effects and spell school/type
//ignore wild/dead magic and silence

DEFINE_PATCH_FUNCTION d2_no_casting
  INT_VAR no_schools=1 no_castsound=0 see_invis=0
          range=~-1~ speed=0 power=~-1~    // -1 = no change
BEGIN

  WRITE_LONG 0x18 THIS | BIT14    // ignore wild magic
  WRITE_LONG 0x18 THIS | BIT25    // ignore silence
  WRITE_SHORT 0x22 0              // no casting visual

  PATCH_IF (no_schools > 0) BEGIN  
    WRITE_BYTE  0x25 0  
    WRITE_BYTE  0x27 0  
  END

  PATCH_IF (no_castsound > 0) BEGIN
    WRITE_ASCII 0x10 ~None~ #8
  END

  PATCH_IF (see_invis > 0) BEGIN  
    WRITE_LONG 0x18 THIS | BIT24
  END

  LPF ALTER_SPELL_HEADER INT_VAR range=range speed=speed END
  LPF ALTER_EFFECT INT_VAR power=power END

END

////////////////////////////////////////////////////////////

//run this to remove fatigue from SPL/ITM (ex. from haste/restoration)

DEFINE_PATCH_FUNCTION d2_no_fatigue BEGIN
  LPF DELETE_EFFECT INT_VAR match_opcode=93 END              // remove fatigue opcode
  LPF DELETE_EFFECT STR_VAR match_resource=~dvwinded~ END    // remove SR fatigue
  LPF DELETE_EFFECT STR_VAR match_resource=~SPCL321D~ END    // remove Enrage fatigue
END

////////////////////////////////////////////////////////////

//blocks Death Gaze in BGEE/EET
//needs to match an opcode (default 101 = immunity opcode)
//can optionally block gaze projectile (default on)

DEFINE_PATCH_FUNCTION block_death_gaze
  INT_VAR block_gaze=1 match_opcode=101
BEGIN

  TEXT_SPRINT death_gaze ~SPIN996~
  PATCH_IF (GAME_IS ~eet~)  BEGIN TEXT_SPRINT death_gaze ~BGIN996~ END

  PATCH_IF (block_gaze > 0) BEGIN
    LPF DELETE_EFFECT INT_VAR match_opcode=83 match_parameter2=64 END
    LPF CLONE_EFFECT INT_VAR silent=1 multi_match=1 match_opcode=match_opcode opcode=83 parameter2=64 END
  END

  PATCH_IF (GAME_IS ~bgee eet~) BEGIN
    LPF DELETE_EFFECT STR_VAR match_resource=EVAL ~%death_gaze%~ END
    LPF CLONE_EFFECT
      INT_VAR silent=1 multi_match=1 match_opcode=match_opcode opcode=206 parameter1=~-1~
      STR_VAR resource=EVAL ~%death_gaze%~
    END
  END

END

////////////////////////////////////////////////////////////

//updates the resource field on op324 and op318
//resource is set to the current file being patched

DEFINE_PATCH_FUNCTION update_op324_restrictions
  INT_VAR opcode = 324     // 318 or 324
  STR_VAR mode = update    // "update" or "delete"
BEGIN

  PATCH_IF ((opcode != 318) AND (opcode != 324)) BEGIN  SET opcode=318 END

  PATCH_IF (opcode=318) BEGIN
    LPF ALTER_EFFECT INT_VAR silent=1 match_opcode=324 opcode=opcode END
  END
  PATCH_IF (opcode=324) BEGIN
    LPF ALTER_EFFECT INT_VAR silent=1 match_opcode=318 opcode=opcode END
  END

  PATCH_IF (~%mode%~ STR_EQ ~update~) BEGIN
    LPF ALTER_EFFECT INT_VAR silent=1 match_opcode=opcode STR_VAR resource=EVAL ~%DEST_RES%~ END
  END
  PATCH_IF ((~%mode%~ STR_EQ ~delete~) OR (~%mode%~ STR_EQ ~remove~)) BEGIN
    LPF DELETE_EFFECT INT_VAR match_opcode=opcode END
  END

END

////////////////////////////////////////////////////////////

//dispel Secondary types (skips level 0 spells)
//set insert=last to put at bottom of stack (default top)

DEFINE_PATCH_FUNCTION d2_remove_spelltypes INT_VAR buffs=1 debuffs=1 STR_VAR insert=first BEGIN
  PATCH_IF (debuffs > 0) BEGIN
    LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=221 timing=1 parameter1=10 parameter2=4 STR_VAR insert=EVAL ~%insert%~ END    // MAGICATTACK
    LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=221 timing=1 parameter1=10 parameter2=5 STR_VAR insert=EVAL ~%insert%~ END    // DIVINATIONATTACK
    LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=221 timing=1 parameter1=10 parameter2=6 STR_VAR insert=EVAL ~%insert%~ END    // CONJURATION
    LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=221 timing=1 parameter1=10 parameter2=9 STR_VAR insert=EVAL ~%insert%~ END    // BATTLEGROUND
    LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=221 timing=1 parameter1=10 parameter2=10 STR_VAR insert=EVAL ~%insert%~ END    // OFFENSIVEDAMAGE
    LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=221 timing=1 parameter1=10 parameter2=11 STR_VAR insert=EVAL ~%insert%~ END    // DISABLING
  END
  PATCH_IF (buffs > 0) BEGIN
    LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=221 timing=1 parameter1=10 parameter2=1 STR_VAR insert=EVAL ~%insert%~ END    // SPELLPROTECTIONS
    LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=221 timing=1 parameter1=10 parameter2=2 STR_VAR insert=EVAL ~%insert%~ END    // SPECIFICPROTECTIONS
    LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=221 timing=1 parameter1=10 parameter2=3 STR_VAR insert=EVAL ~%insert%~ END    // ILLUSIONARYPROTECTIONS
    LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=221 timing=1 parameter1=10 parameter2=7 STR_VAR insert=EVAL ~%insert%~ END    // COMBATPROTECTIONS
    LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=221 timing=1 parameter1=10 parameter2=12 STR_VAR insert=EVAL ~%insert%~ END    // COMBINATION
    LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=221 timing=1 parameter1=10 parameter2=13 STR_VAR insert=EVAL ~%insert%~ END    // NON-COMBAT
  END
END

////////////////////////////////////////////////////////////

DEFINE_PATCH_FUNCTION d2_mindamageboost
  INT_VAR amount=2 timing=0 duration=12
          match_opcode=~-1~ overwrite=1 probability=100
  STR_VAR insert=last
BEGIN
  PATCH_IF (overwrite > 0) BEGIN
    LPF DELETE_EFFECT INT_VAR match_opcode=250 END
  END

  PATCH_IF (duration < 1) BEGIN SET duration = 6 END
  PATCH_IF (timing<0 OR timing>9) BEGIN SET timing = 0 END
  PATCH_IF (amount < 1) BEGIN SET amount = 1 END

  LPF CLONE_EFFECT
    INT_VAR silent=1 check_globals=0 multi_match=1 match_opcode=match_opcode
            opcode=250 parameter1=amount timing=timing duration=duration
            probability1=probability probability2=0 dicenumber=0 dicesize=0 savingthrow=0 special=0
    STR_VAR insert=EVAL ~%insert%~
  END
END

////////////////////////////////////////////////////////////

//add hit stun (op45) to spl or itm (default 1 second)
//can optionally require matching an opcode (same target, saves, dispel/resist, probability)
//if not matching, then adds as new effect (default 100 probability, no save/dispel/resist)

/*
no_saves variable removes saves when matching opcode (i.e. if matching damage with save for half)
by default, adds as last effect or below a match
*/

DEFINE_PATCH_FUNCTION d2_addhitstun
  INT_VAR duration=~-1~ no_saves=0 match_opcode=~-1~ overwrite=1 probability=~-1~ multi_match=999
  STR_VAR insert=below
BEGIN
  PATCH_IF (duration < 1) BEGIN SET duration = 1 END
  PATCH_IF (overwrite > 0) BEGIN
    LPF DELETE_EFFECT INT_VAR match_opcode=45 match_duration=duration END
  END

  SET savingthrow = ~-1~
  PATCH_IF (no_saves > 0) BEGIN SET savingthrow = 0 END

  PATCH_IF (match_opcode >= 0) BEGIN
    LPF CLONE_EFFECT
      INT_VAR silent=1 check_globals=0 multi_match=multi_match match_opcode=match_opcode
              opcode=45 timing=0 duration=duration parameter1=0 parameter2=0 savingthrow=savingthrow
              probability1=probability probability2=0 dicenumber=0 dicesize=0 special=0
      STR_VAR insert=EVAL ~%insert%~
    END
  END ELSE BEGIN
    PATCH_IF (probability < 5) BEGIN SET probability = 100 END
    PATCH_IF (~%DEST_EXT%~ STR_EQ ~spl~) BEGIN
      LPF ADD_SPELL_EFFECT INT_VAR opcode=45 target=2 duration=duration probability1=probability END
    END
    PATCH_IF (~%DEST_EXT%~ STR_EQ ~itm~) BEGIN
      LPF ADD_ITEM_EFFECT INT_VAR opcode=45 target=2 duration=duration probability1=probability type=1 insert_point=0 END
      LPF ADD_ITEM_EFFECT INT_VAR opcode=45 target=2 duration=duration probability1=probability type=2 insert_point=0 END
      LPF ADD_ITEM_EFFECT INT_VAR opcode=45 target=2 duration=duration probability1=probability type=4 insert_point=0 END
    END
  END
END

////////////////////////////////////////////////////////////

//changes casting sound to an opcode effect (cast sound is removed)
//skipped silently if no casting sound, or if wav file does not exist

DEFINE_PATCH_FUNCTION castsound_to_opcode INT_VAR match_opcode=~-1~ STR_VAR insert=first BEGIN
  PATCH_IF (~%SOURCE_EXT%~ STR_EQ ~spl~) BEGIN
    READ_ASCII 0x10 castsound (8) NULL
    WRITE_ASCII 0x10 ~~ #8
    SET patch_sound = 0
    PATCH_IF (FILE_EXISTS_IN_GAME ~%castsound%.wav~) BEGIN SET patch_sound = 1 END

    PATCH_IF (patch_sound > 0) BEGIN
      LPF CLONE_EFFECT
        INT_VAR silent=1 check_globals=0 multi_match=1 match_opcode=match_opcode
                opcode=174 timing=1 duration=0 parameter1=0 parameter2=0
                probability1=100 probability2=0 dicenumber=0 dicesize=0 savingthrow=0 special=0
        STR_VAR resource=EVAL ~%castsound%~ insert=EVAL ~%insert%~
      END
    END
  END
END

////////////////////////////////////////////////////////////

//matches an op101 (immunity to effect) to block a specific resource
//"opcode" can be 318, 324, or 206

/*
"block" is resource to block in patched SPL
"effect" is the number in parameter2 of op101 to match (default 13 = kill target)
*/

//silently skipped if resource doesn't exist
//silently skipped if op101 with "effect" is not in file
//automatically switches to opcode 206 if game is not EE v2.0+

DEFINE_PATCH_FUNCTION d2_immunity_spl_block INT_VAR opcode=318 effect=13 STR_VAR block=vorpal BEGIN
//  PATCH_IF (FILE_EXISTS_IN_GAME ~%block%.spl~) BEGIN
  PATCH_IF !(RESOURCE_CONTAINS ~%SOURCE_FILE%~  ~[^0-9^a-z]%block%[^0-9^a-z]~) BEGIN
    PATCH_IF (opcode!=324 && opcode!=206) BEGIN  SET opcode=318  END
    PATCH_IF !(FILE_EXISTS ~engine.lua~) BEGIN  SET opcode=206  END
    PATCH_IF !(RESOURCE_CONTAINS ~action.ids~ ~zoomlock~) BEGIN  SET opcode=206  END

    PATCH_IF !(GAME_IS ~bg1 totsc pst~) BEGIN
      LPF CLONE_EFFECT
        INT_VAR silent=1 match_opcode=101 match_parameter2=effect
                opcode=opcode parameter1=0 parameter2=0 special=0
        STR_VAR resource=EVAL ~%block%~
      END
    END
  END
//  END
END

////////////////////////////////////////////////////////////

//read or set script name/death variable (all IE games)
//does not change scriptname if it has one already
//if "name" variable not set, it automatically uses the CRE resource
//if "replace" is set to 1, it overwrites existing name
//scriptname and deathvar are the same thing, you can return either if needed

DEFINE_PATCH_FUNCTION read_or_set_scriptname INT_VAR replace=0 STR_VAR name=~~ RET scriptname deathvar BEGIN
  PATCH_IF (~%SOURCE_EXT%~ STR_EQ ~cre~) BEGIN
    PATCH_IF (~%name%~ STR_EQ ~~) BEGIN  TEXT_SPRINT name  ~%DEST_RES%~  END
    SET offset = 0x280
    PATCH_IF (GAME_IS ~iwd2~) BEGIN  SET offset = 0x394  END
    PATCH_IF (GAME_IS ~iwd how totlm~) BEGIN  SET offset = 0x2e8  END

    READ_ASCII offset deathvar (32) NULL
    SET novar = 0
    PATCH_IF (~%deathvar%~ STR_EQ ~~) BEGIN  SET novar = 1  END
    PATCH_IF (~%deathvar%~ STR_EQ ~None~) BEGIN  SET novar = 1  END

    PATCH_IF (novar>0 OR replace>0) BEGIN    // use "name" var if none or if replace=1
      TEXT_SPRINT deathvar  ~%name%~
    END
    WRITE_EVALUATED_ASCII  offset ~%deathvar%~ #32
    TEXT_SPRINT scriptname  ~%deathvar%~
  END

  // if not a cre file
  ELSE BEGIN
    TEXT_SPRINT deathvar  ~~
    TEXT_SPRINT scriptname  ~~
    PATCH_PRINT ~LPF read_or_set_scriptname: not a CRE file~
  END
END

////////////////////////////////////////////////////////////

//read or set area script (all IE games)
//does not change current script if it has one already
//if "script" variable not set, it automatically uses the ARE resource
//if "replace" is set to 1, it overwrites existing script

DEFINE_PATCH_FUNCTION read_or_set_areascript INT_VAR replace=0 STR_VAR script=~~ RET areascript script BEGIN
  PATCH_IF (~%SOURCE_EXT%~ STR_EQ ~are~) BEGIN
    PATCH_IF (~%script%~ STR_EQ ~~) BEGIN  TEXT_SPRINT script  ~%DEST_RES%~  END
    SET offset = 0x94
    PATCH_IF (GAME_IS ~iwd2~) BEGIN  SET offset = 0xa4  END

    READ_ASCII offset bcs (8) NULL
    SET noscript = 0
    PATCH_IF (~%bcs%~ STR_EQ ~~) BEGIN  SET noscript = 1  END
    PATCH_IF (~%bcs%~ STR_EQ ~None~) BEGIN  SET noscript = 1  END

    PATCH_IF (noscript=0 && replace<1) BEGIN    // use current script if it has one, unless replace=1
      TEXT_SPRINT script  ~%bcs%~
    END
    WRITE_EVALUATED_ASCII  offset ~%script%~ #8
    TEXT_SPRINT areascript  ~%script%~
  END

  // if not an ARE
  ELSE BEGIN
    TEXT_SPRINT script  ~~
    TEXT_SPRINT areascript  ~~
    PATCH_PRINT ~LPF read_or_set_areascript: not a ARE file~
  END
END

////////////////////////////////////////////////////////////