//Nurse MT


ACTION_IF (nonursemt = 0) BEGIN


//Remove specialist bonus slots
COPY ~%passives%/fewerspells1.SPL~  ~override/%e4%.SPL~
COPY ~%passives%/opcode206.SPL~     ~override/%e5%.SPL~    // only relevant for 5e casting mod
  LPF ALTER_EFFECT STR_VAR resource=~d5z0slt~ END
  LPF CLONE_EFFECT STR_VAR resource=~D5ZMSLT~ END


//Multiclass import fix
LAF d2_mc_import_fix INT_VAR amount=1  STR_VAR spell=~%s9%~ new_spell=~%#1%~ END
LAF d2_mc_import_fix INT_VAR amount=2  STR_VAR spell=~%s9%~ new_spell=~%#2%~ END
LAF d2_mc_import_fix INT_VAR amount=3  STR_VAR spell=~%s9%~ new_spell=~%#3%~ END
LAF d2_mc_import_fix INT_VAR amount=4  STR_VAR spell=~%s9%~ new_spell=~%#4%~ END
LAF d2_mc_import_fix INT_VAR amount=5  STR_VAR spell=~%s9%~ new_spell=~%#5%~ END
LAF d2_mc_import_fix INT_VAR amount=6  STR_VAR spell=~%s9%~ new_spell=~%#6%~ END
LAF d2_mc_import_fix INT_VAR amount=7  STR_VAR spell=~%s9%~ new_spell=~%#7%~ END
LAF d2_mc_import_fix INT_VAR amount=8  STR_VAR spell=~%s9%~ new_spell=~%#8%~ END
LAF d2_mc_import_fix INT_VAR amount=9  STR_VAR spell=~%s9%~ new_spell=~%#9%~ END


//Array for import fix
ACTION_CLEAR_ARRAY ~import_fix_array~
ACTION_DEFINE_ASSOCIATIVE_ARRAY import_fix_array BEGIN
  ~%#1%~  => 1     // nurse tools
  ~%#2%~  => 6
  ~%#3%~  => 12
  ~%#4%~  => 18
  ~%#5%~  => 24
  ~%#6%~  => 30
  ~%#7%~  => 36
  ~%#8%~  => 42
  ~%#9%~  => 48
END


//Ability tables
COPY ~inlined/d2clab.2DA~  ~override/%nurse%M.2DA~
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e4%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e5%~ END
  LPF tof_denote_spellschool STR_VAR type=~alteration~ END    // tof denote hla
  LPF tof_denote_usewands END

COPY ~inlined/d2clab.2DA~  ~override/%nurse%T.2DA~
  LPF d2clab_artisancheck STR_VAR type=archer END
  LPF d2_kit_ability_adder INT_VAR level=3 passive=1 multi=1 increment=3 STR_VAR resource=~%e1%~ END
//  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e1%~ END
  LPF d2_kit_ability_adder INT_VAR level=6 multi=1 increment=6 STR_VAR resource=~%s9%~ END
  LPF d2_kit_ability_adder STR_VAR resource=~%s9%~ END

  // unlock abilities
  PHP_EACH nursetools_level AS file => level BEGIN
    LPF d2_kit_ability_adder INT_VAR level=level passive=1 STR_VAR resource=~%file%~ END
  END

  // import fix (BG2, ToB, SoD)
  PHP_EACH import_fix_array AS file => level BEGIN
    LPF d2_kit_ability_adder INT_VAR level=level passive=1 STR_VAR resource=~%file%~ END
  END


//
LAF d2_get_mc_text STR_VAR class=thief multi=mt RET baseclass multiclass END
OUTER_SET kit_note = RESOLVE_STR_REF(@353)
OUTER_SET base_kit = RESOLVE_STR_REF(@101603)
LAF d2_build_mc_desc INT_VAR desc1=base_kit desc2=kit_note mode=2 STR_VAR multi=mt RET kit_desc END


//Kit function
LAF ADD_KIT_EX
  INT_VAR
    kit_class = 13
    mixed = RESOLVE_STR_REF (@101601)
    lower = RESOLVE_STR_REF (@101602)
    help = kit_desc
  STR_VAR
    kit_name = ~D2%kitname%MT~
    clab_base_m = ~%nurse%M.2DA~      // -1 spell slots added to Mage table
    clab_base_t = ~%nurse%T.2DA~      // kit abilities added to Thief table
    clsrcreq = ~1 1 1 1 1 1 1~        // Usable by all races, if base class is usable
//    weapprof = ~1 1 2 0 1 0 0 2 0 1 1 0 0 1 1 1 0 1 0 0 0 0 1 2 0 2 2 2 1 1 1 1~
//    weapprof = ~1 1 3 0 1 0 0 3 0 1 1 0 0 1 1 1 0 1 0 0 0 0 1 3 0 3 3 3 1 1 1 1~
    thiefskl = ~40 15~                // start with 40, gain 15 per level
    alignmnt = ~1 1 1 1 1 1 1 1 1~    // all alignments
    clascolr = ~25 28 57 2 24~        // inquisitor colors
  RET 
    kit_id
END


//hla table
OUTER_TEXT_SPRINT ids_name ~D2%kitname%MT~
OUTER_TEXT_SPRINT abbrev   ~%kitid%5~
OUTER_TEXT_SPRINT class    ~MAGE_THIEF~
LAF d2_hla_baseclass STR_VAR kit=~%ids_name%~ class=~%class%~ END
LAF d2_hla_tableres STR_VAR kit=~%ids_name%~ abbrev=~%abbrev%~ END


//armor restriction
OUTER_TEXT_SPRINT ids_name ~D2%kitname%MT~
OUTER_TEXT_SPRINT armor_type ~hide~
OUTER_TEXT_SPRINT $d2_restrict_armor_array (~%ids_name%~) ~%armor_type%~

OUTER_TEXT_SPRINT baseclass ~mt~
OUTER_TEXT_SPRINT $d2_restrict_armor_baseclass (~%ids_name%~) ~%baseclass%~


//Gains archer effects from items
OUTER_TEXT_SPRINT kit_group ~archer~
OUTER_TEXT_SPRINT ids_name  ~D2%kitname%MT~
INCLUDE ~%install%/beamdogitems.tph~


END


/*
*/