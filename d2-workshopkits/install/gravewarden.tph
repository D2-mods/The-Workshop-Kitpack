//Grave Warden (d2wk3)


//Add passive effects
COPY ~%passives%/savebonus4death.SPL~  ~override/%e1%.SPL~
COPY ~%passives%/lorebonus10.SPL~      ~override/%e2%.SPL~


//remove Charm Animal (if given by custom multiclass)
COPY ~%abilities%/applyeffects.SPL~  ~override/%e3%.SPL~
  LPF ALTER_EFFECT STR_VAR resource=~%e3%a~ END
COPY ~%passives%/removespell.SPL~    ~override/%e3%a.SPL~
  LPF ALTER_EFFECT STR_VAR resource=~SPCL311~ END
LAF d2_recast_at_interval STR_VAR spell=~%e3%~ new_spell=~%@3%~ END

COPY ~override/%@3%.SPL~  ~override~
  LPF CLONE_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%e3%~ insert=last END
BUT_ONLY


//Immune to fear
COPY ~%passives%/nopanic.SPL~      ~override/%e4%.SPL~
  LPF cd_apply_batch STR_VAR array_name = cd_fear_removal_arrays END
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_fear_arrays END
  LPF d2_string_immunities END
  LPF ee_fixpack_immunities STR_VAR status=~fear~ END

  // block other fear-related debuffs
  LPF statusarray_function INT_VAR opcode=324 timing=9 STR_VAR status=panic324 mode=protect END
  LPF statusarray_function INT_VAR opcode=318 timing=9 STR_VAR status=panic318 mode=protect END


//Immune to blindness and gaze attacks
COPY ~%passives%/gravewarden.SPL~  ~override/%e5%.SPL~
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_blindness_arrays END
  LPF d2_string_immunities END
  LPF block_death_gaze END

  // block other gaze attacks (ones not using gaze.pro)
  PATCH_FOR_EACH spell IN ~UMBERGAZ~ ~SPIN839~ BEGIN
    LPF d2_protect_on_castequip INT_VAR timing=9 STR_VAR resource=~%spell%~ END
  END

  PATCH_FOR_EACH spell IN ~INSANITY~ ~BEGUILE~ ~SPIN784~ BEGIN
    LPF d2_protect_on_castequip INT_VAR opcode=324 timing=9 STR_VAR resource=~%spell%~ END
  END


//Clone mage spells
LAF d2_convert_spell_type INT_VAR type=2 level=1 STR_VAR spell=~SPWI118~ newspell=~%p1%~ END
LAF d2_convert_spell_type INT_VAR type=2 level=2 STR_VAR spell=~SPWI205~ newspell=~%p2%~ END
LAF d2_convert_spell_type INT_VAR type=2 level=3 STR_VAR spell=~SPWI313~ newspell=~%p3%~ END

ACTION_IF !(FILE_EXISTS ~override/SPWI118.SPL~) BEGIN
  ACTION_IF (GAME_IS ~iwdee~) BEGIN
    COPY_EXISTING ~%p1%.SPL~  ~override~
      SAY UNIDENTIFIED_DESC @40300    // text for IWDEE
    BUT_ONLY
  END
END

ACTION_IF (MOD_IS_INSTALLED ~setup-spell_rev.tp2~ 0) BEGIN
  ACTION_IF (FILE_CONTAINS ~spell_rev/setup-spell_rev.tp2~ ~dv-spell_rev-main~) BEGIN
    COPY_EXISTING ~%p1%.SPL~  ~override~
      SAY UNIDENTIFIED_DESC @40400    // text for spell revisions
    BUT_ONLY
  END
END

LAF d2_evasion_check INT_VAR overwrite=1 STR_VAR spell=~%p3%~ END    // update IWD Evasion (if found)


//add to spell.ids (config option)
ACTION_IF (%nopriestids% = 0) BEGIN
  OUTER_FOR (n = 1; n <= 3; ++n) BEGIN
    LAF ADD_SPELL_EX INT_VAR type=1 level=%n% min=51 max=99 hide=1 STR_VAR file=~override/%p%n%%.SPL~ name=~D2%kitname%_P%n%~ RET spell_res END
    OUTER_TEXT_SPRINT ~p%n%~  ~%spell_res%~
  END

  ACTION_FOR_EACH spell IN ~%p1%~ ~%p2%~ ~%p3%~ BEGIN
    COPY ~override/%spell%.SPL~  ~override~
      LPF update_op324_restrictions END
    BUT_ONLY
  END

  LAF d2_evasion_check INT_VAR overwrite=1 STR_VAR spell=~%p3%~ END    // update new file
END


//Add Denial
COPY ~%icons%/denialicon.BAM~  ~override/%s1%i.BAM~
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@104) STR_VAR bam=~%s1%i~ RET icon END

COPY ~%abilities%/denialglow.BAM~  ~override/%s1%g.BAM~
COPY ~%abilities%/denialglow.VVC~  ~override/%s1%g.VVC~
  WRITE_ASCII 0x8 ~%s1%g~ #8

COPY ~%icons%/denial.BAM~      ~override/%s1%.BAM~
COPY_EXISTING ~SPPR409.SPL~    ~override/%s1%.SPL~        // clone Death Ward
  SAY NAME1 @104
  SAY UNIDENTIFIED_DESC @204
  LPF d2_remove_level_headers END
  LPF d2_convert_spell_type INT_VAR type=0 level=1 END    // convert to special ability
  WRITE_ASCII 0x3a ~%s1%~ #8
  WRITE_ASCII 0x76 ~%s1%~ #8
  LPF ALTER_SPELL_HEADER INT_VAR range=25 speed=3 END
  WRITE_LONG 0x18 THIS | BIT24    // can target invisible

  // remove states (for dispellable buffs)
  SET death_ward = IDS_OF_SYMBOL (~splstate~ ~DEATH_WARD~)
  SET buff_pro = IDS_OF_SYMBOL (~splstate~ ~BUFF_PRO_EFFECTS~)
//  LPF DELETE_EFFECT INT_VAR match_opcode=328 match_parameter2=death_ward END    // remove spell state
  LPF DELETE_EFFECT INT_VAR match_opcode=328 match_parameter2=buff_pro END    // remove spell state
  LPF DELETE_EFFECT INT_VAR match_opcode=282 END    // remove scripting state (for dispellable death wards)

  // block iwdee vorpal
  LPF DELETE_EFFECT STR_VAR match_resource=d2vorpal END    // overwrite if added to death ward already
  LPF DELETE_EFFECT STR_VAR match_resource=vorpal END
  LPF d2_immunity_spl_block STR_VAR block=d2vorpal END
  LPF d2_immunity_spl_block STR_VAR block=vorpal END

  //edit stuff
  LPF DELETE_EFFECT INT_VAR match_opcode=174 match_timing=1 END        // sound moved to VVC
  LPF ALTER_EFFECT INT_VAR power=0 duration=181 resist_dispel=2 END    // edit all effects
  LPF ALTER_EFFECT INT_VAR silent=1 match_opcode=61 duration=0 END     // EEs (silent to not have warning with sr)
  LPF ALTER_EFFECT INT_VAR silent=1 match_opcode=50 duration=1 END     // spell revisions
  LPF CLONE_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END      // not silent to get warning if no icon added

  LPF ALTER_EFFECT INT_VAR silent=1 match_opcode=215 timing=1 parameter2=0 STR_VAR resource=~%s1%g~ END
  LPF ALTER_EFFECT INT_VAR silent=1 match_opcode=174 match_timing=4 STR_VAR resource=~EFF_E03~ END
  LPF block_death_gaze END
  LPF d2_remove_effects_on_cast INT_VAR 326override=1 STR_VAR resource=~%s1%x~ END

  // repeating effect (1 second delay to allow recasting)
  LPF CLONE_EFFECT
    INT_VAR multi_match=1 check_globals=0 opcode=326 timing=4 duration=1
    STR_VAR resource=~%s1%e~ insert=last
  END


//repeating effect (checks HP percent)
COPY ~%abilities%/hpcheck50.SPL~  ~override/%s1%e.SPL~
  LPF ALTER_EFFECT INT_VAR resist_dispel=2 timing=0 duration=180 STR_VAR resource=~%s1%a~ END


//Add Cure subspell (for Denial)
COPY_EXISTING ~%cure_subspell%.SPL~  ~override/%s1%a.SPL~
  LPF d2_remove_level_headers END
  LPF d2_subspell_remove_effects END
  LPF ALTER_EFFECT INT_VAR power=0 resist_dispel=2 END

  // update IWD restrictions
  LPF update_op324_restrictions INT_VAR opcode=318 END
  LPF CLONE_EFFECT INT_VAR silent=1 multi_match=1 match_parameter2=104 parameter1=201 END    // add sword race
  LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=318 opcode=326 STR_VAR resource=~%s1%t~ END

  // edits
  LPF ALTER_EFFECT INT_VAR match_opcode=17 parameter1=100 parameter2=2 END
  LPF d2_remove_effects_on_cast INT_VAR 326override=1 STR_VAR resource=~d2!bleed~ insert=last END    // cures bleeding
  LPF d2_remove_effects_on_cast STR_VAR resource=~%s1%~ insert=last END                              // removes main spell
  LPF d2_remove_effects_on_cast STR_VAR resource=~%s1%e~ insert=last END                             // removes repeating effect

  // text
  SPRINT ability_name @104
  LPF d2_color_string INT_VAR stringref=RESOLVE_STR_REF (@352) STR_VAR color=cloud RET newstring END
  LPF CLONE_EFFECT INT_VAR match_opcode=17 opcode=139 parameter1=newstring END

//IWDEE text (unhealable creatures)
OUTER_SPRINT ability_name @104
COPY ~%abilities%/spellfailed.SPL~  ~override/%s1%t.SPL~
  LPF DELETE_EFFECT INT_VAR match_opcode=174 END
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@350) END    // "Unaffected by..."
  LPF d2_protect_on_castequip INT_VAR duration=180 END


//Remove effects
COPY ~%abilities%/removespelleffects.SPL~  ~override/%s1%x.SPL~
  LPF ALTER_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%s1%~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%s1%e~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%s1%t~ END


//Make clab table
COPY ~inlined/d2clab_ranger_nocharm~  ~override/%kitid%.2DA~
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e1%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 multi=1 increment=1 limit=20 STR_VAR resource=~%e2%~ END
  LPF d2_kit_ability_adder INT_VAR level=5 multi=1 increment=5 STR_VAR resource=~%s1%~ END
  LPF d2_kit_ability_adder STR_VAR resource=~%p1%~ END
  LPF d2_kit_ability_adder STR_VAR resource=~%p2%~ END
  LPF d2_kit_ability_adder STR_VAR resource=~%p3%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 multi=1 increment=1 STR_VAR resource=~%@3%~ END
  LPF d2_kit_ability_adder INT_VAR level=3 passive=1 STR_VAR resource=~%e4%~ END
  LPF d2_kit_ability_adder INT_VAR level=8 passive=1 STR_VAR resource=~%e5%~ END
  LPF d2_kit_ability_adder END
//  LPF tof_denote_usewands END    // ToF: added as feat instead of denoting here


//Kit function
LAF ADD_KIT_EX
  INT_VAR
    kit_class = 12
    mixed = RESOLVE_STR_REF (@100301)
    lower = RESOLVE_STR_REF (@100302)
    help = RESOLVE_STR_REF (@100303)
    fallen_notice = RESOLVE_STR_REF (@100304)
  STR_VAR
    kit_name = ~D2%kitname%~
    clab_path = ~%kitid%~
    clsrcreq = ~1 1 1 1 1 1 1~        // Usable by all races, if base class is usable
    alignmnt = ~1 1 0 1 1 0 0 0 0~    // no evil or chaotic
    clascolr = ~67 25 79 23 24~       // Priest of Lathander colors
  RET 
    kit_id
END


//hla table
OUTER_TEXT_SPRINT ids_name ~D2%kitname%~
OUTER_TEXT_SPRINT abbrev   ~%kitid%~
OUTER_TEXT_SPRINT class    ~ranger~
LAF d2_hla_baseclass STR_VAR kit=~%ids_name%~ class=~%class%~ END
LAF d2_hla_tableres STR_VAR kit=~%ids_name%~ abbrev=~%abbrev%~ END


//armor restriction
OUTER_TEXT_SPRINT ids_name ~D2%kitname%~
OUTER_TEXT_SPRINT armor_type ~chain~
OUTER_TEXT_SPRINT $d2_restrict_armor_array (~%ids_name%~) ~%armor_type%~

OUTER_SET stringref = RESOLVE_STR_REF (@100301)
OUTER_TEXT_SPRINT $d2_restrict_armor_stringref (~%ids_name%~) ~%stringref%~

OUTER_TEXT_SPRINT baseclass ~ranger~
OUTER_TEXT_SPRINT $d2_restrict_armor_baseclass (~%ids_name%~) ~%baseclass%~


//edit ToB starting bag
ACTION_IF (FILE_EXISTS_IN_GAME ~bag25.itm~) BEGIN
  COPY_EXISTING ~25STWEAP.2da~  ~override~
    LPF d2_edit_2da_entry STR_VAR col_name=~D2%kitname%~ row_name=bag entry=bag25 END
    LPF d2_edit_2da_entry STR_VAR col_name=~D2%kitname%~ row_name=MISC4 entry=BOW14 END
    LPF d2_edit_2da_entry STR_VAR col_name=~D2%kitname%~ row_name=MISC5 entry=XBOW09 END
  BUT_ONLY
END


//
//Set variables for external use
OUTER_TEXT_SPRINT gravewarden   ~%kitid%~
OUTER_TEXT_SPRINT denial        ~%s1%~
OUTER_TEXT_SPRINT gravewarden1  ~%p1%~
OUTER_TEXT_SPRINT gravewarden2  ~%p2%~
OUTER_TEXT_SPRINT gravewarden3  ~%p3%~
OUTER_TEXT_SPRINT wardennofear  ~%e4%~     // iconpatch.tph
OUTER_TEXT_SPRINT wardennoblind ~%e5%~     // crosskiteffects.tph
OUTER_TEXT_SPRINT denialheal    ~%s1%a~    // iwdspellpatch.tph

//fnp
OUTER_TEXT_SPRINT fnpclass    ~ranger~
OUTER_TEXT_SPRINT fnpdeity    ~ranger_jungle_spheres~

//tof
OUTER_TEXT_SPRINT tofkitname  ~D2%kitname%~
//OUTER_TEXT_SPRINT tofspheres  ~+protection,+wards,-animal~
OUTER_TEXT_SPRINT tofspheres  ~all,healing,nature,plant,protection,wards~

//dof
OUTER_TEXT_SPRINT dofclab     ~%kitid%~
OUTER_TEXT_SPRINT dofkitname  ~D2%kitname%~


/*
*/