/*
Make base-game abilities unaffected by casting speed penalties (i.e. from Ether Booster)
Some spell-like abilities are skipped (ex. Summon Spirit Animal or Cleric abilities)
*/


ACTION_FOR_EACH ability IN 
    ~SPCL521~    // blade
    ~SPCL522~
    ~SPCL611~    // druid shapeshifts (bg)
    ~SPCL612~
    ~SPCL613~
    ~SPCL643~    // werewolf
    ~SPCL644~
    ~SPCL632~    // avenger
    ~SPCL633~
    ~SPCL634~
    ~SPIN160~    // salamander (breathe fire)
    ~SPCL321~    // berserker
    ~SPCL144~    // kensai
    ~SPCL152~    // barbarian
    ~SPDWD02~    // defender
    ~SPCL811~    // monk
    ~SPCL815~
    ~SPCL820~
    ~SPDM105~    // dark moon
    ~SPCL238~    // sun soul
    ~SPCL236~
    ~SPCL237~
    ~SPCL423~    // poison weapon
    ~SPCL311~    // charm animal
    ~SPCL121~    // called shot
    ~SPCL412~    // set snare
    ~SPCL414~    // bounty hunter
    ~SPDD03~     // dragon disciple
    ~SPCL900~    // fighter hlas
    ~SPCL901~
    ~SPCL902~
    ~SPCL903~
    ~SPCL904~
    ~SPCL905~
    ~SPCL906~
    ~SPCL907~
    ~SPCL908~ 
    ~SPCL909~ 
    ~SPCL910~    // thief hlas
    ~SPCL911~
    ~SPCL912~
    ~SPCL913~
    ~SPCL914~
    ~SPCL916~
    ~SPCL917~
    ~SPCL918~
    ~SPCL919~
    ~SPCL921~    // magic flute
    ~SPCL922~    // tracking (HLA and IWD)
    ~SPCL938~    // shadow form
    ~SPCL940~    // spirit form
    ~#BARD1~     // IWD Bard Songs
    ~#BARD2~
    ~#BARD3~
    ~#BARD4~
    ~#BARD5~
    ~#BARD6~
    ~SPCL107~    // IWD Druid forms
    ~SPCL108~
    ~SPCL101~
    ~SPCL112~
    ~SPCL113~
    ~SPCL114~
    ~SPIN974~    // Psionic Blast (Shapechange)
    ~SPIN829~    // Jan ammo
    ~SPIN117~    // Minsc rage
    ~SPIN116~    // Eldoth arrows
    ~spin119~    // Safana charm
    ~ohhdrain~   // Hexxat drain
    ~ohrrage~    // Wilson rage
    ~SPIN823~    // Slayer change
    ~SPIN852~
    ~SPINHUM~    // Shapeshift: Natural Form
    ~SPCL110~
    ~SPIN122~
    ~SPIN123~
    ~SPIN124~
    ~SPIN123~
    ~SPIN150~
    ~SPIN151~
    ~SPPR617A~
    ~SPWI489~
    ~SPWI490~
    ~SPWI491~
    ~SPIN529~    // End Slayer Change
    ~SPIN667~
    ~SPIN718~
    ~SPMDSLAY~
BEGIN

  COPY_EXISTING ~%ability%.SPL~  ~override~
    LPF DELETE_EFFECT INT_VAR check_headers=0 match_opcode=189 match_timing=0 END
    PATCH_IF (nocastingspeedpatch = 0) BEGIN
      READ_SHORT 0x84 speed
      SET duration = speed + 1
      PATCH_IF (duration > 6) BEGIN SET duration = 6 END
      LPF ADD_SPELL_CFEFFECT
        INT_VAR opcode=189 target=1 parameter1=speed parameter2=1 resist_dispel=2 duration=duration
      END
    END
  IF_EXISTS BUT_ONLY

END


/*
*/