/*
patches for RR, Shohy's mod, Bardic Wonders, refinements, ToF
runs from main component and updater

RR#BDF03 - gives RR#BDF04
RR#BDF04 - HLA song (lasts 1 round)
RR#BDF05 - gives RR#BDF06
RR#BDF06 - HLA song (lasts 3 rounds)

LI#EN01 - gives LI#ENH1
LI#ENH1 - HLA song (lasts 1 round)
TG#LI01 - gives TG#LIN1
TG#LIN1 - HLA song (lasts 2 rounds)

BI#B1SS - gives BI#B1SA
BI#B1SA - shohy song switcher
BI#B1SB - effects
*/


//give song switching abilities
DEFINE_ACTION_FUNCTION pkeg_hlaedits STR_VAR hla=~~ subspell=~~ block=~~ BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~%hla%.SPL~) BEGIN
    OUTER_SPRINT insert  ~first~
    ACTION_IF (~%subspell%~ STR_EQ ~d2BIB1S~) BEGIN  OUTER_SPRINT insert  ~last~  END

    // edit hla
    COPY_EXISTING ~%hla%.SPL~  ~override~
      LPF d2_remove_effects_on_cast INT_VAR 326override=1 STR_VAR resource=~%subspell%~ insert=~%insert%~ END
    BUT_ONLY

    // unlock key spell
    COPY ~override/%pkegstartsong%.SPL~  ~override~
      LPF d2_protect_on_castequip INT_VAR timing=9 STR_VAR resource=~%block%~ END
    BUT_ONLY
  END
END

LAF pkeg_hlaedits STR_VAR hla=~SPCL920~ subspell=~%s5%a~ block=~%s5%x~ END
LAF pkeg_hlaedits STR_VAR hla=~RR#BDF03~ subspell=~%s6%a~ block=~%s6%x~ END
LAF pkeg_hlaedits STR_VAR hla=~RR#BDF05~ subspell=~%s7%a~ block=~%s7%x~ END
LAF pkeg_hlaedits STR_VAR hla=~LI#EN01~ subspell=~%s6%a~ block=~%s6%x~ END
LAF pkeg_hlaedits STR_VAR hla=~TG#LI01~ subspell=~%s7%a~ block=~%s7%x~ END
LAF pkeg_hlaedits STR_VAR hla=~BI#B1SS~ subspell=~d2BIB1S~ block=~%s6%x~ END


//text for RR or Refinements HLAs
OUTER_SPRINT name @120
OUTER_SPRINT desc ~~
OUTER_SPRINT lingering ~~
OUTER_SPRINT hla1  ~%s5%~
OUTER_SPRINT hla2  ~%s5%~

OUTER_SPRINT hlatable  ~lu%kitid%~
ACTION_IF !(FILE_EXISTS_IN_GAME ~%hlatable%.2da~) BEGIN
  LAF d2_get_2da_entry STR_VAR 2da=luabbr col_name=abbrev row_name=bard RET entry END
  OUTER_SPRINT hlatable ~lu%entry%~
END

DEFINE_ACTION_FUNCTION rrsongdesc BEGIN
  // hla song
  COPY_EXISTING ~%s6%.spl~  ~override~
    SAY NAME1 ~%name%~
    SAY UNIDENTIFIED_DESC ~%desc%~
    LPF ALTER_EFFECT INT_VAR match_opcode=251 STR_VAR resource=~%hla1%~ END
  BUT_ONLY

  // lingering song
  COPY_EXISTING ~%s7%.spl~  ~override~
    SAY NAME1 ~%name%~
    SAY UNIDENTIFIED_DESC ~%lingering%~
    LPF ALTER_EFFECT INT_VAR match_opcode=251 STR_VAR resource=~%hla2%~ END
  BUT_ONLY
END

ACTION_IF (RESOURCE_CONTAINS ~%hlatable%.2da~ ~LI#EN01~) BEGIN
  COPY_EXISTING - ~LI#EN01.spl~ ~~  READ_STRREF 0x50 desc  IF_EXISTS
  COPY_EXISTING - ~TG#LI01.spl~ ~~  READ_STRREF 0x50 note  IF_EXISTS
  OUTER_SPRINT lingering  ~%note%%WNL%%WNL%%desc%~
  OUTER_SPRINT hla1  ~LI#ENH1~
  OUTER_SPRINT hla2  ~TG#LIN1~
  LAF rrsongdesc END
END

ACTION_IF (RESOURCE_CONTAINS ~%hlatable%.2da~ ~RR#BDF03~) BEGIN
  OUTER_SPRINT desc @220
  OUTER_SPRINT note @651
  OUTER_SPRINT lingering  ~%desc%%WNL%%WNL%%note%~
  OUTER_SPRINT hla1  ~RR#BDF04~
  OUTER_SPRINT hla2  ~RR#BDF06~
  LAF rrsongdesc END
END

ACTION_IF (RESOURCE_CONTAINS ~%hlatable%.2da~ ~BI#B1SS~) BEGIN
  COPY_EXISTING - ~BI#B1SS.spl~ ~~  READ_STRREF 0x50 desc  IF_EXISTS
  COPY_EXISTING - ~BI#B1SA.spl~ ~~
    READ_STRREF 0x8 name2
    PATCH_IF !(~%name2%~ STR_EQ ~~) BEGIN  SPRINT name ~%name2%~  END
  IF_EXISTS
  OUTER_SPRINT lingering  ~%desc%~
  OUTER_SPRINT hla1  ~BI#B1SB~
  OUTER_SPRINT hla2  ~BI#B1SB~
  LAF rrsongdesc END
END


//
//check for ToF HLAs
OUTER_SET tof_hlasong = 0
ACTION_IF (RESOURCE_CONTAINS ~LUBA0.2DA~ ~AP_DW-HLAVD~) BEGIN    // hlas detected
  ACTION_IF (FILE_EXISTS_IN_GAME ~RR#BDF04.SPL~) BEGIN         // rr hla song
    OUTER_SET tof_hlasong = 1
  END
END

//get name and desc
ACTION_IF (tof_hlasong > 0) BEGIN
  OUTER_SPRINT hlasong_name @120
  OUTER_SPRINT hlasong_desc @220
  OUTER_SPRINT hlasong_res ~RR#BDF04~

  COPY_EXISTING - ~SPCL920.spl~  ~~
    READ_STRREF 0x8 hlasong_name
    READ_STRREF 0x50 hlasong_desc
  IF_EXISTS
  COPY_EXISTING - ~RR#BDF04.spl~  ~~
    READ_STRREF 0x8 name2
    PATCH_IF !(~%name2%~ STR_EQ ~~) BEGIN  SPRINT hlasong_name ~%name2%~  END
  IF_EXISTS
END ELSE BEGIN
  OUTER_SPRINT hlasong_name @119
  OUTER_SPRINT hlasong_desc @219
  OUTER_SPRINT hlasong_res ~SPCL920A~
END


//check Bardic Wonders (extended song tweak)
ACTION_IF (RESOURCE_CONTAINS ~SPCL920A.SPL~ ~c0bardsx~) BEGIN
  OUTER_SPRINT note @650
  OUTER_SPRINT hlasong_desc  ~%hlasong_desc%%WNL%%WNL%%note%~    // add tweak note
END

// patch hla song switcher
COPY_EXISTING ~%enhancedbardsong%.spl~  ~override~
  SAY NAME1 ~%hlasong_name%~
  SAY UNIDENTIFIED_DESC ~%hlasong_desc%~
  LPF ALTER_EFFECT INT_VAR match_opcode=251 STR_VAR resource=~%hlasong_res%~ END
BUT_ONLY

// check song change on HLA
COPY_EXISTING ~SPCL920.spl~  ~override~
  LPF ALTER_EFFECT INT_VAR silent=1 match_opcode=251 STR_VAR resource=~%hlasong_res%~ END
IF_EXISTS BUT_ONLY


//
//add effects to IWD songs if learnable as HLA (ex. ToF mod)
OUTER_SPRINT pkegvar  ~d2pkeg1~
OUTER_SPRINT pkegremove  ~%s0%a~
ACTION_FOR_EACH spell IN
  ~#BARD1~
  ~#BARD2~
  ~#BARD3~
  ~#BARD4~
  ~#BARD5~
  ~#BARD6~
BEGIN
  ACTION_IF (RESOURCE_CONTAINS ~luba2.2da~  ~%spell%~) BEGIN
    COPY_EXISTING ~%spell%.spl~  ~override~
      PATCH_IF !(RESOURCE_CONTAINS ~%SOURCE_FILE%~  ~%pkegvar%~) BEGIN
        LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=251 opcode=309 parameter1=0 parameter2=0 STR_VAR resource=~%pkegvar%~ END
        LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=251 opcode=326 parameter1=0 parameter2=0 STR_VAR resource=~%pkegremove%~ END
      END
    IF_EXISTS BUT_ONLY
  END
END

ACTION_FOR_EACH spell IN
  ~#BARD7~
  ~BI#B1SA~    // shohy HLA song
BEGIN
  COPY_EXISTING ~%spell%.spl~  ~override~
    PATCH_IF !(RESOURCE_CONTAINS ~%SOURCE_FILE%~  ~%pkegvar%~) BEGIN
      LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=251 opcode=309 parameter1=0 parameter2=0 STR_VAR resource=~%pkegvar%~ END
      LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=251 opcode=326 parameter1=0 parameter2=0 STR_VAR resource=~%pkegremove%~ END
    END
  IF_EXISTS BUT_ONLY
END


//shell spell for shohy's mod
ACTION_IF (RESOURCE_CONTAINS ~BI#B1SS.spl~  ~BI#B1SA~) BEGIN
  OUTER_SPRINT spell  ~BI#B1SA~
  OUTER_SPRINT cleanup  ~%s5%t~
  ACTION_IF !(RESOURCE_CONTAINS ~%cleanup%.spl~  ~%spell%~) BEGIN
    COPY_EXISTING ~%cleanup%.spl~  ~override~
      LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=172 STR_VAR resource=~%spell%~ END
    IF_EXISTS BUT_ONLY
  END
END

ACTION_IF !(FILE_EXISTS_IN_GAME ~d2BIB1S.SPL~) BEGIN
  OUTER_SPRINT spell  ~BI#B1SA~
  COPY ~%abilities%/applyeffects.SPL~  ~override/d2BIB1S.SPL~
    LPF ALTER_EFFECT STR_VAR resource=~%s6%a~ END
END


/*
//block during cooldown
COPY_EXISTING ~%pkegdisable%.spl~  ~override~
  LPF d2_protect_on_castequip INT_VAR duration=45 STR_VAR resource=~SPCL920A~ END
  LPF d2_protect_on_castequip INT_VAR duration=45 STR_VAR resource=~RR#BDF04~ END
  LPF d2_protect_on_castequip INT_VAR duration=45 STR_VAR resource=~RR#BDF06~ END
  LPF d2_protect_on_castequip INT_VAR duration=45 STR_VAR resource=~LI#ENH1~ END
  LPF d2_protect_on_castequip INT_VAR duration=45 STR_VAR resource=~TG#LIN1~ END
  LPF d2_protect_on_castequip INT_VAR duration=45 STR_VAR resource=~BI#B1SB~ END
BUT_ONLY
*/


//Set variables for external use
OUTER_TEXT_SPRINT enhancedbardsong   ~%s5%~
OUTER_TEXT_SPRINT rrenhancedsong     ~%s6%~
OUTER_TEXT_SPRINT rrlingeringsong    ~%s7%~
OUTER_TEXT_SPRINT shohyenhancedsong  ~BI#B1SA~


//
//