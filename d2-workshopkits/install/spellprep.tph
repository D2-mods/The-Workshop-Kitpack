/*
prep for subspell effects
misc fixes
*/


//make dummy spell
ACTION_IF !(FILE_EXISTS ~override/d2spell.SPL~) BEGIN
  COPY_EXISTING ~SPCL142.SPL~  ~override/d2spell.SPL~
    LPF d2_subspell_remove_effects END
    LPF ALTER_EFFECT INT_VAR opcode=0 parameter1=0 parameter2=0 timing=0 duration=0 END
    WRITE_LONG 0x18 THIS | BIT14
    WRITE_LONG 0x18 THIS | BIT25
  IF_EXISTS
END

//dummy spell, but can trigger wild magic
COPY_EXISTING ~d2spell.SPL~  ~override/d2spell2.SPL~
  WRITE_LONG 0x18 (THIS BAND BNOT BIT14)
IF_EXISTS


//mage kit spell slots
COPY ~%passives%/fewerspells1.SPL~  ~override/d2sltm1.SPL~
COPY ~%passives%/opcode206.SPL~     ~override/d2slt5e.SPL~    // for 5e casting mod (block effects)
  LPF ALTER_EFFECT STR_VAR resource=~d5z0slt~ END             // -1 slots for specialists (max 5 with 5e casting)
  LPF CLONE_EFFECT STR_VAR resource=~D5ZMSLT~ END             // +1 slots for normal mage (max 6 with 5e casting)
OUTER_TEXT_SPRINT baseslots ~d2sltm1~
OUTER_TEXT_SPRINT 5ecasting ~d2slt5e~


//
//haste effects (as subspell)

/*after using function:
alter durations of op16 and op142
set op318s to new resource, or delete if unneeded

NOTE: SCS/ToF tweak bumps haste effect to a subspell (SPWI305A.SPL)
*/

DEFINE_PATCH_FUNCTION d2prep_haste BEGIN
  LPF d2_remove_level_headers END
  LPF d2_subspell_remove_effects END
  LPF DELETE_EFFECT INT_VAR match_opcode=176 END    // revert some sr changes
  LPF DELETE_EFFECT INT_VAR match_opcode=1 END
  LPF DELETE_EFFECT INT_VAR match_opcode=206 END
  LPF DELETE_EFFECT INT_VAR match_timing=4 END      // remove fatigue
  LPF DELETE_EFFECT INT_VAR match_opcode=16 END                                    // delete opcode 16 (haste)
  LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=142 opcode=16 parameter2=0 END    // add opcode 16
  LPF ALTER_EFFECT INT_VAR silent=1 match_opcode=324 opcode=318 END                // change any op324 to op318 (no message)
END


//
//set variable for cure subspell
OUTER_TEXT_SPRINT cure_subspell ~SPPR502~                // set to main spell
ACTION_IF (FILE_EXISTS ~override/SPPR502B.spl~) BEGIN    // scan subspell for heal opcode
  COPY_EXISTING - ~SPPR502B.spl~  ~~
    GET_OFFSET_ARRAY spl_headers  SPL_V10_HEADERS
    PHP_EACH spl_headers AS num => header_off BEGIN      // array of headers
      GET_OFFSET_ARRAY2 spl_effects header_off SPL_V10_HEAD_EFFECTS
      PHP_EACH spl_effects AS num => effect_off BEGIN    // array of effects on each header
        READ_SHORT effect_off opcode
        PATCH_IF (opcode = 17) BEGIN
          TEXT_SPRINT cure_subspell ~SPPR502B~           // set to subspell (if heal opcode detected)
        END
      END
    END
  // end inlined COPY
END



/*
patches for sleep effects
effects cloned from an existing op39 on the SPL/ITM
*/

//show display string for sleep resist (elf, half-elf)
//string is "Unaffected by effects from sleep"
DEFINE_PATCH_FUNCTION d2_sleep_elfresist INT_VAR overwrite=1 string=1 opcode=318 BEGIN

  PATCH_IF (opcode!=318 AND opcode!=324) BEGIN SET opcode=318 END
  PATCH_IF opcode = 324 BEGIN SET string = 0 END

  PATCH_IF (overwrite > 0) BEGIN
    LPF DELETE_EFFECT INT_VAR match_parameter2=15 match_probability1=90 END
    LPF DELETE_EFFECT INT_VAR match_parameter2=19 match_probability1=30 END
  END

  LPF d2_protect_on_castequip INT_VAR match_opcode=39 opcode=opcode cre_type=15 probability=90 duration=0 STR_VAR insert=above END
  LPF d2_protect_on_castequip INT_VAR match_opcode=39 opcode=opcode cre_type=19 probability=30 duration=0 STR_VAR insert=above END

  PATCH_IF (string > 0) BEGIN
    LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=318 match_parameter2=15 opcode=326 STR_VAR resource=d2sleept END
    LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=318 match_parameter2=19 opcode=326 STR_VAR resource=d2sleept END
  END

END

//
//make d2sleept file (for above function)
COPY_EXISTING - ~SPWI116.spl~  ~~  READ_LONG 0x8 sleep_name
ACTION_GET_STRREF EVAL ~%sleep_name%~  sleep_string
OUTER_TEXT_SPRINT ability_name ~%sleep_string%~
ACTION_TO_LOWER ability_name    // "sleep"

COPY ~%abilities%/spellfailed.SPL~  ~override/d2sleept.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@350) END    // "Unaffected by..."
  LPF DELETE_EFFECT INT_VAR match_opcode=141 END
  LPF DELETE_EFFECT INT_VAR match_opcode=174 END
  LPF d2_protect_on_castequip INT_VAR duration=5 END    // block string for 5 seconds (won't see every hit)

//
//show "Sleep" string
DEFINE_PATCH_FUNCTION d2_sleep_string INT_VAR overwrite=1 BEGIN

  PATCH_IF (GAME_IS ~bgee~)      BEGIN SET sleep_string = 26371 SET unconscious = 20438 END
  PATCH_IF (GAME_IS ~bg2ee eet~) BEGIN SET sleep_string = 14001 SET unconscious = 20438 END
  PATCH_IF (GAME_IS ~iwdee~)     BEGIN SET sleep_string = 37613 SET unconscious = 37340 END

  PATCH_IF (overwrite > 0) BEGIN
    LPF DELETE_EFFECT INT_VAR match_opcode=139 match_parameter1=sleep_string END
    LPF DELETE_EFFECT INT_VAR match_opcode=139 match_parameter1=unconscious END
  END

  LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=39 opcode=139 parameter1=sleep_string timing=1 duration=0 STR_VAR insert=below END

/*
LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=39 opcode=267 parameter1=unconscious timing=0 duration=1 savingthrow=0 special=0 STR_VAR insert=first END

attempt to block "Unconscious" string in BGEE games with op267 (doesn't work)
*/

END


//
//