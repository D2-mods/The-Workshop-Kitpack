//Drifter (d2wkc)


//Add combat bonuses
COPY ~%passives%/acbonus1.SPL~         ~override/%e1%.SPL~
COPY ~%passives%/hitdamage1.SPL~       ~override/%e2%.SPL~
COPY ~%passives%/savebonus4death.SPL~  ~override/%e3%.SPL~    // Save vs. Breath
  LPF ALTER_EFFECT INT_VAR opcode=36 END
COPY ~%passives%/movebonus2.SPL~       ~override/%e4%.SPL~
COPY ~%passives%/disablebutton.SPL~    ~override/%e5%.SPL~
  LPF ALTER_EFFECT INT_VAR parameter2=2 END
COPY ~%passives%/nocasting.SPL~        ~override/%e8%.SPL~
COPY ~%passives%/fewerspells1.SPL~     ~override/%e9%.SPL~
  LPF ALTER_EFFECT INT_VAR opcode=62 parameter2=127 END

//block 5e-casting stuff (aesthetic fix)
COPY ~%passives%/opcode206.SPL~  ~override/%m1%.SPL~
  LPF ALTER_EFFECT STR_VAR resource=~D5ZZ000~ END
  LPF CLONE_EFFECT INT_VAR opcode=172 timing=1 STR_VAR resource=~d5zprp~ END


//
ACTION_IF ((IS_AN_INT buildeffects) AND (buildeffects = 1)) BEGIN
  OUTER_SPRINT spellitemkit @101201
  PRINT @13
  ACTION_IF ((IS_AN_INT silentmode) AND (silentmode > 0)) BEGIN SILENT END
END

//functions
DEFINE_PATCH_FUNCTION drifter206 BEGIN
  PHP_EACH d2riftgate AS spell => type BEGIN
    PATCH_IF !(~%type%~ STRING_CONTAINS_REGEXP ~206~) BEGIN
      LPF ALTER_EFFECT INT_VAR silent=1 opcode=206 parameter1=0 STR_VAR match_resource=~%spell%~ END
    END
  END
END

DEFINE_PATCH_FUNCTION drifter_entangle_check BEGIN
  PHP_EACH d2statusarray AS spell => type BEGIN
    PATCH_IF (~%type%~ STR_EQ ~entangle~) BEGIN
      PATCH_IF !(FILE_EXISTS_IN_GAME ~%spell%.itm~) BEGIN
        LPF d2_protect_on_castequip INT_VAR timing=9 STR_VAR resource=~%spell%~ END
      END ELSE BEGIN
        LPF DELETE_EFFECT STR_VAR match_resource=~%spell%~ END
        LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=206 parameter1=0 parameter2=0 STR_VAR resource=~%spell%~ END
      END
    END
  END
END

DEFINE_ACTION_FUNCTION drifter_hit_effects STR_VAR block = ~entangle ground206~ BEGIN
  ACTION_PHP_EACH d2riftgate AS spell => type BEGIN
    ACTION_IF !(~%block%~ STRING_CONTAINS_REGEXP ~%type%~) BEGIN
      COPY_EXISTING ~%spell%.itm~  ~override~
        LPF drifter_hit_patches END
      IF_EXISTS BUT_ONLY
    END
  END
END

DEFINE_PATCH_FUNCTION drifter_hit_patches BEGIN
  LPF d2_protect_on_castequip INT_VAR globals=0 headers=1 header_type=1 cre_type=109 cre_special=IDS_OF_SYMBOL (kit ~D2%kitname%~) duration=0 overwrite=1 STR_VAR insert=first END
  LPF d2_protect_on_castequip INT_VAR globals=0 headers=1 header_type=2 cre_type=109 cre_special=IDS_OF_SYMBOL (kit ~D2%kitname%~) duration=0 overwrite=0 STR_VAR insert=first END
END


//
//check spell.ids
COPY_EXISTING - ~spell.ids~ ~~
  PHP_EACH ~ids_terrain_immunities~ AS spell_to_add => style BEGIN
    COUNT_REGEXP_INSTANCES ~[^_]%spell_to_add%[^_]~ spell_is_listed
    PATCH_IF (spell_is_listed > 0) BEGIN
      LPF RES_NUM_OF_SPELL_NAME STR_VAR spell_name=~%spell_to_add%~ RET spell_res END
      TEXT_SPRINT $d2riftgate(~%spell_res%~) ~ground~    // ~%spell_res%~ => ground
    END
  END

  PHP_EACH ~ids_cloud_immunities~ AS spell_to_add => style BEGIN
    COUNT_REGEXP_INSTANCES ~[^_]%spell_to_add%[^_]~ spell_is_listed
    PATCH_IF (spell_is_listed > 0) BEGIN
      LPF RES_NUM_OF_SPELL_NAME STR_VAR spell_name=~%spell_to_add%~ RET spell_res END
      TEXT_SPRINT $d2riftgate(~%spell_res%~) ~cloud~    // ~%spell_res%~ => cloud
    END
  END
// end inlined COPY


//
//Check spell resource (stuff not in spell.ids)
ACTION_PHP_EACH ~drifter_not_spellids~ AS spell_to_add => style BEGIN

  ACTION_IF (~%style%~ STR_EQ ~ground~) BEGIN
    OUTER_TEXT_SPRINT $d2riftgate(~%spell_to_add%~) ~ground~    // ~%spell_to_add%~ => ground
  END
  ACTION_IF (~%style%~ STR_EQ ~ground206~) BEGIN
    OUTER_TEXT_SPRINT $d2riftgate(~%spell_to_add%~) ~ground206~    // ~%spell_to_add%~ => ground206
  END
  ACTION_IF (~%style%~ STR_EQ ~cloud~) BEGIN
    OUTER_TEXT_SPRINT $d2riftgate(~%spell_to_add%~) ~cloud~    // ~%spell_to_add%~ => cloud
  END
  ACTION_IF (~%style%~ STR_EQ ~cloud206~) BEGIN
    OUTER_TEXT_SPRINT $d2riftgate(~%spell_to_add%~) ~cloud206~    // ~%spell_to_add%~ => cloud206
  END

END


//
//match spell name as string ref (catch clones of spells)
ACTION_IF (GAME_IS ~bgee~)       BEGIN OUTER_TEXT_SPRINT ee_game ~bgee~ END
ACTION_IF (GAME_IS ~bg2ee eet~)  BEGIN OUTER_TEXT_SPRINT ee_game ~bg2ee~ END
ACTION_IF (GAME_IS ~iwdee~)      BEGIN OUTER_TEXT_SPRINT ee_game ~iwdee~ END

ACTION_BASH_FOR ~override~ ~.*\.SPL~ BEGIN
  ACTION_IF !(FILE_IS_IN_COMPRESSED_BIFF ~%BASH_FOR_FILE%~) BEGIN    // only check mod resource
    COPY - ~override/%BASH_FOR_FILE%~  ~~
      PATCH_IF (SOURCE_SIZE > 0x99) BEGIN    // skip if no headers
        READ_LONG 0x8 stringref

        PHP_EACH ~drifter_%ee_game%_strings~ AS string => style BEGIN    
          PATCH_IF (string = stringref) BEGIN
            PATCH_IF (~%style%~ STR_EQ ~ground~) BEGIN
              TEXT_SPRINT $d2riftgate(~%BASH_FOR_RES%~) ~ground~    // ~%BASH_FOR_RES%~ => ground
            END
            PATCH_IF (~%style%~ STR_EQ ~cloud~) BEGIN
              TEXT_SPRINT $d2riftgate(~%BASH_FOR_RES%~) ~cloud~    // ~%BASH_FOR_RES%~ => cloud
            END
          END
        END
      END
    // end inlined COPY
  END
END


//
//add SPLs for immunities to ground spells
COPY ~%passives%/drifter.SPL~  ~override/%e6%.SPL~
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_entangle_arrays END
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_grease_arrays END
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_web_arrays END
  LPF d2_string_immunities END
  LPF ee_fixpack_immunities STR_VAR status=~web entangle~ END
  LPF riftgate_immunity STR_VAR block=~ground quake ground206~ END
  LPF DELETE_EFFECT INT_VAR match_opcode=206 END
  LPF drifter_entangle_check END
  LPF drifter206 END

//add SPL for immunity to cloud spells
COPY ~%passives%/opcode206.SPL~  ~override/%e7%.SPL~
  LPF riftgate_immunity STR_VAR block=~cloud insect cloud206~ END
  LPF DELETE_EFFECT INT_VAR match_opcode=206 END
  LPF drifter206 END


//Make clab table
COPY ~inlined/d2clab_ranger~  ~override/%kitid%.2DA~
  LPF 2da_find_row_delete STR_VAR row_name = ~SPHERES~ END
  LPF 2da_find_row_delete STR_VAR row_name = ~SPELL_INIT~ END
  LPF d2_kit_ability_adder INT_VAR level=5 passive=1 multi=1 increment=5 STR_VAR resource=~%e1%~ END
  LPF d2_kit_ability_adder INT_VAR level=3 passive=1 multi=1 increment=5 STR_VAR resource=~%e2%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e1%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e3%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e4%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e5%~ END
  LPF d2_kit_ability_adder INT_VAR level=6 passive=1 STR_VAR resource=~%e6%~ END
  LPF d2_kit_ability_adder INT_VAR level=12 passive=1 STR_VAR resource=~%e7%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e8%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 multi=1 increment=1 limit=20 STR_VAR resource=~%e9%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%m1%~ END
  LPF d2_kit_ability_adder END
  LPF d2clab_artisancheck END
  LPF d2clab_evasion INT_VAR always=1 END

  // add a few extra -1 spell slots
  FOR (n = 1; n < 5; ++n) BEGIN
    LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e9%~ END
  END


//Kit function
LAF ADD_KIT_EX
  INT_VAR
    kit_class = 12
    mixed = RESOLVE_STR_REF (@101201)
    lower = RESOLVE_STR_REF (@101202)
    help = RESOLVE_STR_REF (@101203)
    fallen_notice = RESOLVE_STR_REF (@101204)
  STR_VAR
    kit_name = ~D2%kitname%~
//  unusable = ~0x00008000~           // Archer usability (using different method)
    clab_path = ~%kitid%~
    clsrcreq = ~1 1 1 1 1 1 1~        // Usable by all races, if base class is usable
    alignmnt = ~0 0 0 1 1 1 1 1 1~    // non-lawful
    clascolr = ~24 99 144 2 93~       // bounty hunter colors
  RET 
    kit_id
END


//patch weapons (block kit for on-hit effects)
LAF drifter_hit_effects END

/*test
COPY_EXISTING ~SW1H58.ITM~ override
  LPF d2_protect_on_castequip INT_VAR globals=0 headers=1 header_type=1 cre_type=109 cre_special=IDS_OF_SYMBOL (kit ~D2%kitname%~) duration=0 STR_VAR insert=first END
IF_EXISTS BUT_ONLY
*/


//hla table
OUTER_TEXT_SPRINT ids_name ~D2%kitname%~
OUTER_TEXT_SPRINT abbrev   ~%kitid%~
OUTER_TEXT_SPRINT class    ~ranger~
LAF d2_hla_baseclass STR_VAR kit=~%ids_name%~ class=~%class%~ END
LAF d2_hla_tableres STR_VAR kit=~%ids_name%~ abbrev=~%abbrev%~ END


//armor restriction
OUTER_TEXT_SPRINT ids_name ~D2%kitname%~
OUTER_TEXT_SPRINT armor_type ~hide~
OUTER_TEXT_SPRINT $d2_restrict_armor_array (~%ids_name%~) ~%armor_type%~

OUTER_SET stringref = RESOLVE_STR_REF (@101201)
OUTER_TEXT_SPRINT $d2_restrict_armor_stringref (~%ids_name%~) ~%stringref%~

OUTER_TEXT_SPRINT baseclass ~ranger~
OUTER_TEXT_SPRINT $d2_restrict_armor_baseclass (~%ids_name%~) ~%baseclass%~


//Gains swashbuckler/blade effects from items
OUTER_TEXT_SPRINT kit_group ~blade~
OUTER_TEXT_SPRINT ids_name  ~D2%kitname%~
INCLUDE ~%install%/beamdogitems.tph~


//Set variables for external use
OUTER_TEXT_SPRINT drifter             ~%kitid%~
OUTER_TEXT_SPRINT drifter_immunities  ~%e6%~
OUTER_TEXT_SPRINT cloud_immunities    ~%e7%~


/*
*/