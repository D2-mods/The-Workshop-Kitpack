/*
prep class clabs
copy inlined files to new kit clab resource
*/


//blank clab file
<<<<<<<< .../temp/d2clab
2DA         V1.0
****
            1           2           3           4           5           6           7           8           9           10          11          12          13          14          15          16          17          18          19          20          21          22          23          24          25          26          27          28          29          30          31          32          33          34          35          36          37          38          39          40          41          42          43          44          45          46          47          48          49          50
>>>>>>>>
COPY - ~.../temp/d2clab~  ~inlined/d2clab.2DA~


//
DEFINE_PATCH_FUNCTION clabprep_spellids INT_VAR passive=0 STR_VAR spellids=d2spell newtext=~****~ BEGIN
  PATCH_IF (passive > 0) BEGIN
    TEXT_SPRINT prefix ~AP_~
  END ELSE BEGIN
    TEXT_SPRINT prefix ~GA_~
  END

  PATCH_IF (~%newtext%~ STR_EQ ~****~) BEGIN
    //
  END ELSE BEGIN
    TEXT_SPRINT newtext ~%prefix%%newtext%~
  END

  PATCH_IF (RESOURCE_CONTAINS ~spell.ids~ ~[^_]%spellids%[^_]~) BEGIN
    LPF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = ~%spellids%~ RET spell_res END
    REPLACE_TEXTUALLY EXACT_MATCH ~%prefix%%spell_res%~  ~%newtext%~
  END
END



/*
some things removed are from multiclass kit mods (ex. QD_MULTI)
these usually don't matter, but it looks cleaner without them
*/

//fighter
COPY_EXISTING - ~CLABFI01.2DA~  ~inlined/d2clab_fighter~
  LPF 2da_find_row_delete STR_VAR row_name = ~QD_MULTI~ END
  REPLACE_TEXTUALLY ~AP_C0AAZX~  ~****~

//ranger
COPY_EXISTING - ~CLABRN01.2DA~  ~inlined/d2clab_ranger~
  LPF 2da_find_row_delete STR_VAR row_name = ~QD_MULTI~ END
  LPF 2da_find_row_delete STR_VAR row_name = ~RAN_SPLS~ END
COPY - ~inlined/d2clab_ranger~   ~inlined/d2clab_ranger_nocharm~
  REPLACE_TEXTUALLY ~GA_SPCL311~  ~****~    // remove charm animal

//paladin
COPY_EXISTING - ~CLABPA01.2DA~  ~inlined/d2clab_paladin~
COPY_EXISTING - ~CLABPA01.2DA~  ~inlined/d2clab_paladin_noheal~
  REPLACE_TEXTUALLY ~GA_SPCL211~  ~****~    // lay on hands
  REPLACE_TEXTUALLY ~GA_SPCL111~  ~****~    // cure disease (IWD)
  REPLACE_TEXTUALLY ~GA_cdiplcd~  ~****~    // cure disease (IWDification)
  REPLACE_TEXTUALLY ~GA_C0PAL05~  ~****~    // cure disease (Artisan's)
  LPF clabprep_spellids STR_VAR spellids=PALADIN_CURE_DISEASE END
COPY - ~inlined/d2clab_paladin_noheal~  ~inlined/d2clab_paladin_noabilities~
  REPLACE_TEXTUALLY ~GA_SPCL213~  ~****~    // protection from evil (BG2)
  REPLACE_TEXTUALLY ~GA_SPCL212~  ~****~    // protection from evil (IWD), detect evil (BG2)
  REPLACE_TEXTUALLY ~GA_SPCL109~  ~****~    // detect evil (IWD)
  REPLACE_TEXTUALLY ~GA_SPCL122~  ~****~    // smite evil (IWD)
  REPLACE_TEXTUALLY ~GA_cdiplse~  ~****~    // smite evil (IWDification)
  REPLACE_TEXTUALLY ~GA_C0PAL02~  ~****~    // smite evil (Artisan's)
  LPF clabprep_spellids STR_VAR spellids=PALADIN_SMITE_EVIL END

//LPF 2da_find_row_delete STR_VAR row_name = ~PALADINS~ END    // remove FnP Archetypes



//mage, sorcerer
ACTION_IF (FILE_EXISTS_IN_GAME ~CLABMA01.2DA~) BEGIN
  COPY_EXISTING - ~CLABMA01.2DA~  ~inlined/d2clab_mage~    // clone Mage 2da
END 
ELSE BEGIN
  COPY - ~inlined/d2clab.2DA~     ~inlined/d2clab_mage~    // or use blank table
END

COPY - ~inlined/d2clab_mage~  ~inlined/d2clab_mage~
  LPF 2da_find_row_delete STR_VAR row_name = ~QD_MULTI~ END
  LPF 2da_find_row_delete STR_VAR row_name = ~SWITCH~ END
  REPLACE_TEXTUALLY ~AP_A7_CS05~  ~****~
  REPLACE_TEXTUALLY ~AP_A7_CS06~  ~****~
  REPLACE_TEXTUALLY ~AP_D5YMANA~  ~****~

COPY - ~inlined/d2clab_mage~  ~inlined/d2clab_sorcerer~
  REPLACE_TEXTUALLY ~AP_D5ZZ000~  ~****~
  REPLACE_TEXTUALLY ~AP_D5Z0SPL~  ~****~
  REPLACE_TEXTUALLY ~AP_D5ZMSLT~  ~****~
  REPLACE_TEXTUALLY ~AP_D5XCRS1~  ~****~



//thief
COPY_EXISTING - ~CLABTH01.2DA~  ~inlined/d2clab_thief~
  LPF 2da_find_row_delete STR_VAR row_name = ~QD_MULTI~ END
COPY - ~inlined/d2clab_thief~   ~inlined/d2clab_thief_notraps~
  REPLACE_TEXTUALLY ~GA_SPCL412~  ~****~    // remove traps
  REPLACE_TEXTUALLY ~AP_D5CL412~  ~****~    // remove traps (subtledoctor)

//bard
COPY_EXISTING - ~CLABBA01.2DA~  ~inlined/d2clab_bard~
COPY_EXISTING - ~CLABBA01.2DA~  ~inlined/d2clab_bard_nosongs~
  REPLACE_TEXTUALLY ~AP_FJBARD~   ~****~    // remove BG song
  REPLACE_TEXTUALLY ~AP_#BARD-1~  ~****~    // remove IWD songs (also for IWDification)
  REPLACE_TEXTUALLY ~GA_#BARD1~   ~****~
  REPLACE_TEXTUALLY ~GA_#BARD2~   ~****~
  REPLACE_TEXTUALLY ~GA_#BARD3~   ~****~
  REPLACE_TEXTUALLY ~GA_#BARD4~   ~****~
  REPLACE_TEXTUALLY ~GA_#BARD5~   ~****~
  REPLACE_TEXTUALLY ~GA_#BARD6~   ~****~
//mod stuff
  REPLACE_TEXTUALLY ~AP_RR#BDF01~ ~****~    // rr bard song
  REPLACE_TEXTUALLY ~AP_BI#B10A~  ~****~    // Shohy bard song mod
  REPLACE_TEXTUALLY ~GA_BI#B11A~  ~****~
  REPLACE_TEXTUALLY ~GA_BI#B12A~  ~****~
  REPLACE_TEXTUALLY ~GA_BI#B13A~  ~****~
  REPLACE_TEXTUALLY ~GA_BI#B14A~  ~****~
  REPLACE_TEXTUALLY ~GA_BI#B15A~  ~****~
  REPLACE_TEXTUALLY ~GA_BI#B16A~  ~****~

//blade
COPY_EXISTING - ~CLABBA02.2DA~  ~inlined/d2clab_blade~
COPY_EXISTING - ~CLABBA02.2DA~  ~inlined/d2clab_blade_nosongs~
  REPLACE_TEXTUALLY ~AP_FJBLADE~  ~****~    // BG games
  REPLACE_TEXTUALLY ~AP_#BARD-1~  ~****~    // IWDEE
//mod stuff
  REPLACE_TEXTUALLY ~AP_RR#BBL01~ ~****~          // rr song
  REPLACE_TEXTUALLY ~AP_C0BLD01~  ~AP_d2c0bld~    // Bardic Wonders song
  REPLACE_TEXTUALLY ~AP_BI#B20A~  ~AP_d2BIbld~    // Shohy song switching
  REPLACE_TEXTUALLY ~GA_BI#B11A~  ~****~
  REPLACE_TEXTUALLY ~GA_BI#B12A~  ~****~
  REPLACE_TEXTUALLY ~GA_BI#B13A~  ~****~
  REPLACE_TEXTUALLY ~GA_BI#B14A~  ~****~

//Bardic Wonders - Blade passives
COPY_EXISTING ~c0bld01.spl~  ~override/d2c0bld.SPL~    // remove song change
  LPF DELETE_EFFECT INT_VAR match_opcode=251 END
IF_EXISTS

//Shohy song switching - Blade passives
COPY_EXISTING ~BI#B20A.spl~  ~override/d2BIbld.SPL~    // remove song change
  LPF DELETE_EFFECT INT_VAR match_opcode=251 END
  LPF DELETE_EFFECT INT_VAR match_opcode=146 STR_VAR match_resource=~BI#B00D~ END    // remove choir abilities
  LPF ALTER_EFFECT INT_VAR silent=1 match_opcode=54 opcode=278 END
IF_EXISTS



//monk
COPY_EXISTING - ~CLABMO01.2DA~  ~inlined/d2clab_monk~
COPY_EXISTING - ~CLABMO01.2DA~  ~inlined/d2clab_monk_noheal~
  REPLACE_TEXTUALLY ~GA_SPCL815~ ~****~    // lay on hands
  REPLACE_TEXTUALLY ~GA_QDMNKA2~ ~****~    // wholeness of body (monastic orders pre-0.42)
COPY_EXISTING - ~CLABMO01.2DA~  ~inlined/d2clab_monk_nostun~
  REPLACE_TEXTUALLY ~GA_SPCL811~ ~****~    // stunning blow
  REPLACE_TEXTUALLY ~GA_QDMNKA1~ ~****~    // stunning blows (monastic orders pre-0.42)
COPY - ~inlined/d2clab_monk_noheal~  ~inlined/d2clab_monk_nohealstun~
  REPLACE_TEXTUALLY ~GA_SPCL811~ ~****~    // stunning blow
  REPLACE_TEXTUALLY ~GA_QDMNKA1~ ~****~    // stunning blows (monastic orders pre-0.42)
COPY - ~inlined/d2clab_monk_nostun~  ~inlined/d2clab_monk_noquivstun~
  REPLACE_TEXTUALLY ~GA_SPCL820~ ~****~    // quivering palm 
  REPLACE_TEXTUALLY ~GA_QDMNKA4~ ~****~    // quivering palm  (monastic orders pre-0.42)
COPY - ~inlined/d2clab_monk_nohealstun~  ~inlined/d2clab_monk_noabilities~
  REPLACE_TEXTUALLY ~GA_SPCL820~ ~****~    // quivering palm 
  REPLACE_TEXTUALLY ~GA_QDMNKA4~ ~****~    // quivering palm  (monastic orders pre-0.42)

//cleric
COPY_EXISTING - ~CLABPR01.2DA~  ~inlined/d2clab_cleric~
  LPF 2da_find_row_delete STR_VAR row_name = ~QD_MULTI~ END
  LPF 2da_find_row_delete STR_VAR row_name = ~CLER_SPLS~ END
COPY - ~inlined/d2clab_cleric~  ~inlined/d2clab_cleric_nosymbol~
  REPLACE_TEXTUALLY ~AP_CDHLYSYM~  ~****~

//shaman (CLABSH01)
COPY_EXISTING - ~CLABSH01.2DA~  ~inlined/d2clab_shaman~  IF_EXISTS



//druid (CLABDR01)
COPY_EXISTING - ~CLABDR01.2DA~  ~inlined/d2clab_druid~
  LPF 2da_find_row_delete STR_VAR row_name = ~QD_MULTI~ END
  LPF 2da_find_row_delete STR_VAR row_name = ~DRU_SPLS~ END

COPY - ~inlined/d2clab_druid~   ~inlined/d2clab_druid_noanimal~
  LPF 2da_find_row_delete STR_VAR row_name = ~WILDSHAPE~ END
  PATCH_FOR_EACH shapeshift IN
    ~SPCL611~ ~SPCL612~ ~SPCL613~    // bg animals
    ~SPCL107~ ~SPCL108~ ~SPCL101~    // iwd animals
    ~cdidrww~ ~cdidrbb~ ~cdidrpb~
    ~C0WSHAP~ ~DW-DSBA~
  BEGIN
    REPLACE_TEXTUALLY ~GA_%shapeshift%~  ~****~
  END

COPY - ~inlined/d2clab_druid_noanimal~   ~inlined/d2clab_druid_noshape~
  PATCH_FOR_EACH shapeshift IN
    ~SPCL112~ ~SPCL113~ ~SPCL114~    // iwd elementals
    ~cdidrfe~ ~cdidree~ ~cdidrwe~ ~C0WSHA3~
  BEGIN
    REPLACE_TEXTUALLY ~GA_%shapeshift%~  ~****~
  END



//
//variables, functions

//paladin cure disease
OUTER_SET paladin_cd = 0
ACTION_IF (RESOURCE_CONTAINS ~spell.ids~ ~PALADIN_CURE_DISEASE~) BEGIN OUTER_SET paladin_cd = 1 END
ACTION_IF (RESOURCE_CONTAINS ~CLABPA01.2DA~ ~GA_C0PAL05~) BEGIN OUTER_SET paladin_cd = 1 END


// add or remove artisan stuff
DEFINE_PATCH_FUNCTION d2clab_artisancheck STR_VAR type=default mode=add BEGIN

  PATCH_IF (RESOURCE_CONTAINS ~CLABFI01.2DA~  ~C0ARC03~) BEGIN
    TEXT_SPRINT powerattack ~C0FIG01~
    TEXT_SPRINT expertise ~C0FIG02~
    TEXT_SPRINT rapidshot ~C0ARC03~

    TEXT_SPRINT res ~%powerattack% %expertise% %rapidshot%~
    PATCH_IF (~%type%~ STR_EQ ~melee~)   BEGIN TEXT_SPRINT res ~%powerattack% %expertise%~ END
    PATCH_IF (~%type%~ STR_EQ ~ranged~)  BEGIN TEXT_SPRINT res ~%powerattack% %rapidshot%~ END
    PATCH_IF (~%type%~ STR_EQ ~archer~)  BEGIN TEXT_SPRINT res ~%rapidshot%~ END
    PATCH_IF (~%type%~ STR_EQ ~power~)   BEGIN TEXT_SPRINT res ~%powerattack%~ END
    PATCH_IF (~%type%~ STR_EQ ~finesse~) BEGIN TEXT_SPRINT res ~%expertise% %rapidshot%~ END

    REPLACE_TEXTUALLY ~GA_%powerattack%~  ~****~
    REPLACE_TEXTUALLY ~GA_%expertise%~  ~****~
    REPLACE_TEXTUALLY ~GA_%rapidshot%~  ~****~
    PHP_EACH d2clab_artisancheck AS spell => level BEGIN
      PATCH_IF !(~%res%~ STRING_CONTAINS_REGEXP ~%spell%~) BEGIN
        PATCH_IF (~%mode%~ STR_EQ ~add~) BEGIN
          LPF d2_kit_ability_adder INT_VAR level=level STR_VAR resource=~%spell%~ END
        END
      END
    END

  END
END

ACTION_DEFINE_ASSOCIATIVE_ARRAY d2clab_artisancheck BEGIN
  C0FIG01 => 5
  C0FIG02 => 5
  C0ARC03 => 6
END


//tnb innate familiars
DEFINE_PATCH_FUNCTION tnb_innate_familiars BEGIN
  PATCH_IF !(RESOURCE_CONTAINS ~%SOURCE_RES%.2DA~  ~AP_D5FMINN~) BEGIN
    LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~D5FMINN~ END
  END
END


//check Blade passives (RR, Shohy, Artisan)
DEFINE_PATCH_FUNCTION blade_passives BEGIN
  REPLACE_TEXTUALLY ~AP_d2c0bld~  ~****~
  REPLACE_TEXTUALLY ~AP_d2BIbld~  ~****~
  REPLACE_TEXTUALLY ~GA_RR#WDIS~  ~****~
  REPLACE_TEXTUALLY ~AP_RR#BBLIT~  ~****~
  PATCH_IF (RESOURCE_CONTAINS ~CLABBA02.2da~  ~AP_C0BLD01~) BEGIN
    LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=d2c0bld END
  END
  PATCH_IF (RESOURCE_CONTAINS ~CLABBA02.2da~  ~AP_BI#B20A~) BEGIN
    LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=d2BIbld END
  END
  PATCH_IF (RESOURCE_CONTAINS ~CLABBA02.2da~  ~GA_RR#WDIS~) BEGIN
    PATCH_FOR_EACH level IN ~1~ ~10~ ~20~ ~30~ ~40~ BEGIN
      LPF d2_kit_ability_adder INT_VAR level=level STR_VAR resource=~RR#WDIS~ END
    END
  END
  PATCH_IF (RESOURCE_CONTAINS ~CLABBA02.2da~  ~AP_RR#BBLIT~) BEGIN
    LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~RR#BBLIT~ END
  END
  PRETTY_PRINT_2DA
END


//dummy files (so updater won't create new files)
COPY_EXISTING - ~SPCL142.SPL~  ~inlined/dummy.SPL~
  LPF ALTER_EFFECT INT_VAR opcode=0 parameter1=0 parameter2=0 timing=0 duration=0 END

ACTION_FOR_EACH spell IN
  ~d2c0bld~ ~d2BIbld~
BEGIN
  ACTION_IF !(FILE_EXISTS ~override/%spell%.SPL~) BEGIN
    COPY ~inlined/dummy.SPL~  ~override/%spell%.SPL~
  END
END


//
//