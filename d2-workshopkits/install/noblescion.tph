//Noble Scion (d2wke)


//add passives
COPY ~%passives%/hitdamage1ranged.SPL~  ~override/%e1%.SPL~
  LPF DELETE_EFFECT INT_VAR match_opcode=286 END
COPY ~%passives%/critbonus1.SPL~        ~override/%e2%.SPL~
  LPF ALTER_EFFECT INT_VAR special=2 END


//Blood Bullet array
ACTION_DEFINE_ASSOCIATIVE_ARRAY bloodbullets BEGIN
  1 => a
  2 => b
  3 => c
  4 => d
  5 => e
  6 => f     // level 18
  7 => g
  8 => h
  9 => i
  10 => j    // level 30
  11 => k
  12 => l
  13 => m    // level 39
  14 => n
  15 => o
  16 => p
END


//add Blood Bullet
COPY ~%icons%/bloodbulleticon.BAM~   ~override/%s1%i.BAM~
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@125) STR_VAR bam=~%s1%i~ RET icon END

COPY ~%icons%/bloodbullet.BAM~       ~override/%s1%.BAM~
COPY ~%abilities%/bloodbullets.SPL~  ~override/%s1%.SPL~
  SAY NAME1 @125
  SAY UNIDENTIFIED_DESC @225
  WRITE_ASCII 0x3a ~%s1%~ #8
  WRITE_ASCII 0x76 ~%s1%~ #8
  LPF ALTER_EFFECT INT_VAR multi_match=1 match_opcode=326 STR_VAR resource=~%s1%a~ END    // damage bonus
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=326 STR_VAR resource=~%s1%b~ END    // THAC0 bonus
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=326 STR_VAR resource=~%s0%x~ END    // remove effects
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END
  LPF ALTER_EFFECT INT_VAR match_opcode=12 STR_VAR parameter1=5 dicenumber=0 dicesize=0 END    // self-damage = 5
  LPF d2_mindamageboost INT_VAR amount=2 duration=12 END

//damage bonus
COPY ~%abilities%/bloodbulletsbuff.SPL~  ~override/%s1%a.SPL~
  LPF CD_EXTEND-O-MATIC
    INT_VAR
      base_dur=12
      step_dur=0
      step_size=3
      level_cap=50
      min_lev_alt=3
  END

  FOR (n = 1; n <= 16; ++n) BEGIN
    SET header = n - 1
    PHP_EACH bloodbullets AS damage => res BEGIN
      PATCH_IF (damage = n) BEGIN
        LPF ALTER_EFFECT INT_VAR header=header STR_VAR resource=~%s9%%res%~ END
        INNER_ACTION BEGIN
          COPY ~%abilities%/bloodbulletspiercing.EFF~  ~override/%s9%%res%.EFF~
            WRITE_LONG 0x38 damage
        END
      END
    END
  END

  PATCH_IF (spellcap < 50) BEGIN
    LPF CD_TRIM-O-MATIC INT_VAR level_cap = spellcap END    // cap damage at "spellcap" setting (config.ini)
  END


//THAC0 bonus
COPY ~override/%e1%.SPL~  ~override/%s1%b.SPL~
  LPF ALTER_EFFECT INT_VAR timing=0 duration=12 END
  LPF CD_EXTEND-O-MATIC INT_VAR base_dur=12 step_dur=0 step_size=4 level_cap=5 END
  LPF CD_EXTEND-O-MATIC INT_VAR base_dur=12 step_dur=0 step_size=5 level_cap=50 min_lev_alt=5 END
  FOR (n = 1; n <= 10; ++n) BEGIN
    LPF ALTER_EFFECT INT_VAR header=n parameter1=(n + 1) END
  END


//remove effects
COPY ~%abilities%/removespelleffects.SPL~  ~override/%s0%x.SPL~
  LPF ALTER_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%s1%~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%s1%a~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%s1%b~ END


//Make clab table
COPY ~inlined/d2clab_paladin~  ~override/%kitid%.2DA~
  LPF d2clab_artisancheck STR_VAR type=archer END
  LPF d2_kit_ability_adder INT_VAR level=5 passive=1 multi=1 increment=5 STR_VAR resource=~%e1%~ END
  LPF d2_kit_ability_adder INT_VAR level=8 passive=1 multi=1 increment=8 limit=16 STR_VAR resource=~%e2%~ END
  LPF d2_kit_ability_adder INT_VAR level=3 multi=1 increment=3 STR_VAR resource=~%s1%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e1%~ END
  LPF d2_kit_ability_adder END


//Kit function
LAF ADD_KIT_EX
  INT_VAR
    kit_class = 6
    mixed = RESOLVE_STR_REF (@101401)
    lower = RESOLVE_STR_REF (@101402)
    help = RESOLVE_STR_REF (@101403)
    fallen_notice = RESOLVE_STR_REF (@101404)
  STR_VAR
    kit_name = ~D2%kitname%~
//  unusable = ~0x40000000~           // Barbarian usability - restricts some Paladin items
    clab_path = ~%kitid%~
    clsrcreq = ~1 1 1 1 1 1 1~        // Usable by all races, if base class is usable
//    weapprof = ~1 1 5 1 1 1 1 5 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 5 5 5 5 5 1 1 1 1~
    alignmnt = ~1 1 0 1 0 0 0 0 0~    // LG, LN, NG
    clascolr = ~67 25 70 94 43~       // dragon disciple colors
  RET 
    kit_id
END


//hla table
OUTER_TEXT_SPRINT ids_name ~D2%kitname%~
OUTER_TEXT_SPRINT abbrev   ~%kitid%~
OUTER_TEXT_SPRINT class    ~paladin~
LAF d2_hla_baseclass STR_VAR kit=~%ids_name%~ class=~%class%~ END
LAF d2_hla_tableres STR_VAR kit=~%ids_name%~ abbrev=~%abbrev%~ END


//armor restriction
OUTER_TEXT_SPRINT ids_name ~D2%kitname%~
OUTER_TEXT_SPRINT armor_type ~splint~
OUTER_TEXT_SPRINT $d2_restrict_armor_array (~%ids_name%~) ~%armor_type%~

OUTER_SET stringref = RESOLVE_STR_REF (@101401)
OUTER_TEXT_SPRINT $d2_restrict_armor_stringref (~%ids_name%~) ~%stringref%~

OUTER_TEXT_SPRINT baseclass ~paladin~
OUTER_TEXT_SPRINT $d2_restrict_armor_baseclass (~%ids_name%~) ~%baseclass%~


//Gains archer effects from items
OUTER_TEXT_SPRINT kit_group ~archer~
OUTER_TEXT_SPRINT ids_name  ~D2%kitname%~
INCLUDE ~%install%/beamdogitems.tph~


//Set variables for external use
OUTER_TEXT_SPRINT noblescion     ~%kitid%~
OUTER_TEXT_SPRINT bloodbullet    ~%s1%~

//fnp
OUTER_TEXT_SPRINT fnpclass       ~champion~
OUTER_TEXT_SPRINT fnpdeity       ~corellon_spheres~
OUTER_TEXT_SPRINT fnparchetype1  ~SetGlobal("D5PADEF","GLOBAL",1)~
OUTER_TEXT_SPRINT fnparchetype2  ~SetGlobal("D5PAINQ","GLOBAL",1)~
OUTER_TEXT_SPRINT fnparchetype3  ~SetGlobal("D5PAZEA","GLOBAL",1)~

//tof
OUTER_TEXT_SPRINT tofkitname  ~D2%kitname%~
//OUTER_TEXT_SPRINT tofspheres  ~+sun~
OUTER_TEXT_SPRINT tofspheres  ~all,combat,divine,good,healing,protection,sun~

//dof
OUTER_TEXT_SPRINT dofclab     ~%kitid%~
OUTER_TEXT_SPRINT dofkitname  ~D2%kitname%~


/*
*/