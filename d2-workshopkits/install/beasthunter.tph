//Beast Hunter (d2wk1)


//Add passive effects
OUTER_SET demonic = IDS_OF_SYMBOL (race ~demonic~)
OUTER_SET lycanthrope = IDS_OF_SYMBOL (race ~lycanthrope~)

COPY ~%passives%/damagevstype.SPL~  ~override/%e1%.SPL~
  LPF ALTER_EFFECT INT_VAR parameter2=4 special=3 END
  LPF ALTER_EFFECT INT_VAR parameter1=demonic END        // +3 vs. demonic
  LPF CLONE_EFFECT INT_VAR parameter1=lycanthrope END    // +3 vs. lycanthropes

COPY ~%passives%/acbonus1.SPL~  ~override/%e2%.SPL~
  LPF ALTER_EFFECT INT_VAR opcode=219 parameter1=demonic parameter2=4 END
  LPF CLONE_EFFECT INT_VAR parameter1=lycanthrope END


//Add Hunter Tools (previously Fire Weapon)
COPY ~%icons%/fireweapon.BAM~      ~override/%s1%i.BAM~
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@100) STR_VAR bam=~%s1%i~ RET icon END

COPY ~%icons%/huntertools.BAM~     ~override/%s1%.BAM~
COPY ~%abilities%/fireweapon.SPL~  ~override/%s1%.SPL~
  SAY NAME1 @100
  SAY UNIDENTIFIED_DESC @200
  WRITE_ASCII 0x3a ~%s1%~ #8
  WRITE_ASCII 0x76 ~%s1%~ #8
  LPF ALTER_EFFECT INT_VAR match_duration=60 duration=72 END    // duration = 12 rounds
  LPF ALTER_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%s1%~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=248 STR_VAR resource=~%s1%~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END
  LPF CLONE_EFFECT INT_VAR match_opcode=142 opcode=22 parameter1=1 parameter2=0 END     // +1 luck rolls
  LPF CLONE_EFFECT INT_VAR match_opcode=142 opcode=325 parameter1=1 parameter2=0 END    // +1 Saving Throws
//LPF CLONE_EFFECT INT_VAR match_opcode=142 opcode=126 parameter1=3 parameter2=0 END    // +3 movement
//LPF CLONE_EFFECT INT_VAR match_opcode=142 opcode=190 parameter1=3 parameter2=0 END    // +3 Speed Factor

COPY ~%abilities%/fireweapon.EFF~  ~override/%s1%.EFF~
  WRITE_SHORT 0x22 1    // v3.0: change to acid damage


//Add Quicksilver
COPY ~%icons%/quicksilvericon.BAM~    ~override/%s2%i.BAM~
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@101) STR_VAR bam=~%s2%i~ RET icon END

COPY ~%icons%/quicksilver.BAM~        ~override/%s2%.BAM~
COPY ~%abilities%/quicksilver.SPL~    ~override/%s2%.SPL~
  SAY NAME1 @101
  SAY UNIDENTIFIED_DESC @201
  WRITE_ASCII 0x3a ~%s2%~ #8
  WRITE_ASCII 0x76 ~%s2%~ #8
  LPF ALTER_EFFECT INT_VAR match_duration=60 duration=36 END    // duration = 6 rounds
  LPF ALTER_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%s2%~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=249 STR_VAR resource=~%s2%a~ END
  LPF ALTER_EFFECT INT_VAR multi_match=1 match_opcode=177 STR_VAR resource=~%s2%b~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=177 STR_VAR resource=~%s2%c~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=177 STR_VAR resource=~%s2%d~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=177 STR_VAR resource=~%s2%e~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=177 STR_VAR resource=~%s2%f~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=177 STR_VAR resource=~%s2%g~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=177 STR_VAR resource=~%s2%h~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=177 STR_VAR resource=~%s2%i~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=177 STR_VAR resource=~%s2%j~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END

  //level 16 effect ()
  LPF CD_EXTEND-O-MATIC INT_VAR dur_special=1 step_dur=0 step_size=15 END
  LPF ALTER_EFFECT INT_VAR header=1 match_opcode=301 parameter1=2 END

//damage effs
COPY ~%abilities%/quicksilverpiercing.EFF~  ~override/%s2%a.EFF~
COPY ~%abilities%/quicksilverarrows.EFF~    ~override/%s2%b.EFF~
COPY ~%abilities%/quicksilverarrows2.EFF~   ~override/%s2%c.EFF~
COPY ~%abilities%/quicksilverarrows3.EFF~   ~override/%s2%d.EFF~
COPY ~%abilities%/quicksilverbolts.EFF~     ~override/%s2%e.EFF~
COPY ~%abilities%/quicksilverbolts2.EFF~    ~override/%s2%f.EFF~
COPY ~%abilities%/quicksilverbolts3.EFF~    ~override/%s2%g.EFF~
COPY ~%abilities%/quicksilverbullets.EFF~   ~override/%s2%h.EFF~
COPY ~%abilities%/quicksilverbullets2.EFF~  ~override/%s2%i.EFF~
COPY ~%abilities%/quicksilverbullets3.EFF~  ~override/%s2%j.EFF~


//Make clab table
COPY ~inlined/d2clab_fighter~  ~override/%kitid%.2DA~
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e1%~ END
  LPF d2_kit_ability_adder INT_VAR level=2 multi=1 increment=4 STR_VAR resource=~%s1%~ END
  LPF d2_kit_ability_adder INT_VAR level=5 multi=1 increment=4 limit=13 STR_VAR resource=~%s2%~ END
  LPF d2_kit_ability_adder INT_VAR level=8 passive=1 STR_VAR resource=~%e2%~ END
  LPF d2_kit_ability_adder END


//Kit function
LAF ADD_KIT_EX
  INT_VAR
    kit_class = 2
    mixed = RESOLVE_STR_REF (@100101)
    lower = RESOLVE_STR_REF (@100102)
    help = RESOLVE_STR_REF (@100103)
  STR_VAR
    kit_name = ~D2%kitname%~
//  unusable = ~0x00010000~        // Stalker usability (using different method)
    clab_path = ~%kitid%~
    clsrcreq = ~1 1 1 1 1 1 1~     // Usable by all races, if base class is usable
    clascolr = ~28 93 91 91 93~    // Archer colors
  RET 
    kit_id
END


//hla table
OUTER_TEXT_SPRINT ids_name ~D2%kitname%~
OUTER_TEXT_SPRINT abbrev   ~%kitid%~
OUTER_TEXT_SPRINT class    ~fighter~
LAF d2_hla_baseclass STR_VAR kit=~%ids_name%~ class=~%class%~ END
LAF d2_hla_tableres STR_VAR kit=~%ids_name%~ abbrev=~%abbrev%~ END
//LAF d2_clone_hla_table STR_VAR kitids=~%ids_name%~ copy=~%class%~ abbrev=~%abbrev%~ RET hlatable END


//armor restriction
OUTER_TEXT_SPRINT ids_name ~D2%kitname%~
OUTER_TEXT_SPRINT armor_type ~studded~
OUTER_TEXT_SPRINT $d2_restrict_armor_array (~%ids_name%~) ~%armor_type%~

OUTER_SET stringref = RESOLVE_STR_REF (@100101)
OUTER_TEXT_SPRINT $d2_restrict_armor_stringref (~%ids_name%~) ~%stringref%~

OUTER_TEXT_SPRINT baseclass ~fighter~
OUTER_TEXT_SPRINT $d2_restrict_armor_baseclass (~%ids_name%~) ~%baseclass%~


//Set variables for external use
OUTER_TEXT_SPRINT beasthunter       ~%kitid%~
OUTER_TEXT_SPRINT fireweapon        ~%s1%~
OUTER_TEXT_SPRINT quicksilver       ~%s2%~
OUTER_TEXT_SPRINT holysymbolhunter  ~%m9%~


/*
*/