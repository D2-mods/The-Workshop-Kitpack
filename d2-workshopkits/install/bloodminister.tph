//Blood Minister (d2wk9)


//Add passive effects
COPY ~%passives%/noitemtype.SPL~     ~override/%e1%.SPL~
  LPF ALTER_EFFECT INT_VAR opcode=181 special=RESOLVE_STR_REF (@300) END    // "Cannot use item."

COPY ~%passives%/bloodminister.SPL~  ~override/%e2%.SPL~
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_level_drain_arrays END
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_poison_arrays END
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_disease_arrays END
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_poison_resistance_arrays END
  LPF d2_string_immunities END
  LPF CLONE_EFFECT INT_VAR match_opcode=101 match_parameter2=216 opcode=282 parameter1=1 parameter2=2 END    // add scripting state (immune to level drain)


//add icon BAMs
COPY ~%icons%/bloodtransfusion.BAM~   ~override/%s1%.BAM~
COPY ~%icons%/bloodtypea.BAM~         ~override/%s2%.BAM~
COPY ~%icons%/bloodtypeb.BAM~         ~override/%s3%.BAM~
COPY ~%icons%/bloodtypec.BAM~         ~override/%s4%.BAM~
COPY ~%icons%/bloodtyped.BAM~         ~override/%s5%.BAM~
COPY ~%icons%/bloodtypeportrait.BAM~  ~override/%s1%i.BAM~


//
//add Blood Transfusion
COPY ~%abilities%/bloodtransfusion.SPL~  ~override/%s1%.SPL~
  SAY NAME1 @112
  SAY UNIDENTIFIED_DESC @212
  LPF ALTER_EFFECT INT_VAR header=0 match_opcode=214 STR_VAR resource=~%s1%a~ END    // set 2da list to use
  LPF ALTER_EFFECT INT_VAR header=1 match_opcode=214 STR_VAR resource=~%s1%b~ END
  LPF ALTER_EFFECT INT_VAR header=2 match_opcode=214 STR_VAR resource=~%s1%c~ END
  LPF ALTER_EFFECT INT_VAR header=3 match_opcode=214 STR_VAR resource=~%s1%d~ END
  WRITE_ASCII 0x3a ~%s1%~ #8    // icon BAMs
  LPF ALTER_SPELL_HEADER STR_VAR icon=~%s1%~ END

COPY ~%abilities%/bloodtransfusion.2DA~  ~override/%s1%a.2DA~    // make 2da lists
	APPEND ~%s1%a.2DA~  ~healing_blood   %s2% 3~
COPY_EXISTING ~%s1%a.2DA~    ~override/%s1%b.2DA~
	APPEND ~%s1%b.2DA~  ~beast_blood     %s3% 3~
COPY_EXISTING ~%s1%b.2DA~    ~override/%s1%c.2DA~
	APPEND ~%s1%c.2DA~  ~madmans_blood   %s4% 3~
COPY_EXISTING ~%s1%c.2DA~    ~override/%s1%d.2DA~
	APPEND ~%s1%d.2DA~  ~lead_elixir     %s5% 3~


//prep for blood types
COPY - ~%abilities%/applyeffects.SPL~  ~inlined/d2bloodtype.SPL~
  WRITE_ASCII 0x10 CAS_P07 #8
  WRITE_SHORT 0x1c 4    // innate (no "casts" in string message)
  WRITE_LONG  0x34 0    // level 0 (submenu looks better with Bubb's UI)
  LPF ALTER_SPELL_HEADER INT_VAR speed=1 END
  LPF ALTER_EFFECT INT_VAR parameter2=49 END    // affects allies
  LPF CLONE_EFFECT INT_VAR parameter2=50 END    // affects non-allies
  LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=324 timing=0 parameter2=55 END    // undead and golems immune
  LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=324 timing=0 parameter2=9 END     // elementals immune
  LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=324 timing=0 parameter1=119 parameter2=104 END    // slimes immune

COPY ~%abilities%/removespelleffects.SPL~  ~override/%s0%a.SPL~    // cast by blood types (remove effects)
  LPF ALTER_EFFECT INT_VAR target=2 STR_VAR resource=~%s2%a~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%s3%a~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%s4%a~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%s5%a~ END
COPY ~override/%s0%a.SPL~  ~override/%s0%x.SPL~                    // cast by Restoration (can be blocked separately)

COPY ~%abilities%/spellfailed.SPL~  ~override/%s0%b.SPL~
  LPF d2_color_string INT_VAR stringref=RESOLVE_STR_REF (@320) STR_VAR color=blue RET newstring END
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=newstring END    // "Ministration fail..."
  LPF DELETE_EFFECT INT_VAR match_opcode=174 END


//
//add Healing Blood
COPY ~inlined/d2bloodtype.SPL~  ~override/%s2%.SPL~
  SAY NAME1 @603
  SAY UNIDENTIFIED_DESC @212
  WRITE_ASCII 0x3a ~%s2%~ #8    // icon BAMs
  LPF ALTER_SPELL_HEADER STR_VAR icon=~%s2%~ END

  LPF ALTER_EFFECT INT_VAR match_parameter2=49 STR_VAR resource=~%s2%a~ END
  LPF CLONE_EFFECT INT_VAR match_parameter2=49 STR_VAR resource=~%s0%a~ END
  LPF ALTER_EFFECT INT_VAR match_parameter2=50 STR_VAR resource=~%s0%b~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=324 STR_VAR resource=~%s2%~ END

LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@603) STR_VAR bam=~%s1%i~ RET icon END
COPY ~%abilities%/bloodtypea.SPL~  ~override/%s2%a.SPL~
//  LPF ALTER_EFFECT INT_VAR multi_match=1 match_opcode=324 STR_VAR resource=~%s2%a~ END
  LPF DELETE_EFFECT INT_VAR match_opcode=324 END
  LPF DELETE_EFFECT INT_VAR match_opcode=321 END
  LPF DELETE_EFFECT INT_VAR match_opcode=146 END
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@306) END        // "Regenerating"
  LPF CLONE_EFFECT INT_VAR match_opcode=98 opcode=17 parameter1=15 parameter2=2 timing=1 duration=0 END    // heal 15% hp
  LPF CLONE_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END
  LPF d2_remove_effects_on_cast INT_VAR 326override=1 STR_VAR resource=~d2!body~ END     // cures poison and disease
  LPF d2_remove_effects_on_cast INT_VAR 326override=1 STR_VAR resource=~d2!bleed~ END    // cures bleeding
  LPF d2_remove_effects_on_cast INT_VAR 326override=1 STR_VAR resource=~d2!phase~ END    // cures acid dot, phasing, etc.


//
//add Beast Blood
COPY ~inlined/d2bloodtype.SPL~  ~override/%s3%.SPL~
  SAY NAME1 @604
  SAY UNIDENTIFIED_DESC @212
  WRITE_ASCII 0x3a ~%s3%~ #8    // icon BAMs
  LPF ALTER_SPELL_HEADER STR_VAR icon=~%s3%~ END

  LPF ALTER_EFFECT INT_VAR match_parameter2=49 STR_VAR resource=~%s3%a~ END
  LPF CLONE_EFFECT INT_VAR match_parameter2=49 STR_VAR resource=~%s0%a~ END
  LPF ALTER_EFFECT INT_VAR match_parameter2=50 STR_VAR resource=~%s0%b~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=324 STR_VAR resource=~%s3%~ END

LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@604) STR_VAR bam=~%s1%i~ RET icon END
COPY ~%abilities%/bloodtypeb.SPL~  ~override/%s3%a.SPL~
//  LPF ALTER_EFFECT INT_VAR multi_match=1 match_opcode=324 STR_VAR resource=~%s3%a~ END
  LPF DELETE_EFFECT INT_VAR match_opcode=324 END
  LPF DELETE_EFFECT INT_VAR match_opcode=321 END
  LPF ALTER_EFFECT INT_VAR match_opcode=248 STR_VAR resource=~%s3%b~ END    // +1 damage
  LPF CLONE_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END

  LPF cd_apply_batch STR_VAR array_name = cd_immunity_poison_arrays END
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_disease_arrays END
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_poison_resistance_arrays END
  LPF d2_string_immunities END

COPY ~%abilities%/castspellself.EFF~  ~override/%s3%b.EFF~    // +1 damage bonus
  WRITE_ASCII 0x30 ~%s3%b~ #8
COPY ~%abilities%/damageup1.SPL~      ~override/%s3%b.SPL~


//
//add Madman's Blood
COPY ~inlined/d2bloodtype.SPL~  ~override/%s4%.SPL~
  SAY NAME1 @605
  SAY UNIDENTIFIED_DESC @212
  WRITE_ASCII 0x3a ~%s4%~ #8    // icon BAMs
  LPF ALTER_SPELL_HEADER STR_VAR icon=~%s4%~ END

  LPF ALTER_EFFECT INT_VAR match_parameter2=49 STR_VAR resource=~%s4%a~ END
  LPF CLONE_EFFECT INT_VAR match_parameter2=49 STR_VAR resource=~%s0%a~ END
  LPF ALTER_EFFECT INT_VAR match_parameter2=50 STR_VAR resource=~%s0%b~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=324 STR_VAR resource=~%s4%~ END

LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@605) STR_VAR bam=~%s1%i~ RET icon END
COPY ~%abilities%/bloodtypec.SPL~  ~override/%s4%a.SPL~
//  LPF ALTER_EFFECT INT_VAR multi_match=1 match_opcode=324 STR_VAR resource=~%s4%a~ END
  LPF DELETE_EFFECT INT_VAR match_opcode=324 END
  LPF DELETE_EFFECT INT_VAR match_opcode=321 END
  LPF DELETE_EFFECT INT_VAR match_opcode=146 END
  LPF ALTER_EFFECT INT_VAR match_opcode=166 parameter1=5 END    // 5% magic resist
  LPF CLONE_EFFECT INT_VAR match_opcode=19 opcode=6 END         // +4 charisma
  LPF CLONE_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END
  LPF d2_remove_effects_on_cast INT_VAR 326override=1 STR_VAR resource=~d2!vamp~ END    // cures level drain
  LPF d2_protect_on_castequip INT_VAR duration=2 STR_VAR resource=~%s0%x~ insert=first END

/*moved to spellprep3
COPY_EXISTING ~SPPR417.SPL~  ~override/%s4%b.SPL~    // clone Lesser Restoration
  LPF d2_subspell_remove_effects END
  LPF d2_subspell_remove_visuals INT_VAR string=0 END
  LPF d2_no_fatigue END
  LPF DELETE_EFFECT INT_VAR match_opcode=324 END    // remove IWD restrictions
  LPF ALTER_EFFECT INT_VAR resist_dispel=2 END
*/


//
//add Lead Elixir
COPY ~inlined/d2bloodtype.SPL~  ~override/%s5%.SPL~
  SAY NAME1 @606
  SAY UNIDENTIFIED_DESC @212
  WRITE_ASCII 0x3a ~%s5%~ #8    // icon BAMs
  LPF ALTER_SPELL_HEADER STR_VAR icon=~%s5%~ END

  LPF ALTER_EFFECT INT_VAR match_parameter2=49 STR_VAR resource=~%s5%a~ END
  LPF CLONE_EFFECT INT_VAR match_parameter2=49 STR_VAR resource=~%s0%a~ END
  LPF ALTER_EFFECT INT_VAR match_parameter2=50 STR_VAR resource=~%s0%b~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=324 STR_VAR resource=~%s5%~ END

LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@606) STR_VAR bam=~%s1%i~ RET icon END
COPY ~%abilities%/bloodtyped.SPL~  ~override/%s5%a.SPL~
//  LPF ALTER_EFFECT INT_VAR multi_match=1 match_opcode=324 STR_VAR resource=~%s5%a~ END
  LPF DELETE_EFFECT INT_VAR match_opcode=324 END
  LPF DELETE_EFFECT INT_VAR match_opcode=321 END
  LPF ALTER_EFFECT INT_VAR match_parameter1=20 parameter2=0 END    // change to increment
  LPF CLONE_EFFECT INT_VAR match_opcode=86 opcode=27 END
  LPF CLONE_EFFECT INT_VAR match_opcode=86 opcode=28 END
  LPF CLONE_EFFECT INT_VAR match_opcode=86 opcode=29 END
  LPF CLONE_EFFECT INT_VAR match_opcode=86 opcode=30 END
  LPF CLONE_EFFECT INT_VAR match_opcode=86 opcode=31 END
  LPF CLONE_EFFECT INT_VAR match_opcode=86 opcode=84 END
  LPF CLONE_EFFECT INT_VAR match_opcode=86 opcode=85 END
  LPF CLONE_EFFECT INT_VAR match_opcode=86 opcode=173 END
  LPF CLONE_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END

  LPF cd_apply_batch STR_VAR array_name = cd_immunity_level_drain_arrays END
  LPF d2_string_immunities END
  LPF CLONE_EFFECT INT_VAR match_opcode=101 match_parameter2=216 opcode=282 parameter1=1 parameter2=2 END    // add scripting state (immune to level drain)


//
//removed by Restoration or Greater Restoration
ACTION_PHP_EACH d2restoration AS spell => type BEGIN
  ACTION_IF !(~%type%~ STRING_CONTAINS_REGEXP ~restore~) BEGIN    // if "type" DOES contain "restore"
    COPY_EXISTING ~%spell%.spl~  ~override~
      LPF d2_remove_effects_on_cast INT_VAR 326override=1 STR_VAR resource=~%s0%x~ insert=last END
    IF_EXISTS BUT_ONLY
  END
END


//
//add Holy Symbol
COPY ~%files%/holysymbol_core.SPL~    ~override/%m9%.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=122 STR_VAR resource=~%m9%~ END
COPY ~%icons%/holysymbol_blood.BAM~   ~override/%m9%.BAM~
COPY ~%icons%/holysymbol_blood2.BAM~   ~override/%m9%d.BAM~

ACTION_IF (FILE_EXISTS_IN_GAME ~belt12.itm~) BEGIN
  COPY_EXISTING ~belt12.itm~          ~override/%m9%.ITM~    // clone Lathander symbol
    WRITE_LONG 0x1e (THIS BAND BNOT BIT1)                    // remove restrictions
    WRITE_BYTE 0x29 (THIS BAND BNOT BIT6)
END
ELSE BEGIN
  COPY ~%files%/holysymbol_core.ITM~  ~override/%m9%.ITM~    // or add new
END

COPY_EXISTING ~%m9%.ITM~  ~override~
  SAY NAME1 @70100 SAY UNIDENTIFIED_DESC @70200
  SAY NAME2 @70100 SAY DESC @70200
  WRITE_ASCII 0x3a ~%m9%~ #8     // icon
  WRITE_ASCII 0x58 ~%m9%d~ #8    // description image
BUT_ONLY


//Make clab table
COPY ~inlined/d2clab_cleric_nosymbol~  ~override/%kitid%.2DA~
//  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e1%~ END    // no bracers (using below method)
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e2%~ END
  LPF d2_kit_ability_adder INT_VAR level=2 multi=1 increment=2 limit=20 STR_VAR resource=~%s1%~ END
  LPF d2_kit_ability_adder INT_VAR level=25 passive=1 STR_VAR resource=~%m9%~ END
  LPF d2_kit_ability_adder END


//Kit function
LAF ADD_KIT_EX
  INT_VAR
    kit_class = 3
    mixed = RESOLVE_STR_REF (@100901)
    lower = RESOLVE_STR_REF (@100902)
    help = RESOLVE_STR_REF (@100903)
  STR_VAR
    kit_name = ~D2%kitname%~
    clab_path = ~%kitid%~
    clsrcreq = ~1 1 1 1 1 1 1~          // Usable by all races, if base class is usable
    clascolr = ~171 174 190 171 171~    // Necromancer colors
  RET 
    kit_id
END


//hla table
OUTER_TEXT_SPRINT ids_name ~D2%kitname%~
OUTER_TEXT_SPRINT abbrev   ~%kitid%~
OUTER_TEXT_SPRINT class    ~cleric~
LAF d2_hla_baseclass STR_VAR kit=~%ids_name%~ class=~%class%~ END
LAF d2_hla_tableres STR_VAR kit=~%ids_name%~ abbrev=~%abbrev%~ END


//no bracers (new method)
ACTION_IF !(IS_AN_INT noarmorrestrictions) BEGIN OUTER_SET noarmorrestrictions = 0 END

ACTION_IF (noarmorrestrictions = 0) BEGIN
  ACTION_CLEAR_ARRAY ~d2_itemtype_usable~
  OUTER_TEXT_SPRINT $d2_itemtype_usable (~bracers~) ~0~    // type => 0 (unusable)
  OUTER_TEXT_SPRINT $d2_itemtype_usable (~gauntlets~) ~0~
  OUTER_TEXT_SPRINT ids_name  ~D2%kitname%~
  OUTER_TEXT_SPRINT baseclass  ~cleric~
  OUTER_SET stringref = RESOLVE_STR_REF (@100901)    // kit name in item description
  INCLUDE ~%install%/usableitems.tph~
END


//edit ToB starting bag
ACTION_IF (FILE_EXISTS_IN_GAME ~bag27a.itm~) BEGIN
  COPY_EXISTING ~25STWEAP.2da~  ~override~
    LPF d2_edit_2da_entry STR_VAR col_name=~D2%kitname%~ row_name=bag entry=bag27a END
    LPF d2_edit_2da_entry STR_VAR col_name=~D2%kitname%~ row_name=MISC4 entry=BLUN18 END
  BUT_ONLY
END


//
//Higher level healing for FnP
COPY ~%passives%/d5fpclsp.spl~  ~override/%e6%.SPL~
  LPF DELETE_EFFECT INT_VAR multi_match=10 END
  LPF ALTER_EFFECT INT_VAR parameter1=0 parameter2=2 END
BUT_ONLY

COPY_EXISTING ~%kitid%.2DA~  ~override~
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e6%~ END
BUT_ONLY

/*
Added this way instead of using the FnP function because it's too 
much of a pain to re-arrange the way I've already set everything up.
*/


//
//Set variables for external use
OUTER_TEXT_SPRINT bloodminister      ~%kitid%~
OUTER_TEXT_SPRINT bloodtransfusion   ~%s1%~
OUTER_TEXT_SPRINT healingblood       ~%s2%~
OUTER_TEXT_SPRINT healingbloodregen  ~%s2%a~
OUTER_TEXT_SPRINT beastblood         ~%s3%~
OUTER_TEXT_SPRINT madmanblood        ~%s4%~
OUTER_TEXT_SPRINT leadelixir         ~%s5%~
OUTER_TEXT_SPRINT ministernodrain    ~%e2%~    // iconpatch.tph
OUTER_TEXT_SPRINT holysymbolblood    ~%m9%~

//fnp
OUTER_TEXT_SPRINT fnpclass    ~cleric~
OUTER_TEXT_SPRINT fnpdeity    ~druid_lost_spheres~

//tof
OUTER_TEXT_SPRINT tofkitname  ~D2%kitname%~
//OUTER_TEXT_SPRINT tofspheres  ~+plant,+air,+moon,-combat,-good~
OUTER_TEXT_SPRINT tofspheres  ~all,air,charm,divination_cleric,divine,evil,healing,moon,necromantic,plant,protection,summoning,wards~

//dof
OUTER_TEXT_SPRINT dofclab     ~%kitid%~
OUTER_TEXT_SPRINT dofkitname  ~D2%kitname%~


/*
*/