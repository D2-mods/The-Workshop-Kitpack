/*
Adds Hunter Badges, installs with Beast Hunter kit.
This file adds all abilities to make the badges work.
*/


ACTION_IF (nobeasthunter = 0) BEGIN


//add effects to Hunter Tools (op177 and op272)
  COPY_EXISTING ~%s1%.SPL~  ~override~
    LPF CLONE_EFFECT 
      INT_VAR check_globals=0 multi_match=1 match_timing=0 opcode=272 parameter1=3 parameter2=3 duration=67
      STR_VAR resource=~%s1%a~ 
    END
    LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=272 STR_VAR resource=~%s1%b~ END
    LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=272 STR_VAR resource=~%s1%c~ END
    LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=272 STR_VAR resource=~%s1%d~ END
    LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=272 STR_VAR resource=~%s1%e~ END
    LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=272 STR_VAR resource=~%s1%f~ END
    LPF CLONE_EFFECT INT_VAR match_opcode=272 opcode=177 timing=1 parameter1=0 parameter2=2 END
  BUT_ONLY


//
//subspells (apply amulet effect)
ACTION_DEFINE_ASSOCIATIVE_ARRAY hunter_badge_array BEGIN
  ~%s1%a~   => ~%m1%~
  ~%s1%b~   => ~%m2%~
  ~%s1%c~   => ~%m3%~
  ~%s1%d~   => ~%m4%~
  ~%s1%e~   => ~%m5%~
  ~%s1%f~   => ~%m6%~
END

ACTION_PHP_EACH hunter_badge_array AS subspell => amulet BEGIN

  COPY ~%abilities%/castspellself.EFF~  ~override/%subspell%.EFF~
    WRITE_ASCII 0x30 ~%subspell%~ #8
  COPY ~%abilities%/applyeffects.SPL~   ~override/%subspell%.SPL~
    LPF ALTER_EFFECT STR_VAR resource=~%amulet%e~ END
    LPF CLONE_EFFECT STR_VAR resource=~%amulet%x~ END
    LPF d2_remove_effects_on_cast STR_VAR resource=~%amulet%e~ END    // remove amulet effects

  COPY ~%abilities%/hiteffectmelee.SPL~  ~override/%amulet%e.SPL~     // subspell casts amulet effect
    LPF ALTER_EFFECT INT_VAR duration=7 STR_VAR resource=~%amulet%a~ END

  COPY ~%abilities%/opcode318.SPL~       ~override/%amulet%x.SPL~     // subspell blocks amulet effect
    LPF ALTER_EFFECT STR_VAR resource=~%amulet%e~ END

END


//
//install item files

  // BAM images (descriptions)
  COPY ~%icons%/badge_spark2.BAM~      ~override/%m1%d.BAM~
  COPY ~%icons%/badge_radiant2.BAM~    ~override/%m2%d.BAM~
  COPY ~%icons%/badge_cosmic2.BAM~     ~override/%m3%d.BAM~
  COPY ~%icons%/badge_powder2.BAM~     ~override/%m4%d.BAM~
  COPY ~%icons%/badge_sawhunter2.BAM~  ~override/%m5%d.BAM~
  COPY ~%icons%/badge_oldhunter2.BAM~  ~override/%m6%d.BAM~

  // spark hunter badge
  COPY ~%files%/badge_spark.EFF~  ~override/%m1%a.EFF~
  COPY ~%files%/badge_spark.ITM~  ~override/%m1%.ITM~
    SAY NAME1 @70101 SAY UNIDENTIFIED_DESC @70201
    SAY NAME2 @70101 SAY DESC @70201
    WRITE_ASCII 0x3a ~IAMUL22~ #8    // icon
    WRITE_ASCII 0x58 ~%m1%d~ #8      // description image
    LPF d2_protect_on_castequip STR_VAR resource=~%m1%x~ END

  // radiant sword badge
  COPY ~%files%/badge_radianta.EFF~  ~override/%m2%a.EFF~
    WRITE_ASCII 0x30 ~%m2%b~ #8
  COPY ~%files%/badge_radiantb.EFF~  ~override/%m2%b.EFF~
  COPY ~%files%/badge_radiant.ITM~   ~override/%m2%.ITM~
    SAY NAME1 @70102 SAY UNIDENTIFIED_DESC @70202
    SAY NAME2 @70102 SAY DESC @70202
    WRITE_ASCII 0x3a ~IAMUL15~ #8    // icon
    WRITE_ASCII 0x58 ~%m2%d~ #8      // description image
    LPF d2_protect_on_castequip STR_VAR resource=~%m2%x~ END

    // charm immunity (for IWDEE)
    PATCH_IF (GAME_IS ~iwdee~) BEGIN
      LPF CLONE_EFFECT INT_VAR multi_match=1 match_timing=2 opcode=101 parameter2=5 END
      LPF cd_apply_batch STR_VAR array_name = cd_immunity_charm_arrays END
      LPF d2_string_immunities END
      LPF ee_fixpack_immunities STR_VAR status=~charm~ END
    END

  // cosmic watcher badge
  COPY ~%files%/badge_cosmic.ITM~  ~override/%m3%.ITM~
    SAY NAME1 @70103 SAY UNIDENTIFIED_DESC @70203
    SAY NAME2 @70103 SAY DESC @70203
    WRITE_ASCII 0x3a ~IAMUL23~ #8    // icon
    WRITE_ASCII 0x58 ~%m3%d~ #8      // description image
    LPF d2_protect_on_castequip STR_VAR resource=~%m3%x~ END

    // vocalize
    LPF cd_apply_batch STR_VAR array_name = cd_immunity_silence_arrays END
    LPF d2_string_immunities END
    LPF ee_fixpack_immunities STR_VAR status=~silence~ END

  OUTER_SET leveldrain = 41495
  ACTION_IF (GAME_IS ~iwdee~) BEGIN  OUTER_SET leveldrain = 35498 END
  ACTION_IF (GAME_IS ~bgee~) BEGIN  OUTER_SET leveldrain = 25802 END

  COPY ~%abilities%/applyeffects.EFF~  ~override/%m3%a.EFF~
    WRITE_ASCII 0x30 ~%m3%a~ #8
    WRITE_LONG 0x40 1       // save vs. spell
    WRITE_LONG 0x44 ~-2~    // -2 penalty to save
  COPY ~%files%/badge_cosmic.SPL~      ~override/%m3%a.SPL~
    LPF DELETE_EFFECT INT_VAR match_opcode=141 END
    LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=leveldrain END
    LPF ALTER_EFFECT INT_VAR savingthrow=0 savebonus=0 END

  // powder keg badge
  COPY ~%files%/badge_pkeg.EFF~  ~override/%m4%a.EFF~
  COPY ~%files%/badge_pkeg.ITM~  ~override/%m4%.ITM~
    SAY NAME1 @70104 SAY UNIDENTIFIED_DESC @70204
    SAY NAME2 @70104 SAY DESC @70204
    WRITE_ASCII 0x3a ~IAMUL25~ #8    // icon
    WRITE_ASCII 0x58 ~%m4%d~ #8      // description image
    LPF d2_protect_on_castequip STR_VAR resource=~%m4%x~ END

  // saw hunter badge
  COPY ~%icons%/badge_sawhunter.BAM~  ~override/%m5%.BAM~
  COPY ~%files%/badge_sawhunter.EFF~  ~override/%m5%a.EFF~
  COPY ~%files%/badge_sawhunter.ITM~  ~override/%m5%.ITM~
    SAY NAME1 @70129 SAY UNIDENTIFIED_DESC @70229
    SAY NAME2 @70129 SAY DESC @70229
    WRITE_ASCII 0x3a ~%m5%~ #8     // icon
    WRITE_ASCII 0x58 ~%m5%d~ #8    // description image
    LPF d2_protect_on_castequip STR_VAR resource=~%m5%x~ END

  // old hunter badge
  COPY ~%files%/badge_oldhunter.ITM~  ~override/%m6%.ITM~
    SAY NAME1 @70130 SAY UNIDENTIFIED_DESC @70230
    SAY NAME2 @70130 SAY DESC @70230
    WRITE_ASCII 0x3a ~IAMUL20~ #8    // icon
    WRITE_ASCII 0x58 ~%m6%d~ #8      // description image
    LPF d2_protect_on_castequip STR_VAR resource=~%m6%x~ END

  COPY ~%files%/badge_oldhunter.SPL~   ~override/%m6%e.SPL~
    LPF DELETE_EFFECT INT_VAR match_opcode=321 END    // not needed (already removed by main subspell)


//
//Craft hunter badges
  COPY ~%abilities%/giveitem1use.SPL~  ~override/%m1%g.SPL~
    SAY NAME1 @611
    SAY UNIDENTIFIED_DESC @221
    LPF ALTER_EFFECT INT_VAR match_opcode=122 STR_VAR resource=~%m1%~ END
    LPF ALTER_EFFECT INT_VAR match_opcode=172 STR_VAR resource=~%m1%g~ END

  COPY ~%abilities%/giveitem1use.SPL~  ~override/%m2%g.SPL~
    SAY NAME1 @612
    SAY UNIDENTIFIED_DESC @221
    LPF ALTER_EFFECT INT_VAR match_opcode=122 STR_VAR resource=~%m2%~ END
    LPF ALTER_EFFECT INT_VAR match_opcode=172 STR_VAR resource=~%m2%g~ END

  COPY ~%abilities%/giveitem1use.SPL~  ~override/%m3%g.SPL~
    SAY NAME1 @613
    SAY UNIDENTIFIED_DESC @221
    LPF ALTER_EFFECT INT_VAR match_opcode=122 STR_VAR resource=~%m3%~ END
    LPF ALTER_EFFECT INT_VAR match_opcode=172 STR_VAR resource=~%m3%g~ END

  COPY ~%abilities%/giveitem1use.SPL~  ~override/%m4%g.SPL~
    SAY NAME1 @614
    SAY UNIDENTIFIED_DESC @221
    LPF ALTER_EFFECT INT_VAR match_opcode=122 STR_VAR resource=~%m4%~ END
    LPF ALTER_EFFECT INT_VAR match_opcode=172 STR_VAR resource=~%m4%g~ END

  COPY ~%abilities%/giveitem1use.SPL~  ~override/%m5%g.SPL~
    SAY NAME1 @617
    SAY UNIDENTIFIED_DESC @221
    LPF ALTER_EFFECT INT_VAR match_opcode=122 STR_VAR resource=~%m5%~ END
    LPF ALTER_EFFECT INT_VAR match_opcode=172 STR_VAR resource=~%m5%g~ END

  COPY ~%abilities%/giveitem1use.SPL~  ~override/%m6%g.SPL~
    SAY NAME1 @618
    SAY UNIDENTIFIED_DESC @221
    LPF ALTER_EFFECT INT_VAR match_opcode=122 STR_VAR resource=~%m6%~ END
    LPF ALTER_EFFECT INT_VAR match_opcode=172 STR_VAR resource=~%m6%g~ END


//
//Set variables for external use
  OUTER_TEXT_SPRINT badge_spark         ~%m1%~
  OUTER_TEXT_SPRINT badge_radiant       ~%m2%~
  OUTER_TEXT_SPRINT badge_cosmic        ~%m3%~
  OUTER_TEXT_SPRINT badge_pkeg          ~%m4%~
  OUTER_TEXT_SPRINT badge_saw           ~%m5%~
  OUTER_TEXT_SPRINT badge_old           ~%m6%~
  OUTER_TEXT_SPRINT give_badge_spark    ~%m1%g~
  OUTER_TEXT_SPRINT give_badge_radiant  ~%m2%g~
  OUTER_TEXT_SPRINT give_badge_cosmic   ~%m3%g~
  OUTER_TEXT_SPRINT give_badge_pkeg     ~%m4%g~
  OUTER_TEXT_SPRINT give_badge_saw      ~%m5%g~
  OUTER_TEXT_SPRINT give_badge_old      ~%m6%g~

  OUTER_TEXT_SPRINT mode  ~badge~
  INCLUDE ~%install%/itemtypepatch.tph~    // set item fields (function work)


END


/*
*/