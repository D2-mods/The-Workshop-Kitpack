//Confessor (d2wk5)


//Clone mage spells
ACTION_IF (GAME_IS ~iwdee~) BEGIN
  OUTER_TEXT_SPRINT blind ~SPWI226~
END ELSE BEGIN
  OUTER_TEXT_SPRINT blind ~SPWI106~
END

COPY_EXISTING - ~%blind%.SPL~  ~~  READ_LONG 0x50 blindness
COPY_EXISTING - ~SPWI201.SPL~  ~~  READ_LONG 0x50 blur

ACTION_IF !(FILE_EXISTS ~override/%blind%.SPL~) BEGIN
  ACTION_IF (GAME_IS ~iwdee~) BEGIN
    OUTER_SET blindness = RESOLVE_STR_REF (@40303)    // IWDEE
  END ELSE BEGIN
    OUTER_SET blindness = RESOLVE_STR_REF (@40203)    // BGEE
  END
END

ACTION_IF !(FILE_EXISTS ~override/SPWI201.SPL~) BEGIN
  ACTION_IF (GAME_IS ~iwdee~) BEGIN
    OUTER_SET blur = RESOLVE_STR_REF (@40304)    // IWDEE
  END ELSE BEGIN
    OUTER_SET blur = RESOLVE_STR_REF (@40204)    // BGEE
  END
END

ACTION_IF (MOD_IS_INSTALLED ~setup-spell_rev.tp2~ 0) BEGIN
  ACTION_IF (FILE_CONTAINS ~spell_rev/setup-spell_rev.tp2~ ~dv-spell_rev-main~) BEGIN
    OUTER_SET blindness = RESOLVE_STR_REF (@40403)
    OUTER_SET blur      = RESOLVE_STR_REF (@40404)
  END
END

LAF d2_convert_spell_type INT_VAR type=2 level=1 STR_VAR spell=~%blind%~ newspell=~%p1%~ END
LAF d2_convert_spell_type INT_VAR type=2 level=2 STR_VAR spell=~SPWI201~ newspell=~%p2%~ END
LAF d2_convert_spell_type INT_VAR type=2 level=3 STR_VAR spell=~SPWI314~ newspell=~%p3%~ END
LAF d2_convert_spell_type INT_VAR type=2 level=4 STR_VAR spell=~SPWI419~ newspell=~%p4%~ END

COPY_EXISTING ~%p1%.SPL~  ~override~  WRITE_LONG 0x50 blindness  BUT_ONLY
COPY_EXISTING ~%p2%.SPL~  ~override~  WRITE_LONG 0x50 blur       BUT_ONLY


//add to spell.ids (config option)
ACTION_IF (%nopriestids% = 0) BEGIN
  OUTER_FOR (n = 1; n <= 4; ++n) BEGIN
    LAF ADD_SPELL_EX INT_VAR type=1 level=%n% min=51 max=99 hide=1 STR_VAR file=~override/%p%n%%.SPL~ name=~D2%kitname%_P%n%~ RET spell_res END
    OUTER_TEXT_SPRINT ~p%n%~  ~%spell_res%~
  END

  ACTION_FOR_EACH spell IN ~%p1%~ ~%p3%~ ~%p4%~ BEGIN
    COPY ~override/%spell%.SPL~  ~override~
      LPF update_op324_restrictions END
    BUT_ONLY
  END

  COPY ~override/%p2%.SPL~  ~override~
    LPF update_op324_restrictions END
    LPF d2_remove_effects_on_cast END
  BUT_ONLY
END


//
//Add Rooting Shot
COPY ~%icons%/rootingshoticon.BAM~  ~override/%s1%i.BAM~
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@106) STR_VAR bam=~%s1%i~ RET icon END

COPY ~%icons%/rootingshot.BAM~      ~override/%s1%.BAM~
COPY ~%abilities%/rootingshot.SPL~  ~override/%s1%.SPL~
  SAY NAME1 @106
  SAY UNIDENTIFIED_DESC @206
  WRITE_ASCII 0x3a ~%s1%~ #8
  WRITE_ASCII 0x76 ~%s1%~ #8
  LPF ALTER_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%s1%~ END
  LPF ALTER_EFFECT INT_VAR multi_match=1 match_opcode=249 STR_VAR resource=~%s1%x~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=249 STR_VAR resource=~%s1%e~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END

//main subspell
COPY ~%abilities%/applyeffects.EFF~  ~override/%s1%e.EFF~
  WRITE_ASCII 0x30 ~%s1%e~ #8
COPY ~%abilities%/rootingapply.SPL~  ~override/%s1%e.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%s1%a~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=177 STR_VAR resource=~%s1%b~ END
  LPF DELETE_EFFECT INT_VAR match_opcode=139 END
  LPF d2_remove_effects_on_cast END

COPY ~%abilities%/rootingentangle.SPL~  ~override/%s1%a.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%s1%a~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=139 parameter1=RESOLVE_STR_REF (@301) END    // "Entangled"

COPY ~%abilities%/applyeffects.EFF~  ~override/%s1%b.EFF~
  WRITE_ASCII 0x30 ~%s1%a~ #8
  WRITE_LONG 0x40 1    // save vs. spell

//remove after 1 shot
COPY ~%abilities%/castspellself.EFF~       ~override/%s1%x.EFF~
  WRITE_ASCII 0x30 ~%s1%x~ #8
COPY ~%abilities%/removespelleffects.SPL~  ~override/%s1%x.SPL~
  LPF ALTER_EFFECT STR_VAR resource=~%s1%~ END


//
//Add Bloodletter
COPY ~%icons%/bloodletter.BAM~      ~override/%s2%i.BAM~
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@107) STR_VAR bam=~%s2%i~ RET icon END
COPY ~%abilities%/bloodletter.SPL~  ~override/%s2%.SPL~
  SAY NAME1 @107
  SAY UNIDENTIFIED_DESC @207
  LPF ALTER_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%s2%~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=248 STR_VAR resource=~%s2%a~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=248 STR_VAR resource=~%s2%b~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END

//healing
COPY ~%abilities%/castspellself.EFF~    ~override/%s2%a.EFF~
  WRITE_ASCII 0x30 ~%s2%a~ #8
COPY ~%abilities%/bloodletterheal.SPL~  ~override/%s2%a.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@307) END    // "Healed"

//bleeding
COPY ~%abilities%/applyeffects.EFF~  ~override/%s2%b.EFF~
  WRITE_ASCII 0x30 ~%s2%b~ #8
COPY ~inlined/bleed_core.SPL~        ~override/%s2%b.SPL~
  LPF d2_bleed_delay INT_VAR limit=6 STR_VAR resource=d2bleed2 END

LAF d2_bleed_removal STR_VAR resource=~%s2%b~ END    // add to d2!bleed.spl


//Make clab table
COPY ~inlined/d2clab_paladin~  ~override/%kitid%.2DA~
  REPLACE_TEXTUALLY ~GA_SPCL211~ ~****~    // remove Lay On Hands
  LPF d2_kit_ability_adder INT_VAR level=2 multi=1 increment=4 STR_VAR resource=~%s1%~ END
  LPF d2_kit_ability_adder INT_VAR level=5 multi=1 increment=4 limit=13 STR_VAR resource=~%s2%~ END
  LPF d2_kit_ability_adder STR_VAR resource=~%p1%~ END
  LPF d2_kit_ability_adder STR_VAR resource=~%p2%~ END
  LPF d2_kit_ability_adder STR_VAR resource=~%p3%~ END
  LPF d2_kit_ability_adder STR_VAR resource=~%p4%~ END
  LPF d2_kit_ability_adder END


//Kit function
LAF ADD_KIT_EX
  INT_VAR
    kit_class = 6
    mixed = RESOLVE_STR_REF (@100501)
    lower = RESOLVE_STR_REF (@100502)
    help = RESOLVE_STR_REF (@100503)
    fallen_notice = RESOLVE_STR_REF (@100504)
  STR_VAR
    kit_name = ~D2%kitname%~
//  unusable = ~0x00010000~           // Stalker usability (using different method)
    clab_path = ~%kitid%~
    clsrcreq = ~1 1 1 1 1 1 1~        // Usable by all races, if base class is usable
    alignmnt = ~1 0 0 0 0 0 0 0 0~    // LG
    clascolr = ~25 83 66 28 23~       // Priest of Talos colors
  RET 
    kit_id
END


//Usability - restrict to studded leather
ACTION_DEFINE_ASSOCIATIVE_ARRAY d2_restrict_armor_array BEGIN
  ~D2%kitname%~ => ~studded~
END


//
//function to remove rooting shot effects
DEFINE_PATCH_FUNCTION confessor_entangle_remove BEGIN
  LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=101 match_parameter2=154 opcode=321 parameter2=0 duration=0 STR_VAR resource=~%confessorhold%~ END
END

//function to block rooting shot effects
DEFINE_PATCH_FUNCTION confessor_entangle_block BEGIN
  LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=101 match_parameter2=154 opcode=318 parameter2=0 STR_VAR resource=~%confessorhold%~ END
END

//
DEFINE_PATCH_FUNCTION confessor_entangle_spells BEGIN
  LPF confessor_entangle_remove END
  LPF confessor_entangle_block END
END


//
//Set variables for external use
OUTER_TEXT_SPRINT confessor        ~%kitid%~
OUTER_TEXT_SPRINT rootingshot      ~%s1%~
OUTER_TEXT_SPRINT confessorhold    ~%s1%a~
OUTER_TEXT_SPRINT bloodletter      ~%s2%~
OUTER_TEXT_SPRINT confessorbleed   ~%s2%b~

//spells
OUTER_TEXT_SPRINT confessor1       ~%p1%~
OUTER_TEXT_SPRINT confessor2       ~%p2%~
OUTER_TEXT_SPRINT confessor3       ~%p3%~
OUTER_TEXT_SPRINT confessor4       ~%p4%~

//fnp
OUTER_TEXT_SPRINT fnpclass         ~zealot~
OUTER_TEXT_SPRINT fnpdeity         ~auril_spheres~
OUTER_TEXT_SPRINT fnparchetype1    ~SetGlobal("D5PADEF","GLOBAL",0)~
OUTER_TEXT_SPRINT fnparchetype2    ~SetGlobal("D5PAINQ","GLOBAL",1)~
OUTER_TEXT_SPRINT fnparchetype3    ~SetGlobal("D5PAZEA","GLOBAL",1)~

//tof
OUTER_TEXT_SPRINT tofkitname  ~D2%kitname%~
//OUTER_TEXT_SPRINT tofspheres  ~+charm,+divination_cleric,-good~
OUTER_TEXT_SPRINT tofspheres  ~all,charm,combat,divination_cleric,divine,healing,protection~

//dof
OUTER_TEXT_SPRINT dofclab     ~%kitid%~
OUTER_TEXT_SPRINT dofkitname  ~D2%kitname%~


/*
*/