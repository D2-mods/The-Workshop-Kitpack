/*
Make song switching abilities for HLA songs
Moved updater stuff to powderkeghla2.tph

RR#BDF03 - gives RR#BDF04
RR#BDF04 - HLA song (lasts 1 round)
RR#BDF05 - gives RR#BDF06
RR#BDF06 - HLA song (lasts 3 rounds)

LI#EN01 - gives LI#ENH1
LI#ENH1 - HLA song (lasts 1 round)
TG#LI01 - gives TG#LIN1
TG#LIN1 - HLA song (lasts 3 rounds)
*/


//spell should be 7 characters or less
//suffix with a, b, x, and g should be free
//cleanup is a subspell that removes older effects

DEFINE_ACTION_FUNCTION pkeghlaswitcher STR_VAR spell=~~ cleanup=~~ BEGIN
  //main subspell
  COPY ~%abilities%/applyeffects.SPL~  ~override/%spell%a.SPL~    // casts B and X
    LPF ALTER_EFFECT STR_VAR resource=~%spell%b~ END
    LPF CLONE_EFFECT STR_VAR resource=~%spell%x~ END
  COPY ~%abilities%/applyeffects.SPL~  ~override/%spell%b.SPL~    // B casts G
    LPF ALTER_EFFECT STR_VAR resource=~%spell%g~ END
    LPF CLONE_EFFECT INT_VAR opcode=272 parameter1=6 parameter2=3 timing=9 END    // repeating effect (import fix)
    LPF d2_remove_effects_on_cast INT_VAR 326override=1 STR_VAR resource=~%cleanup%~ END
  COPY ~%abilities%/opcode318.SPL~     ~override/%spell%x.SPL~    // X blocks B
    LPF ALTER_EFFECT STR_VAR resource=~%spell%b~ END

  //give ability
  COPY ~%abilities%/applyeffects.EFF~  ~override/%spell%g.EFF~
    WRITE_ASCII 0x30 ~%spell%g~ #8
  COPY ~%passives%/giveability.SPL~    ~override/%spell%g.SPL~
    LPF ALTER_EFFECT STR_VAR resource=~%spell%~ END
    LPF CLONE_EFFECT INT_VAR opcode=172 STR_VAR resource=~%spell%~ END
    LPF d2_protect_on_castequip INT_VAR duration=3600000 END
END


//remove old effects
OUTER_SPRINT cleanup  ~%s5%t~
COPY ~%abilities%/removespelleffects.SPL~  ~override/%cleanup%.SPL~
  LPF ALTER_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%s5%b~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%s6%b~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%s7%b~ END
  LPF ADD_SPELL_EFFECT INT_VAR opcode=172 target=2 timing=1 STR_VAR resource=~%s5%~ END
  LPF ADD_SPELL_EFFECT INT_VAR opcode=172 target=2 timing=1 STR_VAR resource=~%s6%~ END
  LPF ADD_SPELL_EFFECT INT_VAR opcode=172 target=2 timing=1 STR_VAR resource=~%s7%~ END


//make subspells
LAF pkeghlaswitcher STR_VAR spell=~%s5%~ cleanup=~%cleanup%~ END    // base HLA song
LAF pkeghlaswitcher STR_VAR spell=~%s6%~ cleanup=~%cleanup%~ END    // mod HLA song (rr or tg)
LAF pkeghlaswitcher STR_VAR spell=~%s7%~ cleanup=~%cleanup%~ END    // mod lingering song (rr or tg)


//base HLA song
COPY ~%abilities%/changebardsong.SPL~  ~override/%s5%.SPL~
  SAY NAME1 @119
  SAY UNIDENTIFIED_DESC @219
  LPF ALTER_EFFECT INT_VAR match_opcode=251 STR_VAR resource=~SPCL920A~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%s0%a~ END
  LPF ALTER_EFFECT INT_VAR check_headers=0 STR_VAR resource=~%s5%~ END
  WRITE_ASCII 0x3a ~SPENBARB~ #8    // icons
  WRITE_ASCII 0x76 ~SPENBARB~ #8

//mod HLA song (rr or tg)
COPY ~%abilities%/changebardsong.SPL~  ~override/%s6%.SPL~
  SAY NAME1 @120
//  SAY UNIDENTIFIED_DESC @220
  LPF ALTER_EFFECT INT_VAR match_opcode=251 STR_VAR resource=~RR#BDF04~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%s0%a~ END
  LPF ALTER_EFFECT INT_VAR check_headers=0 STR_VAR resource=~%s6%~ END
  WRITE_ASCII 0x3a ~SPENBARB~ #8    // icons
  WRITE_ASCII 0x76 ~SPENBARB~ #8

//mod lingering song (rr or tg)
COPY ~%abilities%/changebardsong.SPL~  ~override/%s7%.SPL~
  SAY NAME1 @120
//  SAY UNIDENTIFIED_DESC @220
  LPF ALTER_EFFECT INT_VAR match_opcode=251 STR_VAR resource=~RR#BDF06~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%s0%a~ END
  LPF ALTER_EFFECT INT_VAR check_headers=0 STR_VAR resource=~%s7%~ END
  WRITE_ASCII 0x3a ~SPENBARB~ #8    // icons
  WRITE_ASCII 0x76 ~SPENBARB~ #8


//Set variables for external use
OUTER_TEXT_SPRINT enhancedbardsong   ~%s5%~
OUTER_TEXT_SPRINT rrenhancedsong     ~%s6%~
OUTER_TEXT_SPRINT rrlingeringsong    ~%s7%~
OUTER_TEXT_SPRINT shohyenhancedsong  ~BI#B1SA~


//
//