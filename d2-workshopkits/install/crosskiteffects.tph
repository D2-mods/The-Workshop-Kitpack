/*
Misc patching (spell interactions, etc.)
anything that's not directly for making the kit work
*/


//immune to Rooting Shot entangle
ACTION_IF (FILE_EXISTS ~override/%rootingshot%.SPL~) BEGIN
  COPY_EXISTING ~%drifter_immunities%.SPL~  ~override~
    LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=318 parameter2=0 STR_VAR resource=~%confessorhold%~ END
  IF_EXISTS BUT_ONLY
END


//immune to Dust Explosion statuses
ACTION_IF (FILE_EXISTS ~override/%dustexplosion%.SPL~) BEGIN
  COPY_EXISTING ~%cloud_immunities%.SPL~  ~override~
    LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=318 parameter2=0 STR_VAR resource=~%dustblind%~ END
    LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=318 parameter2=0 STR_VAR resource=~%dustslow%~ END
  IF_EXISTS BUT_ONLY

  COPY_EXISTING ~%wardennoblind%.SPL~  ~override~
    LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=318 parameter2=0 STR_VAR resource=~%dustblind%~ END
  IF_EXISTS BUT_ONLY
END



//change ToB starting items for some kits
ACTION_IF (GAME_INCLUDES ~tob~) BEGIN
  COPY_EXISTING ~25STWEAP.2da~  ~override~
    PATCH_FOR_EACH kit IN ~d2drifter~ ~d2nurse~ BEGIN
      LPF d2_edit_2da_entry STR_VAR col_name=~%kit%~ row_name=armor entry=leat20 END    // changed to hide armor
    END

    PATCH_FOR_EACH kit IN ~d2beasthunter~ ~d2beasthunterfc~ ~d2confessor~ BEGIN
      LPF d2_edit_2da_entry STR_VAR col_name=~%kit%~ row_name=armor entry=leat19 END    // changed to dragon scale
    END

    PATCH_FOR_EACH kit IN ~d2moonlightknight~ ~d2moonlightknightcr~ BEGIN
      LPF d2_edit_2da_entry STR_VAR col_name=~%kit%~ row_name=armor entry=chan09 END    // changed to chain mail
    END

    PATCH_FOR_EACH kit IN ~d2warhound~ ~d2warhoundfm~ ~d2noblescion~ BEGIN
      LPF d2_edit_2da_entry STR_VAR col_name=~%kit%~ row_name=MISC5 entry=dagg12 END    // firetooth
    END

    PATCH_FOR_EACH kit IN ~d2nursemt~ ~d2medic~ BEGIN
      LPF d2_edit_2da_entry STR_VAR col_name=~%kit%~ row_name=MISC4 entry=dagg12 END    // firetooth
      LPF d2_edit_2da_entry STR_VAR col_name=~%kit%~ row_name=MISC5 entry=SLNG07 END    // sling of seeking
    END

    PRETTY_PRINT_2DA
  BUT_ONLY


  // change some bag items
  ACTION_FOR_EACH bag IN ~bag25~ ~bag28~ BEGIN
    COPY_EXISTING ~%bag%.sto~  ~override~
      LPF ADD_STORE_ITEM_EX INT_VAR overwrite=1 STR_VAR item_name=SLNG07 flags=identified position=last END    // sling of seeking
    BUT_ONLY
  END

  ACTION_FOR_EACH bag IN ~bag27a~ BEGIN
    COPY_EXISTING ~%bag%.sto~  ~override~
      LPF ADD_STORE_ITEM_EX INT_VAR overwrite=1 STR_VAR item_name=SLNG07 flags=identified position=last END    // sling of seeking
      LPF ADD_STORE_ITEM_EX INT_VAR overwrite=1 STR_VAR item_name=BLUN18 flags=identified position=last END    // skullcrusher
    BUT_ONLY
  END
END


//
//