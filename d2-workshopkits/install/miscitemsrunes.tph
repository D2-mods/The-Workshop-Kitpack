/*
Caryll Runes
*/


//icons
COPY ~%icons%/rune_clawmark.BAM~       ~override/%z1%i.BAM~
COPY ~%icons%/rune_bloodrapture.BAM~   ~override/%z2%i.BAM~
COPY ~%icons%/rune_oedonwrithe.BAM~    ~override/%z3%i.BAM~
COPY ~%icons%/rune_corruption.BAM~     ~override/%z4%i.BAM~
COPY ~%icons%/rune_anticlockwise.BAM~  ~override/%z5%i.BAM~
COPY ~%icons%/rune_clockwise.BAM~      ~override/%z6%i.BAM~
COPY ~%icons%/rune_greatlake.BAM~      ~override/%z7%i.BAM~
COPY ~%icons%/rune_fadinglake.BAM~     ~override/%z8%i.BAM~
COPY ~%icons%/rune_beastembrace.BAM~   ~override/%z9%i.BAM~
COPY ~%icons%/rune_hunter.BAM~         ~override/%za%i.BAM~
COPY ~%icons%/rune_moon.BAM~           ~override/%zb%i.BAM~
COPY ~%icons%/rune_milkweed.BAM~       ~override/%zc%i.BAM~


//Remove rune effects
COPY ~%abilities%/castspellself.EFF~       ~override/%z0%a.EFF~
  WRITE_ASCII 0x30 ~%z0%a~ #8
COPY ~%abilities%/removespelleffects.SPL~  ~override/%z0%a.SPL~
  LPF ALTER_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%z1%~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%z2%~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%z3%~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%z4%~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%z5%~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%z5%a~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%z6%~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%z7%~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%z8%~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%z9%~ END
//  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%z9%a~ END  // not needed
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%z9%b~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%za%~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%zb%~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%zc%~ END
//  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%zc%a~ END  // not needed
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%zc%b~ END


//Visual effects
COPY ~%abilities%/castspellself.EFF~  ~override/%z0%b.EFF~
  WRITE_ASCII 0x30 ~%z0%b~ #8
COPY ~%files%/rune_visual.SPL~        ~override/%z0%b.SPL~

LAF d2_color_string INT_VAR stringref=RESOLVE_STR_REF (@305) STR_VAR color=gold RET newstring END

COPY ~%icons%/caryllruneicon.BAM~  ~override/%z0%i.BAM~    // portrait icon
COPY ~%icons%/caryllrunedesc2.BAM~  ~override/%z0%d.BAM~    // portrait icon



//Clawmark
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@70114) STR_VAR bam=~%z0%i~ RET icon END
COPY ~%files%/rune_clawmark.ITM~  ~override/%z1%.ITM~
  SAY NAME1 @70114 SAY UNIDENTIFIED_DESC @70214
  SAY NAME2 @70114 SAY DESC @70214
  WRITE_ASCII 0x3a ~%z1%i~ #8    // icon
  WRITE_ASCII 0x58 ~%z0%d~ #8    // description image
  WRITE_ASCII 0x76 ~%z1%i~ #8    // icon (ability)
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=newstring END                 // dialogue message
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%z0%b~ END             // visual
  LPF CLONE_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%z0%a~ END             // remove rune effects
  LPF ALTER_EFFECT INT_VAR match_opcode=341 STR_VAR resource=~%z1%b~ END             // critical effect
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END

COPY ~%abilities%/applyeffects.EFF~  ~override/%z1%b.EFF~
  WRITE_ASCII 0x30 ~%z1%b~ #8
COPY ~inlined/bleed_core.SPL~        ~override/%z1%b.SPL~
  LPF d2_bleed_delay INT_VAR limit=4 STR_VAR resource=d2bleed3 END

LAF d2_bleed_removal STR_VAR resource=~%z1%b~ END    // add to d2!bleed.spl
LAF d2_bleed_effects STR_VAR resource=~%z1%b~ END    // blocked by damage immunity (ex. shield of lathander)


//Blood Rapture
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@70115) STR_VAR bam=~%z0%i~ RET icon END
COPY ~%files%/rune_core.ITM~   ~override/%z2%.ITM~
  SAY NAME1 @70115 SAY UNIDENTIFIED_DESC @70215
  SAY NAME2 @70115 SAY DESC @70215
  WRITE_ASCII 0x3a ~%z2%i~ #8    // icon
  WRITE_ASCII 0x58 ~%z0%d~ #8    // description image
  WRITE_ASCII 0x76 ~%z2%i~ #8    // icon (ability)
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=newstring END                 // dialogue message
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%z0%b~ END             // visual
  LPF CLONE_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%z0%a~ END             // remove rune effects
  LPF ALTER_EFFECT INT_VAR match_opcode=341 STR_VAR resource=~%z2%a~ END             // critical effect
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END

COPY ~%abilities%/castspellself.EFF~    ~override/%z2%a.EFF~
  WRITE_ASCII 0x30 ~%z2%a~ #8
COPY ~%abilities%/healselfnosound.SPL~  ~override/%z2%a.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=17 parameter1=9 END
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@307) END    // "Healed"


//Oedon Writhe
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@70116) STR_VAR bam=~%z0%i~ RET icon END
COPY ~%files%/rune_core.ITM~   ~override/%z3%.ITM~
  SAY NAME1 @70116 SAY UNIDENTIFIED_DESC @70216
  SAY NAME2 @70116 SAY DESC @70216
  WRITE_ASCII 0x3a ~%z3%i~ #8    // icon
  WRITE_ASCII 0x58 ~%z0%d~ #8    // description image
  WRITE_ASCII 0x76 ~%z3%i~ #8    // icon (ability)
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=newstring END                 // dialogue message
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%z0%b~ END             // visual
  LPF CLONE_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%z0%a~ END             // remove rune effects
  LPF ALTER_EFFECT INT_VAR match_opcode=341 STR_VAR resource=~%z3%a~ END             // critical effect
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END

COPY ~%abilities%/castspellself.EFF~  ~override/%z3%a.EFF~
  WRITE_ASCII 0x30 ~%z3%a~ #8
COPY ~%abilities%/restore1spell.SPL~  ~override/%z3%a.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=261 parameter1=6 END


//Corruption
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@70117) STR_VAR bam=~%z0%i~ RET icon END
COPY ~%files%/rune_corruption.ITM~  ~override/%z4%.ITM~
  SAY NAME1 @70117 SAY UNIDENTIFIED_DESC @70217
  SAY NAME2 @70117 SAY DESC @70217
  WRITE_ASCII 0x3a ~%z4%i~ #8    // icon
  WRITE_ASCII 0x58 ~%z0%d~ #8    // description image
  WRITE_ASCII 0x76 ~%z4%i~ #8    // icon (ability)
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=newstring END                 // dialogue message
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%z0%b~ END             // visual
  LPF CLONE_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%z0%a~ END             // remove rune effects
  LPF ALTER_EFFECT INT_VAR match_opcode=232 match_parameter2=2 STR_VAR resource=~%z4%a~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=232 match_parameter2=3 STR_VAR resource=~%z4%b~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=232 match_parameter2=4 STR_VAR resource=~%z4%b~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END

COPY ~%abilities%/healselfnosound.SPL~  ~override/%z4%a.SPL~                   // heal 4 HP
  LPF DELETE_EFFECT INT_VAR match_opcode=139 END
  LPF DELETE_EFFECT INT_VAR match_opcode=61 END
  LPF ALTER_EFFECT INT_VAR match_opcode=17 parameter1=4 END
  LPF CLONE_EFFECT INT_VAR opcode=142 parameter2=87 timing=0 duration=7 END    // "Regenerating" icon
COPY ~%abilities%/healselfnosound.SPL~  ~override/%z4%b.SPL~                   // heal 2 HP
  LPF DELETE_EFFECT INT_VAR match_opcode=139 END
  LPF DELETE_EFFECT INT_VAR match_opcode=61 END
  LPF ALTER_EFFECT INT_VAR match_opcode=17 parameter1=2 END


//Anti-Clockwise Metamorphosis
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@70118) STR_VAR bam=~%z0%i~ RET icon END
COPY ~%files%/rune_move.ITM~  ~override/%z5%.ITM~
  SAY NAME1 @70118 SAY UNIDENTIFIED_DESC @70218
  SAY NAME2 @70118 SAY DESC @70218
  WRITE_ASCII 0x3a ~%z5%i~ #8    // icon
  WRITE_ASCII 0x58 ~%z0%d~ #8    // description image
  WRITE_ASCII 0x76 ~%z5%i~ #8    // icon (ability)
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=newstring END                      // dialogue message
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%z5%a~ END                  // +4 movement
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=326 STR_VAR resource=~%z0%b~ END    // visual
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=326 STR_VAR resource=~%z0%a~ END    // remove rune effects
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END

  // repeating effect to reapply after shapeshifts
  SET timer = 7200 - 5
  LPF ALTER_EFFECT INT_VAR match_opcode=272 duration=timer STR_VAR resource=~%z5%a~ END    // repeating EFF (+4 movement)

COPY ~%abilities%/applyeffects.EFF~  ~override/%z5%a.EFF~
  WRITE_ASCII 0x30 ~%z5%a~ #8
COPY ~%files%/rune_move.SPL~          ~override/%z5%a.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%z5%a~ END


//Clockwise Metamorphosis
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@70119) STR_VAR bam=~%z0%i~ RET icon END
COPY ~%files%/rune_core.ITM~  ~override/%z6%.ITM~
  SAY NAME1 @70119 SAY UNIDENTIFIED_DESC @70219
  SAY NAME2 @70119 SAY DESC @70219
  WRITE_ASCII 0x3a ~%z6%i~ #8    // icon
  WRITE_ASCII 0x58 ~%z0%d~ #8    // description image
  WRITE_ASCII 0x76 ~%z6%i~ #8    // icon (ability)
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=newstring END                      // dialogue message
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%z0%b~ END                  // visual
  LPF CLONE_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%z0%a~ END                  // remove rune effects
  LPF ALTER_EFFECT INT_VAR match_opcode=341 opcode=18 parameter1=120 parameter2=5 END     // 120% HP
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END


//Great Lake
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@70120) STR_VAR bam=~%z0%i~ RET icon END
COPY ~%files%/rune_core.ITM~  ~override/%z7%.ITM~
  SAY NAME1 @70120 SAY UNIDENTIFIED_DESC @70220
  SAY NAME2 @70120 SAY DESC @70220
  WRITE_ASCII 0x3a ~%z7%i~ #8    // icon
  WRITE_ASCII 0x58 ~%z0%d~ #8    // description image
  WRITE_ASCII 0x76 ~%z7%i~ #8    // icon (ability)
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=newstring END                 // dialogue message
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%z0%b~ END             // visual
  LPF CLONE_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%z0%a~ END             // remove rune effects
  LPF ALTER_EFFECT INT_VAR match_opcode=341 opcode=89 parameter1=10 END              // missile resist
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=89 opcode=86 END               // slashing resist
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=89 opcode=87 END               // crushing resist
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=89 opcode=88 END               // piercing resist
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END


//Fading Lake
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@70121) STR_VAR bam=~%z0%i~ RET icon END
COPY ~%files%/rune_core.ITM~  ~override/%z8%.ITM~
  SAY NAME1 @70121 SAY UNIDENTIFIED_DESC @70221
  SAY NAME2 @70121 SAY DESC @70221
  WRITE_ASCII 0x3a ~%z8%i~ #8    // icon
  WRITE_ASCII 0x58 ~%z0%d~ #8    // description image
  WRITE_ASCII 0x76 ~%z8%i~ #8    // icon (ability)
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=newstring END                     // dialogue message
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%z0%b~ END                 // visual
  LPF CLONE_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%z0%a~ END                 // remove rune effects
  LPF ALTER_EFFECT INT_VAR match_opcode=341 opcode=30 parameter1=80 parameter2=1 END     // fire resist
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=30 opcode=84 END                   // magical fire resist
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=16 END
  LPF CLONE_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END


//Beast's Embrace (subspells at werewolf.tph)
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@70122) STR_VAR bam=~%z0%i~ RET icon END
COPY ~%files%/rune_werewolf.ITM~  ~override/%z9%.ITM~
  SAY NAME1 @70122 SAY UNIDENTIFIED_DESC @70222
  SAY NAME2 @70122 SAY DESC @70222
  WRITE_ASCII 0x3a ~%z9%i~ #8    // icon
  WRITE_ASCII 0x58 ~%z0%d~ #8    // description image
  WRITE_ASCII 0x76 ~%z9%i~ #8    // icon (ability)
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=newstring END                 // dialogue message
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%z0%b~ END             // visual
  LPF CLONE_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%z0%a~ END             // remove rune effects
  LPF ALTER_EFFECT INT_VAR match_opcode=232 STR_VAR resource=~%z9%a~ END             // polymorph
  LPF CLONE_EFFECT INT_VAR match_opcode=232 STR_VAR resource=~%z9%c~ END             // remove polymorph
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END

COPY ~%files%/rune_effects.SPL~      ~override/%z9%a.SPL~
  LPF ALTER_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%werewolf1%~ END                                  // other classes
  LPF CLONE_EFFECT INT_VAR multi_match=1 parameter2=39 STR_VAR resource=~%werewolf2%~ END                    // Paladin
  LPF CLONE_EFFECT INT_VAR multi_match=1 parameter1=204 parameter2=105 STR_VAR resource=~%werewolf2%~ END    // Cleric
  LPF CLONE_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%z9%b~ insert=~below~ END                      // remove ability

COPY ~%passives%/removespell.SPL~    ~override/%z9%b.SPL~
  LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=206 STR_VAR resource=~%z9%a~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=172 timing=4 duration=7 STR_VAR resource=~SPIN124~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=172 STR_VAR resource=~SPCL110~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=172 STR_VAR resource=~SPINHUM~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=172 STR_VAR resource=~SPIN122~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=172 duration=2 END

COPY ~%files%/rune_effects.SPL~      ~override/%z9%c.SPL~
  LPF CLONE_EFFECT INT_VAR match_opcode=326 opcode=321 STR_VAR resource=~%z9%c~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=326 timing=4 duration=15 STR_VAR resource=~%z9%x~ END

COPY ~%files%/rune_effects.SPL~      ~override/%z9%x.SPL~
  LPF CLONE_EFFECT INT_VAR match_opcode=326 opcode=321 STR_VAR resource=~%z9%b~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%humanform%~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%werewolf1%~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%werewolf2%~ END

//this slayer form blocks other shapeshifts (has unique string message)
OUTER_SPRINT slayerform  ~slayerw3~
COPY_EXISTING ~%slayerform%.itm~  ~override~
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=206 STR_VAR resource=~%werewolf1%~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=206 STR_VAR resource=~%werewolf2%~ END
IF_EXISTS BUT_ONLY


//Hunter
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@70125) STR_VAR bam=~%z0%i~ RET icon END
COPY ~%files%/rune_core.ITM~  ~override/%za%.ITM~
  SAY NAME1 @70125 SAY UNIDENTIFIED_DESC @70225
  SAY NAME2 @70125 SAY DESC @70225
  WRITE_ASCII 0x3a ~%za%i~ #8    // icon
  WRITE_ASCII 0x58 ~%z0%d~ #8    // description image
  WRITE_ASCII 0x76 ~%za%i~ #8    // icon (ability)
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=newstring END               // dialogue message
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%z0%b~ END           // visual
  LPF CLONE_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%z0%a~ END           // remove rune effects
  LPF ALTER_EFFECT INT_VAR match_opcode=341 opcode=278 parameter1=1 END            // +1 THAC0
  LPF CLONE_EFFECT INT_VAR match_opcode=278 opcode=277 parameter1=5 END            // +5 thief skills
  LPF CLONE_EFFECT INT_VAR match_opcode=278 opcode=276 parameter1=5 END
  LPF CLONE_EFFECT INT_VAR match_opcode=278 opcode=275 parameter1=5 END
  LPF CLONE_EFFECT INT_VAR match_opcode=278 opcode=92  parameter1=5 END
  LPF CLONE_EFFECT INT_VAR match_opcode=278 opcode=91  parameter1=5 END
  LPF CLONE_EFFECT INT_VAR match_opcode=278 opcode=90  parameter1=5 END
  LPF CLONE_EFFECT INT_VAR match_opcode=278 opcode=59  parameter1=5 END
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END


//Moon
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@70126) STR_VAR bam=~%z0%i~ RET icon END
COPY ~%files%/rune_core.ITM~  ~override/%zb%.ITM~
  SAY NAME1 @70126 SAY UNIDENTIFIED_DESC @70226
  SAY NAME2 @70126 SAY DESC @70226
  WRITE_ASCII 0x3a ~%zb%i~ #8    // icon
  WRITE_ASCII 0x58 ~%z0%d~ #8    // description image
  WRITE_ASCII 0x76 ~%zb%i~ #8    // icon (ability)
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=newstring END                                 // dialogue message
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%z0%b~ END                             // visual
  LPF CLONE_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%z0%a~ END                             // remove rune effects
  LPF ALTER_EFFECT INT_VAR match_opcode=341 opcode=232 parameter2=12 STR_VAR resource=~%zb%a~ END    // +65 XP per kill
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END

COPY ~%files%/rune_effects.SPL~    ~override/%zb%a.SPL~
  LPF ALTER_EFFECT INT_VAR opcode=104 target=3 parameter1=65 timing=9 END


//Milkweed
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@70127) STR_VAR bam=~%z0%i~ RET icon END
COPY ~%files%/rune_werewolf.ITM~  ~override/%zc%.ITM~
  SAY NAME1 @70127 SAY UNIDENTIFIED_DESC @70227
  SAY NAME2 @70127 SAY DESC @70227
  WRITE_ASCII 0x3a ~%zc%i~ #8    // icon
  WRITE_ASCII 0x58 ~%z0%d~ #8    // description image
  WRITE_ASCII 0x76 ~%zc%i~ #8    // icon (ability)
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=newstring END                 // dialogue message
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%z0%b~ END             // visual
  LPF CLONE_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%z0%a~ END             // remove rune effects
  LPF ALTER_EFFECT INT_VAR match_opcode=232 STR_VAR resource=~%zc%a~ END             // polymorph
  LPF CLONE_EFFECT INT_VAR match_opcode=232 STR_VAR resource=~%zc%c~ END             // remove polymorph
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END

COPY ~%files%/rune_effects.SPL~      ~override/%zc%a.SPL~
  LPF ALTER_EFFECT INT_VAR multi_match=1 STR_VAR resource=~SPIN152~ END                   // polymorph
  LPF ADD_SPELL_EFFECT INT_VAR opcode=10 target=2 duration=7200 parameter1=(0 - 2) END    // CON modifier
  LPF ADD_SPELL_EFFECT INT_VAR opcode=49 target=2 duration=7200 parameter1=(0 - 2) END    // WIS modifier
  LPF CLONE_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%zc%b~ insert=~last~ END    // remove ability

COPY ~%passives%/removespell.SPL~    ~override/%zc%b.SPL~
  LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=206 STR_VAR resource=~%zc%a~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=172 timing=4 duration=7 STR_VAR resource=~spin151~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=172 STR_VAR resource=~spin152~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=172 STR_VAR resource=~SPINHUM~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=172 duration=2 END

COPY ~%files%/rune_effects.SPL~      ~override/%zc%c.SPL~
  LPF CLONE_EFFECT INT_VAR match_opcode=326 opcode=321 STR_VAR resource=~%zc%c~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=326 timing=4 duration=15 STR_VAR resource=~%zc%x~ END

COPY ~%files%/rune_effects.SPL~      ~override/%zc%x.SPL~
  LPF CLONE_EFFECT INT_VAR match_opcode=326 opcode=321 STR_VAR resource=~%zc%b~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=326 opcode=321 STR_VAR resource=~%zc%a~ END    // remove stat drain
  LPF CLONE_EFFECT INT_VAR match_opcode=326 opcode=321 STR_VAR resource=~%zc%d~ END    // SCS stat drain
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%humanform%~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=326 opcode=172 STR_VAR resource=~%psionicblast%~ END    // remove psionic blast
  LPF CLONE_EFFECT INT_VAR match_opcode=326 opcode=172 STR_VAR resource=~%psionicscs%~ END      // SCS psionic blast



//Set variables for external use
OUTER_TEXT_SPRINT clawmark        ~%z1%~
OUTER_TEXT_SPRINT bloodrapture    ~%z2%~
OUTER_TEXT_SPRINT oedonwrithe     ~%z3%~
OUTER_TEXT_SPRINT corruption      ~%z4%~
OUTER_TEXT_SPRINT anticlockwise   ~%z5%~
OUTER_TEXT_SPRINT clockwise       ~%z6%~
OUTER_TEXT_SPRINT greatlake       ~%z7%~
OUTER_TEXT_SPRINT fadinglake      ~%z8%~
OUTER_TEXT_SPRINT beastembrace    ~%z9%~
OUTER_TEXT_SPRINT hunter_rune     ~%za%~
OUTER_TEXT_SPRINT moon_rune       ~%zb%~
OUTER_TEXT_SPRINT milkweed        ~%zc%~

OUTER_TEXT_SPRINT clawmarkbleed   ~%z1%b~


/*
*/