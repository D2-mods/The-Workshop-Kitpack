/*
This file adds effects for items from the "Misc items" component.
Will also patch certain kit abilities, but only if the kit is installed.

Caryll runes are in a separate file (miscitemsrunes.tph).
*/


/*
//Crowfeather Cloak (cloak)
//Etherbomb Songbook (quick item)
//Ether Booster (amulet)
//Prisoner's Chain (ring)
//Cruciform (amulet)
//Golem Ring (ring)
//Calamity Ring (ring)
//Stone Ring (ring)
//Flynn's Ring (ring)
//Ether Limiter (amulet)
//Gold Scarab (amulet)
//Winged Sword Insignia (ring)
//Red Tearstone Ring (ring)
//Hunter Bone (quick item)
//Jade Feather (amulet)
//Colorless Demon Soul (consumable)
//Shaman Bone Blade (quick item)
*/


//
//Crowfeather Cloak (cloak)
COPY ~%files%/crowfeather.ITM~  ~override/%m1%.ITM~
  SAY NAME1 @70105 SAY UNIDENTIFIED_DESC @70205
  SAY NAME2 @70105 SAY DESC @70205
  WRITE_ASCII 0x3a ~IBDCLK05~ #8    // icon
  WRITE_ASCII 0x58 ~CDCLCK01~ #8    // description image

ACTION_IF (RESOURCE_CONTAINS ~kit.ids~  ~d2crowhunter~) BEGIN

  // add repeating effect to item
  COPY_EXISTING ~%m1%.ITM~  ~override~
    LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=272 parameter1=3 parameter2=3 STR_VAR resource=~%m1%e~ END
  BUT_ONLY

  // add cloak effect to op321 lists (remove effects)
  COPY_EXISTING ~%visceralattack%.SPL~  ~override~
    LPF CLONE_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%m1%a~ END
    LPF d2_protect_on_castequip INT_VAR duration=10 STR_VAR resource=~%m1%e~ END
  BUT_ONLY

  COPY_EXISTING ~%visceralremove%.SPL~  ~override~
    LPF CLONE_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%m1%a~ END
  BUT_ONLY

  // add cloak subspells
  COPY ~%abilities%/castspellself.EFF~  ~override/%m1%e.EFF~
    WRITE_ASCII 0x30 ~%m1%e~ #8
  COPY ~%abilities%/applyeffects.SPL~   ~override/%m1%e.SPL~
    LPF ALTER_EFFECT INT_VAR probability1=5   STR_VAR resource=~%m1%a~ END    // 5% chance
    LPF CLONE_EFFECT INT_VAR probability1=100 STR_VAR resource=~%m1%x~ END
    LPF d2_remove_effects_on_cast STR_VAR resource=~%m1%a~ END                // remove amulet effects

  COPY_EXISTING ~%visceralattack%.SPL~  ~override/%m1%a.SPL~    // subspell casts amulet effect
    LPF d2_subspell_remove_visuals END    // remove visuals
    WRITE_LONG 0x8 ~-1~                   // remove spell name
    LPF ALTER_EFFECT INT_VAR match_duration=10 duration=15 END
    LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=248 STR_VAR resource=~%m1%b~ END
    LPF DELETE_EFFECT STR_VAR match_resource=~%visceralmulti%~ END

  COPY ~%abilities%/opcode318.SPL~      ~override/%m1%x.SPL~    // subspell blocks amulet effect
    LPF ALTER_EFFECT STR_VAR resource=~%m1%a~ END

  COPY ~%abilities%/applyeffects.EFF~   ~override/%m1%b.EFF~    // "Visceral Attack"
    WRITE_LONG 0x10 139
    WRITE_LONG 0x1c RESOLVE_STR_REF (@114)

  // activate cloak effects (add to damage bonus subspells)
  ACTION_FOR_EACH crowbonus IN ~%crowdamage1%~ ~%crowdamage2%~ ~%crowdamage3%~ BEGIN
    COPY_EXISTING ~%crowbonus%.SPL~  ~override~
      LPF d2_protect_on_castequip INT_VAR timing=9 STR_VAR resource=~%m1%x~ END
    BUT_ONLY
  END

END


//
//Etherbomb Songbook (quick item)
COPY ~%icons%/etherbomb.BAM~  ~override/%m2%.BAM~
COPY ~%files%/etherbomb.ITM~  ~override/%m2%.ITM~
  SAY NAME1 @70106 SAY UNIDENTIFIED_DESC @70206
  SAY NAME2 @70106 SAY DESC @70206
  WRITE_ASCII 0x3a ~%m2%~ #8       // icon
  WRITE_ASCII 0x76 ~%m2%~ #8       // icon (ability)
  WRITE_ASCII 0x58 ~CBOOK89~ #8    // description image
  LPF ALTER_EFFECT INT_VAR parameter1=80 parameter2=~%d2lorecheck%~ STR_VAR resource=~%m2%e~ END    // lore >= 80
  LPF CLONE_EFFECT INT_VAR parameter1=80 parameter2=~%d2loreunder%~ STR_VAR resource=~%m2%s~ END    // lore < 80

COPY ~%abilities%/powderkegcast.SPL~  ~override/%m2%e.SPL~
//  SAY NAME1 @615
  LPF ALTER_EFFECT INT_VAR match_opcode=146 STR_VAR resource=~%m2%f~ END    // hits enemies
  LPF DELETE_EFFECT INT_VAR match_opcode=363 END
  LPF CLONE_EFFECT INT_VAR match_opcode=174 STR_VAR resource=~EFF_M51~ END

COPY ~%abilities%/powderkegcast.SPL~  ~override/%m2%s.SPL~
//  SAY NAME1 @615
  LPF ALTER_EFFECT INT_VAR match_opcode=146 STR_VAR resource=~%m2%t~ END    // hits all
  LPF DELETE_EFFECT INT_VAR match_opcode=363 END
  LPF CLONE_EFFECT INT_VAR match_opcode=174 STR_VAR resource=~EFF_M51~ END

COPY - ~%files%/etherbomb.PRO~        ~%workspace%/%m2%p.PRO~
ADD_PROJECTILE ~%workspace%/%m2%p.PRO~

COPY ~%files%/etherbomb.SPL~  ~override/%m2%f.SPL~    // hits enemies
  SAY NAME1 @615
  WRITE_SHORT 0x98 ~%m2%p~    // projectile
  LPF d2_safe_spell END

COPY ~%files%/etherbomb.SPL~  ~override/%m2%t.SPL~    // hits all
  SAY NAME1 @615
  WRITE_SHORT 0x98 ~%m2%p~    // projectile

//
ACTION_IF (RESOURCE_CONTAINS ~kit.ids~  ~d2powderkeg~) BEGIN

  COPY_EXISTING ~%m2%.ITM~  ~override~
    LPF CLONE_EFFECT 
      INT_VAR multi_match=1 opcode=177 parameter1=IDS_OF_SYMBOL (kit ~D2POWDERKEG~) parameter2=9
      STR_VAR resource=~%m2%u~
    END
  BUT_ONLY

  COPY ~%abilities%/castspellself.EFF~   ~override/%m2%u.EFF~
    WRITE_ASCII 0x30 ~%m2%u~ #8
  COPY ~%abilities%/changebardsong.SPL~  ~override/%m2%u.SPL~    // change Bard Song
    LPF DELETE_EFFECT INT_VAR check_headers=0 END
    LPF DELETE_EFFECT INT_VAR match_opcode=174 END
    LPF ALTER_EFFECT INT_VAR match_opcode=251 STR_VAR resource=~%m2%v~ END
    LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%pkegremove%~ END
    LPF CLONE_EFFECT INT_VAR match_opcode=251 opcode=309 parameter1=1 parameter2=0 timing=9 STR_VAR resource=d2pkeg1 END    // local variable

  COPY ~%abilities%/powderkegnosong.SPL~     ~override/%m2%y.SPL~
    LPF ALTER_EFFECT INT_VAR match_timing=4 STR_VAR resource=~%m2%v~ END
    LPF ALTER_EFFECT INT_VAR match_timing=9 STR_VAR resource=d2spell END

  COPY ~%abilities%/powderkegcast.SPL~       ~override/%m2%v.SPL~
    LPF ALTER_EFFECT INT_VAR match_opcode=146 STR_VAR resource=~%m2%f~ END
    LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%pkegdisable%~ END
    LPF CLONE_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%m2%y~ END
    LPF CLONE_EFFECT INT_VAR match_opcode=174 STR_VAR resource=~EFF_M51~ END

  // removal delayed effects (song switching)
  COPY_EXISTING ~%pkegremove%.SPL~  ~override~
    LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%m2%y~ END
  BUT_ONLY

  // block during cooldown
  COPY_EXISTING ~%pkegdisable%.spl~  ~override~
    LPF d2_protect_on_castequip INT_VAR duration=45 STR_VAR resource=~%m2%v~ END
  BUT_ONLY

END


//
//Ether Booster (amulet)
COPY ~%files%/etherbooster.ITM~  ~override/%m3%.ITM~
  SAY NAME1 @70107 SAY UNIDENTIFIED_DESC @70207
  SAY NAME2 @70107 SAY DESC @70207
  WRITE_ASCII 0x3a ~IAMUL24~ #8    // icon
  WRITE_ASCII 0x58 ~PAMUL26~ #8    // description image
  LPF d2_protect_on_castequip STR_VAR resource=~%m3%x~ END

ACTION_IF (RESOURCE_CONTAINS ~kit.ids~  ~d2warmagician~) BEGIN

  COPY_EXISTING ~%unleashmagic%.SPL~  ~override~
    LPF CLONE_EFFECT 
      INT_VAR check_globals=0 multi_match=1 match_timing=0 opcode=272 parameter1=3 parameter2=3 duration=19
      STR_VAR resource=~%unleashmagic%e~
    END
    LPF CLONE_EFFECT INT_VAR match_opcode=272 opcode=177 timing=1 parameter1=0 parameter2=2 END
  BUT_ONLY

  COPY ~%abilities%/castspellself.EFF~  ~override/%unleashmagic%e.EFF~
    WRITE_ASCII 0x30 ~%unleashmagic%e~ #8
  COPY ~%abilities%/applyeffects.SPL~   ~override/%unleashmagic%e.SPL~
    LPF ALTER_EFFECT STR_VAR resource=~%m3%e~ END
    LPF CLONE_EFFECT STR_VAR resource=~%m3%x~ END
    LPF d2_remove_effects_on_cast STR_VAR resource=~%m3%e~ END    // remove amulet effects

  COPY ~%abilities%/castingspeed1.SPL~   ~override/%m3%e.SPL~     // subspell casts amulet effect
    LPF ALTER_EFFECT INT_VAR parameter1=6 duration=7 END
    LPF ALTER_SPELL_HEADER INT_VAR projectile=1 END

  COPY ~%abilities%/opcode318.SPL~       ~override/%m3%x.SPL~     // subspell blocks amulet effect
    LPF ALTER_EFFECT STR_VAR resource=~%m3%e~ END

END


//
//Prisoner's Chain (ring)
COPY ~%icons%/prisonchain.BAM~  ~override/%m4%.BAM~
COPY ~%files%/prisonchain.ITM~  ~override/%m4%.ITM~
  SAY NAME1 @70108 SAY UNIDENTIFIED_DESC @70208
  SAY NAME2 @70108 SAY DESC @70208
  WRITE_ASCII 0x3a ~%m4%~ #8       // icon
  WRITE_ASCII 0x58 ~CRING29~ #8    // description image


//
//Cruciform (amulet)
COPY ~%icons%/cruciform.BAM~  ~override/%m5%.BAM~
COPY ~%files%/cruciform.ITM~  ~override/%m5%.ITM~
  SAY NAME1 @70109 SAY UNIDENTIFIED_DESC @70209
  SAY NAME2 @70109 SAY DESC @70209
  WRITE_ASCII 0x3a ~%m5%~ #8    // icon
  WRITE_ASCII 0x58 ~%m5%d~ #8    // description image
  LPF d2_protect_on_castequip STR_VAR resource=~%m5%x~ END
  LPF d2_protect_on_castequip STR_VAR resource=~%m5%y~ END

ACTION_IF (RESOURCE_CONTAINS ~kit.ids~  ~d2confessor~) BEGIN

  COPY_EXISTING ~%bloodletter%.SPL~  ~override~
    LPF CLONE_EFFECT 
      INT_VAR check_globals=0 multi_match=1 match_timing=0 opcode=272 parameter1=3 parameter2=3 duration=25
      STR_VAR resource=~%bloodletter%e~
    END
    LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=272 STR_VAR resource=~%bloodletter%f~ END
    LPF CLONE_EFFECT INT_VAR match_opcode=272 opcode=177 timing=1 parameter1=0 parameter2=2 END
  BUT_ONLY

  // main subspells
  COPY ~%abilities%/castspellself.EFF~  ~override/%bloodletter%e.EFF~
    WRITE_ASCII 0x30 ~%bloodletter%e~ #8
  COPY ~%abilities%/applyeffects.SPL~   ~override/%bloodletter%e.SPL~
    LPF ALTER_EFFECT STR_VAR resource=~%m5%e~ END
    LPF CLONE_EFFECT STR_VAR resource=~%m5%x~ END
    LPF d2_remove_effects_on_cast STR_VAR resource=~%m5%e~ END    // remove amulet effects

  COPY ~%abilities%/castspellself.EFF~  ~override/%bloodletter%f.EFF~
    WRITE_ASCII 0x30 ~%bloodletter%f~ #8
  COPY ~%abilities%/applyeffects.SPL~   ~override/%bloodletter%f.SPL~
    LPF ALTER_EFFECT STR_VAR resource=~%m5%f~ END
    LPF CLONE_EFFECT STR_VAR resource=~%m5%y~ END
    LPF d2_remove_effects_on_cast STR_VAR resource=~%m5%f~ END    // remove amulet effects

  // hit effects
  COPY ~%abilities%/hiteffectmelee.SPL~  ~override/%m5%e.SPL~    // subspell casts amulet effect
    LPF ALTER_EFFECT INT_VAR duration=7 STR_VAR resource=~%m5%a~ END
  COPY ~%abilities%/opcode318.SPL~       ~override/%m5%x.SPL~    // subspell blocks amulet effect
    LPF ALTER_EFFECT STR_VAR resource=~%m5%e~ END

  COPY ~%abilities%/hiteffectmelee.SPL~  ~override/%m5%f.SPL~    // subspell casts amulet effect
    LPF ALTER_EFFECT INT_VAR duration=7 STR_VAR resource=~%m5%b~ END
  COPY ~%abilities%/opcode318.SPL~       ~override/%m5%y.SPL~    // subspell blocks amulet effect
    LPF ALTER_EFFECT STR_VAR resource=~%m5%f~ END

  // bleeding effects
  COPY_EXISTING ~%confessorbleed%.EFF~  ~override/%m5%a.EFF~
    WRITE_ASCII 0x30 ~%m5%a~ #8
  COPY_EXISTING ~%confessorbleed%.SPL~  ~override/%m5%a.SPL~
    LPF ALTER_EFFECT STR_VAR match_resource=~%confessorbleed%~ resource=~%m5%a~ END
    LPF DELETE_EFFECT INT_VAR match_opcode=139 END

  COPY_EXISTING ~%confessorbleed%.EFF~  ~override/%m5%b.EFF~
    WRITE_ASCII 0x30 ~%m5%b~ #8
  COPY_EXISTING ~%confessorbleed%.SPL~  ~override/%m5%b.SPL~
    LPF ALTER_EFFECT STR_VAR match_resource=~%confessorbleed%~ resource=~%m5%b~ END
    LPF DELETE_EFFECT INT_VAR match_opcode=139 END

  LAF d2_bleed_removal STR_VAR resource=~%m5%a~ END    // add to d2!bleed.spl
  LAF d2_bleed_effects STR_VAR resource=~%m5%a~ END    // blocked by damage immunity (ex. shield of lathander)

  LAF d2_bleed_removal STR_VAR resource=~%m5%b~ END    // add to d2!bleed.spl
  LAF d2_bleed_effects STR_VAR resource=~%m5%b~ END    // blocked by damage immunity (ex. shield of lathander)

END


//
//Golem Ring (ring)
COPY ~%icons%/golemring.BAM~  ~override/%m6%.BAM~
COPY ~%files%/golemring.ITM~  ~override/%m6%.ITM~
  SAY NAME1 @70110 SAY UNIDENTIFIED_DESC @70210
  SAY NAME2 @70110 SAY DESC @70210
  WRITE_ASCII 0x3a ~%m6%~ #8       // icon
  WRITE_ASCII 0x76 ~%m6%~ #8       // icon (ability)
  WRITE_ASCII 0x58 ~CRING26~ #8    // description image
//  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@304) END    // "Activated Golem Ring"
  LPF ALTER_EFFECT INT_VAR match_opcode=139 opcode=326 STR_VAR resource=~%m6%~ END
  LPF d2_remove_effects_on_cast INT_VAR globals=0 headers=1 326override=1 STR_VAR resource=~d2!slow~ END
  LPF d2_remove_effects_on_cast INT_VAR globals=0 headers=1 326override=1 STR_VAR resource=~d2!haste~ END
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_haste_arrays END
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_slow_arrays END
  LPF d2_string_immunities END
  LPF ee_fixpack_immunities STR_VAR status=~haste slow~ END

COPY ~%icons%/ironflesh.BAM~  ~override/%m6%i.BAM~
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@616) STR_VAR bam=~%m6%i~ RET icon END
COPY ~%files%/golemring.SPL~  ~override/%m6%.SPL~
  SAY NAME1 @70110
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END
//  LPF ALTER_EFFECT INT_VAR match_opcode=176 parameter2=5 END            // movement (set mode 5)
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_haste_arrays END
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_slow_arrays END
  LPF d2_string_immunities END
  LPF ee_fixpack_immunities STR_VAR status=~haste slow~ END
  LPF d2_remove_effects_on_cast END

LAF d2_bleed_protection STR_VAR resource=~%m6%~ END


//
//Calamity Ring (ring)
COPY ~%icons%/calamity.BAM~  ~override/%m7%.BAM~
COPY ~%files%/calamity.ITM~  ~override/%m7%.ITM~
  SAY NAME1 @70111 SAY UNIDENTIFIED_DESC @70211
  SAY NAME2 @70111 SAY DESC @70211
  WRITE_ASCII 0x3a ~%m7%~ #8       // icon
  WRITE_ASCII 0x58 ~CRING36~ #8    // description image


//
//Stone Ring (ring)
COPY ~%icons%/stonering.BAM~  ~override/%m8%.BAM~
COPY ~%files%/stonering.ITM~  ~override/%m8%.ITM~
  SAY NAME1 @70112 SAY UNIDENTIFIED_DESC @70212
  SAY NAME2 @70112 SAY DESC @70212
  WRITE_ASCII 0x3a ~%m8%~ #8       // icon
  WRITE_ASCII 0x58 ~CRING30~ #8    // description image
  LPF ALTER_EFFECT INT_VAR match_opcode=248 STR_VAR resource=~%m8%a~ END    // stun
  LPF ALTER_EFFECT INT_VAR match_opcode=249 STR_VAR resource=~%m8%b~ END    // wing buffet

COPY_EXISTING - ~STUNSTRG.EFF~ ~~    // get string ref for "Stunned" (this file is in every game)
  READ_LONG 0x1c stunstring

COPY ~%abilities%/applyeffects.EFF~  ~override/%m8%a.EFF~    // stun
  WRITE_SHORT 0x2c 15
  WRITE_ASCII 0x30 ~%m8%a~ #8
COPY ~%files%/stoneringstun.SPL~  ~override/%m8%a.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=stunstring END

COPY ~%abilities%/applyeffects.EFF~  ~override/%m8%b.EFF~    // wing buffet
  WRITE_SHORT 0x2c 25
  WRITE_ASCII 0x30 ~%m8%b~ #8
COPY ~%abilities%/shockwavepush.SPL~    ~override/%m8%b.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=235 parameter1=30 END
  LPF ALTER_EFFECT INT_VAR duration=10 END


//
//Flynn's Ring (ring)
COPY ~%files%/flynnsring.EFF~  ~override/%m9%a.EFF~    // block item effects
  WRITE_ASCII 0x30 ~%m9%~ #8
COPY ~%files%/flynnsring.EFF~  ~override/%m9%b.EFF~    // block spell effects
  WRITE_ASCII 0x30 ~%m9%s~ #8

COPY ~%icons%/flynnsring.BAM~  ~override/%m9%.BAM~
COPY ~%files%/flynnsring.ITM~  ~override/%m9%.ITM~
  SAY NAME1 @70113 SAY UNIDENTIFIED_DESC @70213
  SAY NAME2 @70113 SAY DESC @70213
  WRITE_ASCII 0x3a ~%m9%~ #8       // icon
  WRITE_ASCII 0x58 ~CRING06~ #8    // description image
  LPF ALTER_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%m9%~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%m9%s~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=272 STR_VAR resource=~%m9%s~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=183 STR_VAR resource=~%m9%a~ END

COPY ~%abilities%/castspellself.EFF~  ~override/%m9%s.EFF~
  WRITE_ASCII 0x30 ~%m9%s~ #8
COPY ~%files%/flynnsring.SPL~  ~override/%m9%s.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%m9%s~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=183 STR_VAR resource=~%m9%b~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=206 STR_VAR resource=~%m9%s~ END


//
//Ether Limiter (amulet)
COPY ~%files%/etherlimiter.ITM~  ~override/%ma%.ITM~
  SAY NAME1 @70128 SAY UNIDENTIFIED_DESC @70228
  SAY NAME2 @70128 SAY DESC @70228
  WRITE_ASCII 0x3a ~IAMUL19~ #8    // icon
  WRITE_ASCII 0x58 ~CAMUL20~ #8    // description image
  LPF d2_protect_on_castequip STR_VAR resource=~%shockwaveeffects%~ END    // block base subspells
  LPF d2_protect_on_castequip STR_VAR resource=~%incinerateeffects%~ END

//Shockwave
ACTION_IF (FILE_EXISTS ~override/%shockwave%.SPL~) BEGIN

  COPY_EXISTING ~%shockwave%.SPL~  ~override~
    LPF CLONE_EFFECT STR_VAR match_resource=~%shockwaveeffects%~ resource=~%ma%a~ insert=below END
  BUT_ONLY

  // amulet subspells
  COPY_EXISTING ~%shockwaveeffects%.SPL~  ~override/%ma%a.SPL~
    LPF ALTER_EFFECT STR_VAR match_resource=~%shockwaveblast%~ resource=~%ma%b~ END

  COPY ~%abilities%/castspelltarget.EFF~  ~override/%ma%b.EFF~
    WRITE_ASCII 0x30 ~%ma%b~ #8
  COPY_EXISTING ~%shockwaveblast%.SPL~    ~override/%ma%b.SPL~
    SAY NAME1 @102
    LPF DELETE_EFFECT INT_VAR match_parameter2=63 END
    LPF DELETE_EFFECT INT_VAR match_parameter2=43 END
    LPF d2_safe_spell STR_VAR insert=above END

  // base subspell blocks amulet effects
  COPY_EXISTING ~%shockwaveeffects%.SPL~  ~override~
    LPF d2_protect_on_castequip INT_VAR duration=1 STR_VAR resource=~%ma%a~ END
  BUT_ONLY

  // remove effects
  COPY_EXISTING ~%warhoundremove%.SPL~  ~override~
    LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%ma%a~ END
  BUT_ONLY

END

//Incinerate
ACTION_IF (FILE_EXISTS ~override/%incinerate%.SPL~) BEGIN

  COPY_EXISTING ~%incinerate%.SPL~  ~override~
    LPF CLONE_EFFECT STR_VAR match_resource=~%incinerateeffects%~ resource=~%ma%e~ insert=below END
  BUT_ONLY

  // amulet subspells
  COPY_EXISTING ~%incinerateeffects%.SPL~  ~override/%ma%e.SPL~
    LPF ALTER_EFFECT STR_VAR match_resource=~%incinerateblast%~ resource=~%ma%f~ END

  COPY ~%abilities%/castspelltarget.EFF~   ~override/%ma%f.EFF~
    WRITE_ASCII 0x30 ~%ma%f~ #8
  COPY_EXISTING ~%incinerateblast%.SPL~    ~override/%ma%f.SPL~
    SAY NAME1 @103
    LPF DELETE_EFFECT INT_VAR match_parameter2=63 END
    LPF DELETE_EFFECT INT_VAR match_parameter2=43 END
    LPF d2_safe_spell STR_VAR insert=above END

  // base subspell blocks amulet effects
  COPY_EXISTING ~%incinerateeffects%.SPL~  ~override~
    LPF d2_protect_on_castequip INT_VAR duration=1 STR_VAR resource=~%ma%e~ END
  BUT_ONLY

  // remove effects
  COPY_EXISTING ~%warhoundremove%.SPL~  ~override~
    LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%ma%e~ END
  BUT_ONLY

END


//
//Gold Scarab (amulet)
COPY ~%icons%/goldscarab.BAM~  ~override/%mb%.BAM~
COPY ~%files%/goldscarab.ITM~  ~override/%mb%.ITM~
  SAY NAME1 @70131 SAY UNIDENTIFIED_DESC @70231
  SAY NAME2 @70131 SAY DESC @70231
  WRITE_ASCII 0x3a ~%mb%~ #8       // icon
  WRITE_ASCII 0x58 ~CAMUL16~ #8    // description image
  LPF ALTER_EFFECT INT_VAR match_opcode=232 STR_VAR resource=~%mb%e~ END

COPY ~%files%/goldscarab.SPL~  ~override/%mb%e.SPL~


//
//Winged Sword Insignia (ring)
COPY ~%icons%/wingedsword.BAM~  ~override/%mc%.BAM~
COPY ~%files%/wingedsword.ITM~  ~override/%mc%.ITM~
  SAY NAME1 @70132 SAY UNIDENTIFIED_DESC @70232
  SAY NAME2 @70132 SAY DESC @70232
  WRITE_ASCII 0x3a ~%mc%~ #8       // icon
  WRITE_ASCII 0x58 ~CRING38~ #8    // description image
  LPF ALTER_EFFECT INT_VAR match_opcode=248 STR_VAR resource=~%mc%e~ END

COPY ~%abilities%/castspellself.EFF~  ~override/%mc%e.EFF~    // +1 damage bonus
  WRITE_ASCII 0x30 ~%mc%e~ #8
COPY ~%abilities%/damageup1.SPL~      ~override/%mc%e.SPL~
  LPF ALTER_EFFECT INT_VAR probability1=33 END


//
//Red Tearstone Ring (ring)
COPY ~%icons%/redtearstoneicon.BAM~  ~override/%md%i.BAM~
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@70133) STR_VAR bam=~%md%i~ RET icon END

COPY ~%icons%/redtearstone.BAM~  ~override/%md%.BAM~
COPY ~%files%/redtearstone.ITM~  ~override/%md%.ITM~
  SAY NAME1 @70133 SAY UNIDENTIFIED_DESC @70233
  SAY NAME2 @70133 SAY DESC @70233
  WRITE_ASCII 0x3a ~%md%~ #8       // icon
  WRITE_ASCII 0x58 ~CMISC8O~ #8    // description image
  LPF ALTER_EFFECT INT_VAR match_opcode=232 STR_VAR resource=~%md%e~ END

COPY ~%files%/redtearstone.SPL~  ~override/%md%e.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%md%e~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END


//
//Hunter Bone (quick item)
DEFINE_PATCH_FUNCTION hunterbone_haste BEGIN
  LPF d2_subspell_remove_visuals INT_VAR icon=0 END
  LPF ALTER_EFFECT INT_VAR silent=1 match_opcode=16 duration=20 END
  LPF ALTER_EFFECT INT_VAR silent=1 match_opcode=142 duration=20 END
  LPF ALTER_EFFECT INT_VAR silent=1 match_opcode=318 STR_VAR resource=~%DEST_RES%~ END
END

COPY ~%icons%/hunterbone.BAM~  ~override/%me%.BAM~
COPY ~%files%/hunterbone.ITM~  ~override/%me%.ITM~
  SAY NAME1 @70134 SAY UNIDENTIFIED_DESC @70234
  SAY NAME2 @70134 SAY DESC @70234
  WRITE_ASCII 0x3a ~%me%~ #8     // icon
  WRITE_ASCII 0x76 ~%me%~ #8     // icon (ability)
  WRITE_ASCII 0x58 ~%me%d~ #8    // description image
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%me%a~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%me%b~ END
//  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@312) END    // "Used Hunter Bone"
  LPF DELETE_EFFECT INT_VAR match_opcode=139 END

//clone Blur
COPY_EXISTING ~SPWI201.SPL~  ~override/%me%a.SPL~
  LPF d2_remove_level_headers END
  LPF d2_subspell_remove_effects END
  LPF d2_subspell_remove_visuals INT_VAR visual=0 icon=0 END

  SAY NAME1 @70134
  LPF DELETE_EFFECT INT_VAR match_opcode=321 END
  LPF ALTER_EFFECT INT_VAR match_timing=0 duration=20 END
  LPF d2_remove_effects_on_cast END

//clone Haste
COPY_EXISTING ~SPWI305.SPL~  ~override/%me%b.SPL~
  LPF d2prep_haste END
  LPF hunterbone_haste END
  TEXT_SPRINT $d2statusarray(~%DEST_RES%~) ~haste~

  // edits
  LPF ALTER_EFFECT INT_VAR silent=1 STR_VAR match_resource=SPWI305A resource=~%me%c~ END    // for SCS/ToF
  LPF d2_remove_effects_on_cast INT_VAR 326override=1 STR_VAR resource=~d2!slow~ END

//make haste subspell (for SCS/ToF)
COPY_EXISTING ~SPWI305A.SPL~  ~override/%me%c.SPL~
  LPF d2prep_haste END
  LPF hunterbone_haste END
  TEXT_SPRINT $d2statusarray(~%DEST_RES%~) ~haste~
IF_EXISTS


//
//Jade Feather (amulet)
COPY ~%icons%/jadefeather.BAM~  ~override/%mf%.BAM~
COPY ~%files%/jadefeather.ITM~  ~override/%mf%.ITM~
  SAY NAME1 @70135 SAY UNIDENTIFIED_DESC @70235
  SAY NAME2 @70135 SAY DESC @70235
  WRITE_ASCII 0x3a ~%mf%~ #8        // icon
  WRITE_ASCII 0x58 ~BDCFEATH~ #8    // description image
  LPF d2_protect_on_castequip STR_VAR resource=~%blackremedyeffects%~ END    // block base subspells
  LPF d2_protect_on_castequip STR_VAR resource=~%whiteremedyeffects%~ END

  LPF cd_apply_batch STR_VAR array_name = cd_immunity_kill_target_arrays END
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_slay_arrays END
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_petrification_arrays END
  LPF d2_string_immunities END
  LPF ee_fixpack_immunities STR_VAR status=~death~ END
  LPF block_death_gaze END

//Black Remedy
ACTION_IF (FILE_EXISTS ~override/%blackremedy%.SPL~) BEGIN

  COPY_EXISTING ~%blackremedy%.SPL~  ~override~
    LPF CLONE_EFFECT STR_VAR match_resource=~%blackremedyeffects%~ resource=~%mf%a~ insert=below END
  BUT_ONLY

  // amulet subspells
  COPY_EXISTING ~%blackremedyeffects%.SPL~  ~override/%mf%a.SPL~
    LPF ALTER_EFFECT STR_VAR match_resource=~%blackremedyblast%~ resource=~%mf%b~ END

  COPY ~%abilities%/castspelltarget.EFF~  ~override/%mf%b.EFF~
    WRITE_ASCII 0x30 ~%mf%b~ #8
  COPY_EXISTING ~%blackremedyblast%.SPL~  ~override/%mf%b.SPL~
    LPF ALTER_SPELL_HEADER INT_VAR projectile=~%remedy15%~ END    // 15-ft. radius
    LPF ALTER_EFFECT STR_VAR match_resource=~%blackremedyblast%~ resource=~%mf%b~ END

  // base subspell blocks amulet effects
  COPY_EXISTING ~%blackremedyeffects%.SPL~  ~override~
    LPF d2_protect_on_castequip INT_VAR duration=1 STR_VAR resource=~%mf%a~ END
  BUT_ONLY

  // remove effects
  COPY_EXISTING ~%medicremove%.SPL~  ~override~
    LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%mf%a~ END
  BUT_ONLY

END

//White Remedy
ACTION_IF (FILE_EXISTS ~override/%whiteremedy%.SPL~) BEGIN

  COPY_EXISTING ~%whiteremedy%.SPL~  ~override~
    LPF CLONE_EFFECT STR_VAR match_resource=~%whiteremedyeffects%~ resource=~%mf%e~ insert=below END
  BUT_ONLY

  // amulet subspells
  COPY_EXISTING ~%whiteremedyeffects%.SPL~  ~override/%mf%e.SPL~
    LPF ALTER_EFFECT STR_VAR match_resource=~%whiteremedyblast%~ resource=~%mf%f~ END

  COPY ~%abilities%/castspelltarget.EFF~  ~override/%mf%f.EFF~
    WRITE_ASCII 0x30 ~%mf%f~ #8
  COPY_EXISTING ~%whiteremedyblast%.SPL~  ~override/%mf%f.SPL~
    LPF ALTER_SPELL_HEADER INT_VAR projectile=~%remedy15%~ END    // 15-ft. radius
    LPF ALTER_EFFECT STR_VAR match_resource=~%whiteremedyblast%~ resource=~%mf%f~ END

  // base subspell blocks amulet effects
  COPY_EXISTING ~%whiteremedyeffects%.SPL~  ~override~
    LPF d2_protect_on_castequip INT_VAR duration=1 STR_VAR resource=~%mf%e~ END
  BUT_ONLY

  // remove effects
  COPY_EXISTING ~%medicremove%.SPL~  ~override~
    LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%mf%e~ END
  BUT_ONLY

END


//
//Colorless Demon Soul (consumable)
COPY ~%icons%/demonsoul.BAM~  ~override/%mg%.BAM~
COPY ~%files%/demonsoul.ITM~  ~override/%mg%.ITM~
  SAY NAME1 @70136 SAY UNIDENTIFIED_DESC @70236
  SAY NAME2 @70136 SAY DESC @70236
  WRITE_ASCII 0x3a ~%mg%~ #8     // icon
  WRITE_ASCII 0x76 ~%mg%~ #8     // icon (ability)
  WRITE_ASCII 0x58 ~%mg%d~ #8    // description image
  LPF CLONE_EFFECT INT_VAR match_opcode=49 opcode=44 parameter1=1 END

  // color string
  LPF d2_color_string INT_VAR stringref=RESOLVE_STR_REF (@313) STR_VAR color=gold RET newstring END
  LPF ALTER_EFFECT INT_VAR match_opcode=139 opcode=330 parameter1=newstring timing=4 duration=2 END


//
//Shaman Bone Blade (quick item)
COPY ~%icons%/shamanbone.BAM~  ~override/%mh%.BAM~
COPY ~%files%/shamanbone.ITM~  ~override/%mh%.ITM~
  SAY NAME1 @70137 SAY UNIDENTIFIED_DESC @70237
  SAY NAME2 @70137 SAY DESC @70237
  WRITE_ASCII 0x3a ~%mh%~ #8     // icon
  WRITE_ASCII 0x76 ~%mh%~ #8     // icon (ability)
  WRITE_ASCII 0x58 ~%mh%d~ #8    // description image
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%mh%b~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%mh%a~ END
  LPF d2_protect_on_castequip INT_VAR headers=1 cre_type=55 duration=0 STR_VAR insert=first END
  LPF d2_protect_on_castequip INT_VAR headers=1 cre_type=19 probability=30 duration=0 STR_VAR insert=below END
  LPF d2_protect_on_castequip INT_VAR headers=1 cre_type=15 probability=90 duration=0 STR_VAR insert=below END
  LPF CLONE_EFFECT INT_VAR match_opcode=318 opcode=326 STR_VAR resource=~%mh%c~ END    // immune string

//immune string
OUTER_SPRINT ability_name @70137
COPY ~%abilities%/spellfailed.SPL~  ~override/%mh%c.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@350) END    // "Unaffected by..."
//  LPF DELETE_EFFECT INT_VAR match_opcode=174 END

//charm subspell
COPY ~%files%/shamanbonea.SPL~  ~override/%mh%a.SPL~
  SAY NAME1 @70137
  LPF ALTER_EFFECT INT_VAR match_opcode=318 STR_VAR resource=~%mh%b~ END    // block berserk if save failed
  LPF DELETE_EFFECT INT_VAR match_opcode=324 END
//  LPF ALTER_EFFECT INT_VAR match_duration=24 duration=48 END                // duration 8 rounds
  LPF d2_remove_effects_on_cast END
  LPF d2_remove_effects_on_cast STR_VAR resource=~%mh%b~ END                // overwrite berserk subspell
  // repeating charm
  LPF CLONE_EFFECT INT_VAR match_opcode=5 opcode=272 duration=45 parameter1=1 parameter2=0 STR_VAR resource=~%mh%a~ END

//repeating charm (no message)
COPY ~%abilities%/charm7seconds.EFF~  ~override/%mh%a.EFF~
    WRITE_LONG 0x20 1002
    WRITE_LONG 0x28 5

//berserk subspell
COPY ~%files%/shamanboneb.SPL~  ~override/%mh%b.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@311) END    // "Berserk!"
  TEXT_SPRINT $d2statusarray(~%DEST_RES%~) ~berserk~

//add to status cures (delayed sound effect)
LAF ee_fixpack_add_status STR_VAR resource=~%mh%b~ status=~berserk~ END    // berserk removed by EEFP subspell

ACTION_PHP_EACH d2restoration AS spell => type BEGIN
  ACTION_IF (~%type%~ STR_EQ ~clarity~) BEGIN
    COPY_EXISTING ~%spell%.spl~  ~override~
      LPF d2_remove_effects_on_cast STR_VAR resource=~%mh%a~ END
    IF_EXISTS BUT_ONLY
  END
END


//
//function for Evasion (used later)
DEFINE_ACTION_FUNCTION miscitemsevasion BEGIN
  LAF d2_evasion_check INT_VAR clone=1 STR_VAR spell=~%m2%f~ END    // Etherbomb Song
  LAF d2_evasion_check INT_VAR clone=1 STR_VAR spell=~%m2%t~ END
//LAF d2_evasion_check INT_VAR clone=1 STR_VAR spell=~%ma%b~ END    // Shockwave (v4.8: no evasion)
  LAF d2_evasion_check INT_VAR clone=1 STR_VAR spell=~%ma%f~ END    // Incinerate
END


//
//Set variables for external use
OUTER_TEXT_SPRINT crowfeather    ~%m1%~
OUTER_TEXT_SPRINT etherbomb      ~%m2%~
OUTER_TEXT_SPRINT etherbooster   ~%m3%~
OUTER_TEXT_SPRINT prisonchain    ~%m4%~
OUTER_TEXT_SPRINT cruciform      ~%m5%~
OUTER_TEXT_SPRINT golemring      ~%m6%~
OUTER_TEXT_SPRINT calamity       ~%m7%~
OUTER_TEXT_SPRINT stonering      ~%m8%~
OUTER_TEXT_SPRINT flynnring      ~%m9%~
OUTER_TEXT_SPRINT etherlimiter   ~%ma%~
OUTER_TEXT_SPRINT goldscarab     ~%mb%~
OUTER_TEXT_SPRINT wingedsword    ~%mc%~
OUTER_TEXT_SPRINT redtearstone   ~%md%~
OUTER_TEXT_SPRINT hunterbone     ~%me%~
OUTER_TEXT_SPRINT jadefeather    ~%mf%~
OUTER_TEXT_SPRINT demonsoul      ~%mg%~
OUTER_TEXT_SPRINT shamanbone     ~%mh%~

OUTER_TEXT_SPRINT bleeding1      ~%m5%a~
OUTER_TEXT_SPRINT bleeding2      ~%m5%b~


/*
*/