//Powder Keg (d2wkb)


//Fire resist
COPY ~%passives%/resistup20fire.SPL~  ~override/%e1%.SPL~

//Change bard song at level 1 (Blasting Song)
COPY_EXISTING ~FJBARD.SPL~            ~override/%e2%.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=251 STR_VAR resource=~%s1%s~ END
  LPF DELETE_EFFECT STR_VAR match_resource=~#bard7a~ END    // compat fix for IWDification

//other new songs not automatically set, except HLA song


//
//text strings
OUTER_SET deafness  = 14073
OUTER_SET confused  = 14782
OUTER_SET panic     = 14007
OUTER_SET berserk   = RESOLVE_STR_REF (@311)

ACTION_IF (GAME_IS ~bgee~) BEGIN
  OUTER_SET confused  = 25807
  OUTER_SET panic     = 25818
END

ACTION_IF (GAME_IS ~iwdee~) BEGIN
  OUTER_SET confused  = 37604
  OUTER_SET panic     = 35484
END

//deafness and berserk strings need to be blocked by immunity to those statuses


//icons
ACTION_IF (nopowderkegpatch = 0) BEGIN
  COPY ~%icons%/powderkeg1.BAM~  ~override/%s1%.BAM~
  COPY ~%icons%/powderkeg2.BAM~  ~override/%s2%.BAM~
  COPY ~%icons%/powderkeg3.BAM~  ~override/%s3%.BAM~
  COPY ~%icons%/powderkeg4.BAM~  ~override/%s4%.BAM~
  OUTER_TEXT_SPRINT icon1 ~%s1%~
  OUTER_TEXT_SPRINT icon2 ~%s2%~
  OUTER_TEXT_SPRINT icon3 ~%s3%~
  OUTER_TEXT_SPRINT icon4 ~%s4%~
END ELSE BEGIN
  OUTER_TEXT_SPRINT icon1 ~bard6~
  OUTER_TEXT_SPRINT icon2 ~bard4~
  OUTER_TEXT_SPRINT icon3 ~bard3~
  OUTER_TEXT_SPRINT icon4 ~bard2~
END


//add "Change Song" abilities
COPY ~%abilities%/changebardsong.SPL~  ~override/%s1%.SPL~    // Blasting Song
  SAY NAME1 @115
  SAY UNIDENTIFIED_DESC @215
  LPF ALTER_EFFECT INT_VAR match_opcode=251 STR_VAR resource=~%s1%s~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%s0%a~ END
  LPF ALTER_EFFECT INT_VAR check_headers=0 STR_VAR resource=~%s1%~ END
  WRITE_ASCII 0x3a ~%icon1%~ #8    // icons
  WRITE_ASCII 0x76 ~%icon1%~ #8

COPY ~%abilities%/changebardsong.SPL~  ~override/%s2%.SPL~    // Booming Song
  SAY NAME1 @116
  SAY UNIDENTIFIED_DESC @216
  LPF ALTER_EFFECT INT_VAR match_opcode=251 STR_VAR resource=~%s2%s~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%s0%a~ END
  LPF ALTER_EFFECT INT_VAR check_headers=0 STR_VAR resource=~%s2%~ END
  WRITE_ASCII 0x3a ~%icon2%~ #8    // icons
  WRITE_ASCII 0x76 ~%icon2%~ #8

COPY ~%abilities%/changebardsong.SPL~  ~override/%s3%.SPL~    // Dust Explosion
  SAY NAME1 @117
  SAY UNIDENTIFIED_DESC @217
  LPF ALTER_EFFECT INT_VAR match_opcode=251 STR_VAR resource=~%s3%s~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%s0%a~ END
  LPF ALTER_EFFECT INT_VAR check_headers=0 STR_VAR resource=~%s3%~ END
  WRITE_ASCII 0x3a ~%icon3%~ #8    // icons
  WRITE_ASCII 0x76 ~%icon3%~ #8

COPY ~%abilities%/changebardsong.SPL~  ~override/%s4%.SPL~    // Frenzying Flame
  SAY NAME1 @118
  SAY UNIDENTIFIED_DESC @218
  LPF ALTER_EFFECT INT_VAR match_opcode=251 STR_VAR resource=~%s4%s~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%s0%a~ END
  LPF ALTER_EFFECT INT_VAR check_headers=0 STR_VAR resource=~%s4%~ END
  WRITE_ASCII 0x3a ~%icon4%~ #8    // icons
  WRITE_ASCII 0x76 ~%icon4%~ #8

COPY ~%abilities%/changebardsong.SPL~  ~override/%s5%.SPL~    // Enhanced Bard Song
  SAY NAME1 @119
  SAY UNIDENTIFIED_DESC @219
  LPF ALTER_EFFECT INT_VAR match_opcode=251 STR_VAR resource=~SPCL920A~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%s0%a~ END
  LPF ALTER_EFFECT INT_VAR check_headers=0 STR_VAR resource=~%s5%~ END
  WRITE_ASCII 0x3a ~SPENBARB~ #8    // icons
  WRITE_ASCII 0x76 ~SPENBARB~ #8


//shared subspells
COPY ~%abilities%/removespelleffects.SPL~  ~override/%s0%a.SPL~    // remove delayed effect
  LPF ALTER_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%s1%y~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%s2%y~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%s3%y~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%s4%y~ END

COPY ~%abilities%/powderkegdisable.SPL~    ~override/%s0%x.SPL~    // disable song button


//
//Add Blasting Song
COPY ~%abilities%/powderkegnosong.SPL~     ~override/%s1%y.SPL~
  LPF ALTER_EFFECT INT_VAR match_timing=4 STR_VAR resource=~%s1%s~ END
  LPF ALTER_EFFECT INT_VAR match_timing=9 STR_VAR resource=d2spell END

COPY ~%abilities%/powderkegcast.SPL~       ~override/%s1%s.SPL~
//  SAY NAME1 @607
  LPF ALTER_EFFECT INT_VAR match_opcode=146 STR_VAR resource=~%s1%f~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=146 timing=4 duration=1 STR_VAR resource=~%s1%e~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%s0%x~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%s1%y~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=174 STR_VAR resource=~#EFF_P35~ END

COPY - ~%abilities%/powderkegflare.PRO~ ~%workspace%/%s1%f.PRO~
ADD_PROJECTILE ~%workspace%/%s1%f.PRO~
COPY ~%abilities%/powderkegblast.SPL~      ~override/%s1%f.SPL~    // initial flare (2 fire damage)
  WRITE_SHORT 0x98 ~%s1%f~    // projectile
  LPF DELETE_EFFECT INT_VAR match_opcode=318 END
  LPF d2_safe_spell END
  LPF ALTER_EFFECT INT_VAR match_opcode=12 parameter1=2 dicenumber=0 dicesize=0 savingthrow=16777216 special=0 END

COPY - ~%abilities%/powderkegblast.PRO~ ~%workspace%/%s1%e.PRO~
ADD_PROJECTILE ~%workspace%/%s1%e.PRO~
COPY ~%abilities%/powderkegblast.SPL~      ~override/%s1%e.SPL~    // follow up blast (1d4 per level)
  SAY NAME1 @607
  LPF ALTER_EFFECT INT_VAR match_opcode=318 STR_VAR resource=~%s1%e~ END
  WRITE_SHORT 0x98 ~%s1%e~    // projectile

  //damage scales to level 20 (header 19)
  LPF CD_EXTEND-O-MATIC  INT_VAR level_cap = 20  END
  FOR (n = 1; n <= 19; ++n) BEGIN
    LPF ALTER_EFFECT INT_VAR header=n match_opcode=12 dicenumber=(n+1) END
  END


//
//Add Booming Song
COPY ~%abilities%/powderkegnosong.SPL~     ~override/%s2%y.SPL~
  LPF ALTER_EFFECT INT_VAR match_timing=4 STR_VAR resource=~%s2%s~ END
  LPF ALTER_EFFECT INT_VAR match_timing=9 STR_VAR resource=d2spell END

COPY ~%abilities%/powderkegcast.SPL~       ~override/%s2%s.SPL~
//  SAY NAME1 @608
  LPF ALTER_EFFECT INT_VAR match_opcode=146 STR_VAR resource=~%s2%e~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%s0%x~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%s2%y~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=174 STR_VAR resource=~EFF_M43~ END

COPY - ~%abilities%/powderkegboom2.PRO~ ~%workspace%/%s2%e.PRO~
  WRITE_ASCII 0x228 ~%s2%e~ #8
  WRITE_ASCII 0x230 ~%s2%e~ #8
ADD_PROJECTILE ~%workspace%/%s2%e.PRO~
COPY ~%abilities%/powderkegboom.BAM~ ~override/%s2%e.BAM~    // edit color?

COPY ~%abilities%/powderkegboom.SPL~       ~override/%s2%e.SPL~
  SAY NAME1 @608
  LPF d2_safe_spell END
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%s2%a~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%s2%b~ END
  WRITE_SHORT 0x98 ~%s2%e~    // projectile

COPY ~%abilities%/powderkegboomd.SPL~      ~override/%s2%a.SPL~    // deafness 2 rounds
  LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=139 parameter1=deafness STR_VAR insert=last END
  LPF DELETE_EFFECT INT_VAR match_opcode=206 END

COPY ~%abilities%/powderkegboomd.SPL~      ~override/%s2%b.SPL~    // deafness 4 rounds
  LPF ALTER_EFFECT INT_VAR match_opcode=206 STR_VAR resource=~%s2%a~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=139 parameter1=deafness STR_VAR insert=last END
  LPF ALTER_EFFECT INT_VAR savingthrow=1 END
  LPF ALTER_EFFECT INT_VAR match_duration=12 duration=24 END


//
//Add Dust Explosion
COPY ~%abilities%/powderkegnosong.SPL~   ~override/%s3%y.SPL~
  LPF ALTER_EFFECT INT_VAR match_timing=4 STR_VAR resource=~%s3%s~ END
  LPF ALTER_EFFECT INT_VAR match_timing=9 STR_VAR resource=d2spell END

COPY ~%abilities%/powderkegcast.SPL~     ~override/%s3%s.SPL~
//  SAY NAME1 @609
  LPF ALTER_EFFECT INT_VAR multi_match=1 match_opcode=146 STR_VAR resource=~%s3%e~ END    // cloud
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=146 STR_VAR resource=~%s3%f~ END    // fake projectile
//  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=146 STR_VAR resource=~%s3%f~ END    // fake projectile
//  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=146 STR_VAR resource=~%s3%f~ END    // fake projectile
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=146 STR_VAR resource=~%s3%d~ END    // fireball
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%s0%x~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%s3%y~ END

COPY - ~%abilities%/powderkegdust.PRO~ ~%workspace%/%s3%e.PRO~
  WRITE_ASCII 0x208 ~%s3%e~ #8
  WRITE_ASCII 0x228 ~%s3%e~ #8
ADD_PROJECTILE ~%workspace%/%s3%e.PRO~
OUTER_SET dustcloud = (~%s3%e~ - 1)    // for clearair.2da
COPY ~%abilities%/powderkegdust.BAM~ ~override/%s3%e.BAM~
COPY ~%abilities%/powderkegdust.WAV~ ~override/%s3%e.WAV~

COPY ~%abilities%/powderkegdust.SPL~     ~override/%s3%e.SPL~
  LPF ALTER_EFFECT INT_VAR multi_match=1 match_opcode=326 STR_VAR resource=~%s3%a~ END    // blindness
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=326 STR_VAR resource=~%s3%b~ END    // blur/invisible
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=326 STR_VAR resource=~%s3%c~ END    // move penalty
  WRITE_SHORT 0x98 ~%s3%e~    // projectile

COPY ~%abilities%/powderkegdust.SPL~     ~override/%s3%f.SPL~    // fake projectile (for visuals)
  WRITE_SHORT 0x98 ~%s3%e~    // projectile

COPY ~%abilities%/powderkegdusta.SPL~    ~override/%s3%a.SPL~    // blindness
COPY ~%abilities%/powderkegdustb.SPL~    ~override/%s3%b.SPL~    // blur/invisible
  LPF ALTER_EFFECT INT_VAR duration=15 END
COPY ~%abilities%/powderkegdustc.SPL~    ~override/%s3%c.SPL~    // move penalty
  LPF d2_remove_effects_on_cast INT_VAR 326override=1 STR_VAR resource=~d2!haste~ END    // remove haste
  LPF d2_safe_spell INT_VAR block_opcode=176 duration=1 END

COPY - ~%abilities%/powderkegdustd.PRO~ ~%workspace%/%s3%d.PRO~    // fireball
ADD_PROJECTILE ~%workspace%/%s3%d.PRO~
COPY ~%abilities%/powderkegdustd.SPL~    ~override/%s3%d.SPL~
  SAY NAME1 @609
  WRITE_SHORT 0x98 ~%s3%d~    // projectile
  LPF d2_safe_spell END


//
//Add Frenzying Flame
COPY ~%abilities%/powderkegnosong.SPL~   ~override/%s4%y.SPL~
  LPF ALTER_EFFECT INT_VAR match_timing=4 STR_VAR resource=~%s4%s~ END
  LPF ALTER_EFFECT INT_VAR match_timing=9 STR_VAR resource=d2spell END

COPY ~%abilities%/powderkegcast.SPL~     ~override/%s4%s.SPL~
//  SAY NAME1 @610
  LPF ALTER_EFFECT INT_VAR match_opcode=146 STR_VAR resource=~%s4%e~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%s0%x~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%s4%y~ END

COPY - ~%abilities%/powderkegfrenzy.PRO~ ~%workspace%/%s4%e.PRO~
ADD_PROJECTILE ~%workspace%/%s4%e.PRO~
OUTER_SET frenzycloud = (~%s4%e~ - 1)    // for clearair.2da

COPY ~%abilities%/powderkegfrenzy.SPL~   ~override/%s4%e.SPL~
  SAY NAME1 @610
  LPF d2_safe_spell INT_VAR duration=1 END
  LPF ALTER_EFFECT INT_VAR multi_match=1 match_opcode=326 probability1=24 probability2=0  STR_VAR resource=~%s4%a~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=326 probability1=49 probability2=25 STR_VAR resource=~%s4%b~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=326 probability1=74 probability2=50 STR_VAR resource=~%s4%c~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=326 probability1=99 probability2=75 STR_VAR resource=~%s4%d~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=206 STR_VAR resource=~%s4%e~ END
  WRITE_SHORT 0x98 ~%s4%e~    // projectile

COPY ~%abilities%/powderkegfrenzya.SPL~  ~override/%s4%a.SPL~
  LPF d2_sleep_elfresist END
  LPF d2_sleep_string END
COPY ~%abilities%/powderkegfrenzyb.SPL~  ~override/%s4%b.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=confused END    // "Confused"
COPY ~%abilities%/powderkegfrenzyc.SPL~  ~override/%s4%c.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=panic END       // "Panic"
COPY ~%abilities%/powderkegfrenzyd.SPL~  ~override/%s4%d.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=berserk END     // "Berserk"
  TEXT_SPRINT $d2statusarray(~%DEST_RES%~) ~berserk~


//clearair.2da
APPEND ~CLEARAIR.2DA~  ~d2dustexplosion %dustcloud%~
//APPEND ~CLEARAIR.2DA~  ~d2frenzyingflame %frenzycloud%~

COPY_EXISTING ~CLEARAIR.2DA~  ~override~ 
  PRETTY_PRINT_2DA
BUT_ONLY


//
//Make clab table
COPY ~inlined/d2clab_bard_nosongs~  ~override/%kitid%.2DA~
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e1%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e2%~ END
  LPF d2_kit_ability_adder INT_VAR level=1 STR_VAR resource=~%s1%~ END
  LPF d2_kit_ability_adder INT_VAR level=5 STR_VAR resource=~%s2%~ END
  LPF d2_kit_ability_adder INT_VAR level=10 STR_VAR resource=~%s3%~ END
  LPF d2_kit_ability_adder INT_VAR level=15 STR_VAR resource=~%s4%~ END
  LPF d2_kit_ability_adder END


//Kit function
LAF ADD_KIT_EX
  INT_VAR
    kit_class = 5
    mixed = RESOLVE_STR_REF (@101101)
    lower = RESOLVE_STR_REF (@101102)
    help = RESOLVE_STR_REF (@101103)
  STR_VAR
    kit_name = ~D2%kitname%~
    clab_path = ~%kitid%~
    clsrcreq = ~1 1 1 1 1 1 1~       // Usable by all races, if base class is usable
    thiefscl = ~25 0 0 0 0 0 0 0~    // 25% Pick Pocket
    clascolr = ~67 46 47 2 43~       // sorcerer colors
  RET 
    kit_id
END


//patch HLA song (gives "Change Song" ability)
ACTION_IF (FILE_EXISTS_IN_GAME ~SPCL920.SPL~) BEGIN
  COPY ~%abilities%/castspelltarget.EFF~  ~override/%s5%a.EFF~    // unmodded
    WRITE_ASCII 0x30 ~%s5%a~ #8
  COPY ~%passives%/giveability.SPL~       ~override/%s5%a.SPL~
    LPF ALTER_EFFECT STR_VAR resource=~%s5%~ END

  COPY_EXISTING ~SPCL920.SPL~  ~override~
    LPF CLONE_EFFECT
      INT_VAR
        multi_match   = 1
        match_timing  = 9
        opcode        = 177
        parameter1    = IDS_OF_SYMBOL (kit ~D2%kitname%~)
        parameter2    = 9
      STR_VAR
        resource      = ~%s5%a~
    END
  BUT_ONLY
END


//Gains jester effects from items (damage-related only)
OUTER_TEXT_SPRINT kit_group ~jester~
OUTER_TEXT_SPRINT ids_name  ~D2%kitname%~

ACTION_PHP_EACH item_kit_effects AS item => class BEGIN
  ACTION_IF !(~%kit_group%~ STRING_CONTAINS_REGEXP ~%class%~) BEGIN
    ACTION_FOR_EACH res IN ~SPL~ ~ITM~ BEGIN
      ACTION_IF (FILE_EXISTS_IN_GAME ~%item%.%res%~) BEGIN
        COPY_EXISTING ~%item%.%res%~  ~override~
          PHP_EACH kit_ids_array AS class2 => eff_to_copy BEGIN
            PATCH_IF (~%class%~ STR_EQ ~%class2%~) BEGIN
              LPF CLONE_EFFECT 
                INT_VAR 
                  silent            = 1
                  match_opcode      = 177 
                  match_parameter1  = %eff_to_copy%
                  match_parameter2  = 9 
                  parameter1        = IDS_OF_SYMBOL (kit ~%ids_name%~)
              END
            END
          END
        BUT_ONLY
      END
    END
  END
END


//function to block deafness/berserk strings
DEFINE_PATCH_FUNCTION powderkeg_block_strings BEGIN

  LPF DELETE_EFFECT INT_VAR match_opcode=267 match_parameter1=deafness END
  LPF DELETE_EFFECT INT_VAR match_opcode=267 match_parameter1=berserk END
  LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=101 match_parameter2=80 opcode=267 parameter1=deafness END
  LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=101 match_parameter2=3  opcode=267 parameter1=berserk END

END


//Set variables for external use
OUTER_TEXT_SPRINT powderkeg           ~%kitid%~
OUTER_TEXT_SPRINT blastingsong        ~%s1%~
OUTER_TEXT_SPRINT boomingsong         ~%s2%~
OUTER_TEXT_SPRINT dustexplosion       ~%s3%~
OUTER_TEXT_SPRINT frenzyingflame      ~%s4%~
OUTER_TEXT_SPRINT enhancedbardsong    ~%s5%~

//subspells
OUTER_TEXT_SPRINT blastingsongblast   ~%s1%e~    // for Evasion
OUTER_TEXT_SPRINT boomingsongblast    ~%s2%e~
OUTER_TEXT_SPRINT dustexplosionblast  ~%s3%d~
OUTER_TEXT_SPRINT dustblind           ~%s3%a~
OUTER_TEXT_SPRINT dustslow            ~%s3%c~


/*
*/