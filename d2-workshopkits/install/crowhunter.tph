//Crow Hunter (d2wka)


//Add passive effects
COPY ~%passives%/damagevstype.SPL~  ~override/%e1%.SPL~
  LPF ALTER_EFFECT INT_VAR parameter1=1 parameter2=3 special=1 END    // +1 vs. humanoids
  LPF d2_remove_effects_on_cast END
COPY ~%passives%/damagevstype.SPL~  ~override/%e7%.SPL~
  LPF ALTER_EFFECT INT_VAR parameter1=1 parameter2=3 special=2 END    // +2 vs. humanoids
  LPF d2_remove_effects_on_cast STR_VAR resource=~%e1%~ END
  LPF d2_remove_effects_on_cast END
COPY ~%passives%/damagevstype.SPL~  ~override/%e8%.SPL~
  LPF ALTER_EFFECT INT_VAR parameter1=1 parameter2=3 special=3 END    // +3 vs. humanoids
  LPF d2_remove_effects_on_cast STR_VAR resource=~%e1%~ END
  LPF d2_remove_effects_on_cast STR_VAR resource=~%e7%~ END
  LPF d2_remove_effects_on_cast END

//for hla traps
COPY ~%passives%/useeff.SPL~           ~override/%e3%.SPL~
  LPF ALTER_EFFECT STR_VAR resource=~%e3%~ END
COPY ~%passives%/settraps1second.EFF~  ~override/%e3%.EFF~    // skill bonus from EFF works (even if 0 in thiefscl.2da)
  WRITE_LONG 0x1c 200
  WRITE_LONG 0x24 9
  WRITE_LONG 0x28 0

//set snares unusable
COPY ~%passives%/opcode206.SPL~   ~override/%e6%.SPL~
  LPF ALTER_EFFECT STR_VAR resource=~D5CL412~ END
  LPF CLONE_EFFECT STR_VAR resource=~SPCL412~ END


//remove set snares (for multiclass variants or custom multiclasses)
COPY ~%abilities%/applyeffects.SPL~  ~override/%e2%.SPL~
  LPF ALTER_EFFECT STR_VAR resource=~%e2%a~ END
COPY ~%passives%/removespell.SPL~    ~override/%e2%a.SPL~
  LPF ALTER_EFFECT STR_VAR resource=~SPCL412~ END
LAF d2_recast_at_interval STR_VAR spell=~%e2%~ new_spell=~%@2%~ END

COPY ~override/%@2%.SPL~  ~override~
  LPF CLONE_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%e2%~ insert=last END
BUT_ONLY


//
//for Splintering Shot (add piercing resist check to splprot.2da)
OUTER_TEXT_SPRINT d2splintercheck  ~d2splintercheck 23 100 4~    // piercing resist >= 100

LAF d2_splprot_adder STR_VAR match=d2splintercheck string=~%d2splintercheck%~ RET stat END
OUTER_SET d2splintercheck = stat


//
//Add Splintering Shot
COPY ~%icons%/calledshot.BAM~  ~override/%s1%i.BAM~
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@113) STR_VAR bam=~%s1%i~ RET icon END

COPY ~%icons%/splinteringshot.BAM~      ~override/%s1%b.BAM~
COPY ~%abilities%/splinteringshot.SPL~  ~override/%s1%.SPL~
  SAY NAME1 @113
  SAY UNIDENTIFIED_DESC @213
  WRITE_ASCII 0x3a ~%s1%b~ #8
  WRITE_ASCII 0x76 ~%s1%b~ #8
  LPF ALTER_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%s1%~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=249 STR_VAR resource=~%s1%a~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=icon END

COPY ~%abilities%/castspelltarget.EFF~    ~override/%s1%a.EFF~
  WRITE_ASCII 0x30 ~%s1%a~ #8
COPY ~%abilities%/splinteringtarget.SPL~  ~override/%s1%a.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=318 STR_VAR resource=~%s1%s~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=146 STR_VAR resource=~%s1%b~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=146 STR_VAR resource=~%s1%s~ END
  LPF d2_safe_spell STR_VAR insert=above END

COPY - ~%abilities%/invisible15enemy.PRO~  ~%workspace%/%s1%s.PRO~
ADD_PROJECTILE ~%workspace%/%s1%s.PRO~
COPY ~%abilities%/splinteringburst.SPL~    ~override/%s1%s.SPL~
  WRITE_SHORT 0x98 ~%s1%s~    // projectile

COPY ~inlined/bleed_core.SPL~  ~override/%s1%b.SPL~
  WRITE_SHORT 0x98 ~%s1%s~    // projectile
  LPF d2_bleed_delay INT_VAR limit=10 STR_VAR resource=d2bleed1 END
  LPF d2_protect_on_castequip INT_VAR duration=0 cre_type=d2splintercheck STR_VAR insert=first END
  LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=326 STR_VAR resource=d2bleedt END

LAF d2_bleed_removal STR_VAR resource=~%s1%b~ END    // add to d2!bleed.spl
LAF d2_bleed_effects STR_VAR resource=~%s1%b~ END    // blocked by damage immunity (ex. shield of lathander)


//
//Add Visceral Attack
COPY ~%icons%/visceralattackicon.BAM~  ~override/%s2%i.BAM~
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@114) STR_VAR bam=~%s2%i~ RET icon END

LAF d2_color_string INT_VAR stringref=RESOLVE_STR_REF (@114) STR_VAR color=dark RET newtext END
COPY ~%icons%/visceralattack.BAM~      ~override/%s2%b.BAM~
COPY ~%abilities%/visceralattack.SPL~  ~override/%s2%.SPL~
  SAY NAME1 ~%newtext%~
  SAY UNIDENTIFIED_DESC @214
  WRITE_ASCII 0x3a ~%s2%b~ #8
  WRITE_ASCII 0x76 ~%s2%b~ #8
  LPF ALTER_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%s2%~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=248 STR_VAR resource=~%s2%b~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=248 STR_VAR resource=~%s2%a~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=icon END
  LPF d2_mindamageboost INT_VAR amount=3 duration=7 END

//subspell (heal + haste)
COPY ~%abilities%/castspellself.EFF~          ~override/%s2%a.EFF~
  WRITE_ASCII 0x30 ~%s2%a~ #8
COPY ~%abilities%/visceralattackeffects.SPL~  ~override/%s2%a.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=14023 END    // "Hasted"
  LPF d2_remove_effects_on_cast INT_VAR 326override=1 STR_VAR resource=~d2!slow~ END
//  TEXT_SPRINT $d2statusarray(~%DEST_RES%~) ~haste~
  LPF d2_addhitstun END

  // change haste to APR + move bonus
  LPF ALTER_EFFECT INT_VAR match_opcode=16 opcode=1 parameter1=1 parameter2=0 END
  LPF CLONE_EFFECT INT_VAR match_opcode=1 opcode=126 parameter1=250 parameter2=5 END
  LPF d2_remove_effects_on_cast END

  // change healing to 5 + 20%
  LPF ALTER_EFFECT INT_VAR match_opcode=17 parameter1=20 parameter2=2 END
  LPF CLONE_EFFECT INT_VAR match_opcode=17 parameter1=5 parameter2=0 END

//remove effects
COPY ~%abilities%/castspellself.EFF~          ~override/%s2%b.EFF~
  WRITE_ASCII 0x30 ~%s2%b~ #8
COPY ~%abilities%/removespelleffects.SPL~     ~override/%s2%b.SPL~
  LPF ALTER_EFFECT STR_VAR resource=~%s2%~ END


//Make clab table
COPY ~inlined/d2clab_thief_notraps~  ~override/%kitid%.2DA~
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e1%~ END
  LPF d2_kit_ability_adder INT_VAR level=4 multi=1 increment=4 STR_VAR resource=~%s1%~ END
  LPF d2_kit_ability_adder INT_VAR level=9 STR_VAR resource=~%s2%~ END
  LPF d2_kit_ability_adder INT_VAR level=13 STR_VAR resource=~%s2%~ END
  LPF d2_kit_ability_adder INT_VAR level=17 STR_VAR resource=~%s2%~ END
  LPF d2_kit_ability_adder INT_VAR level=5 passive=1 STR_VAR resource=~%e7%~ END
  LPF d2_kit_ability_adder INT_VAR level=10 passive=1 STR_VAR resource=~%e8%~ END
  LPF d2_kit_ability_adder INT_VAR level=14 passive=1 STR_VAR resource=~%e3%~ END    // for hla traps

  //prevent gaining/using traps (for custom multiclass)
  LPF d2_kit_ability_adder INT_VAR passive=1 multi=1 increment=1 STR_VAR resource=~%@2%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e6%~ END
  LPF d2_kit_ability_adder END


//Kit function
LAF ADD_KIT_EX
  INT_VAR
    kit_class = 4
    mixed = RESOLVE_STR_REF (@101001)
    lower = RESOLVE_STR_REF (@101002)
    help = RESOLVE_STR_REF (@101003)
  STR_VAR
    kit_name = ~D2%kitname%~
    clab_path = ~%kitid%~
    clsrcreq = ~1 1 1 1 1 1 1~                  // Usable by all races, if base class is usable
    thiefscl = ~100 100 100 100 100 100 0 0~    // no points in set traps
    thiefskl = ~40 20~                          // start with 40, gain 20 per level
    alignmnt = ~1 1 1 1 1 1 1 1 1~              // all alignments
    clascolr = ~93 182 136 184 93~              // use Stalker colors
  RET 
    kit_id
END


//hla table
OUTER_TEXT_SPRINT ids_name ~D2%kitname%~
OUTER_TEXT_SPRINT abbrev   ~%kitid%~
OUTER_TEXT_SPRINT class    ~thief~
LAF d2_hla_baseclass STR_VAR kit=~%ids_name%~ class=~%class%~ END
LAF d2_hla_tableres STR_VAR kit=~%ids_name%~ abbrev=~%abbrev%~ END


//Gains stalker effects from items
OUTER_TEXT_SPRINT kit_group ~stalker~
OUTER_TEXT_SPRINT ids_name  ~D2%kitname%~
INCLUDE ~%install%/beamdogitems.tph~


//Set variables for external use
OUTER_TEXT_SPRINT crowhunter        ~%kitid%~
OUTER_TEXT_SPRINT splinteringshot   ~%s1%~
OUTER_TEXT_SPRINT visceralattack    ~%s2%~
OUTER_TEXT_SPRINT crowhunterbleed   ~%s1%b~


/*
*/