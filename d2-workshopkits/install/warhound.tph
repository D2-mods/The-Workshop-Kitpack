//Warhound (d2wk2)


//Add passive effects
COPY ~%passives%/acbonus1.SPL~          ~override/%e1%.SPL~
  LPF ALTER_EFFECT INT_VAR parameter2=2 END
COPY ~%passives%/hitdamage1ranged.SPL~  ~override/%e2%.SPL~


//Add Shockwave
COPY ~%icons%/shockwaveicon.BAM~  ~override/%s1%i.BAM~
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@102) STR_VAR bam=~%s1%i~ RET icon END

//COPY ~%icons%/shockwave.BAM~     ~override/%s1%b.BAM~
COPY ~%abilities%/shockwave.SPL~  ~override/%s1%.SPL~
  SAY NAME1 @102
  SAY UNIDENTIFIED_DESC @202
//  WRITE_ASCII 0x3a ~%s1%b~ #8
//  WRITE_ASCII 0x76 ~%s1%b~ #8
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%s1%a~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%s0%x~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=icon END
  LPF ALTER_EFFECT INT_VAR match_duration=12 duration=15 END

COPY ~%abilities%/hiteffectranged.SPL~  ~override/%s1%a.SPL~
  LPF ALTER_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%s1%b~ END    // explosion
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%s1%c~ END    // main target knockback
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%s0%x~ END
  LPF ALTER_EFFECT INT_VAR match_duration=12 duration=15 END

COPY - ~%abilities%/shockwave.PRO~ ~%workspace%/%s1%a.PRO~
ADD_PROJECTILE ~%workspace%/%s1%a.PRO~

COPY ~%abilities%/castspelltarget.EFF~  ~override/%s1%b.EFF~
  WRITE_ASCII 0x30 ~%s1%b~ #8
COPY ~%abilities%/shockwaveblast.SPL~   ~override/%s1%b.SPL~
  SAY NAME1 @102
  LPF ALTER_SPELL_HEADER INT_VAR projectile=~%s1%a~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=12 parameter1=3 END

  // knockback
  LPF CLONE_EFFECT INT_VAR match_opcode=235 duration=25 STR_VAR insert=last END    // shift effects below damage
  LPF CLONE_EFFECT INT_VAR match_opcode=45 duration=25 STR_VAR insert=last END
  LPF DELETE_EFFECT INT_VAR multi_match=1 match_opcode=235 END                     // delete old effects
  LPF DELETE_EFFECT INT_VAR multi_match=1 match_opcode=45 END
  LPF CLONE_EFFECT INT_VAR match_opcode=45 opcode=165 STR_VAR insert=last END      // add "pause" effect (same duration as knockback)
  LPF ALTER_EFFECT INT_VAR match_opcode=45 opcode=39 parameter2=1 duration=45 special=130 END    // change stun to sleep
//  LPF d2_safe_spell INT_VAR source=1 END    // immune to own explosions

/*no effect on huge creatures (not added)
  LPF d2_safe_spell STR_VAR insert=below END
  LPF d2_protect_on_castequip INT_VAR cre_type=13 duration=0 STR_VAR insert=below END
*/

COPY ~%abilities%/castspelltarget.EFF~  ~override/%s1%c.EFF~
  WRITE_ASCII 0x30 ~%s1%c~ #8
COPY ~%abilities%/shockwavepush.SPL~    ~override/%s1%c.SPL~
  LPF ALTER_EFFECT INT_VAR duration=25 END
  LPF ALTER_EFFECT INT_VAR match_opcode=45 opcode=39 parameter2=1 duration=45 special=130 END    // change stun to sleep

/*no effect on huge creatures (not added)
  LPF d2_safe_spell INT_VAR block_opcode=235 END
  LPF d2_protect_on_castequip INT_VAR cre_type=13 duration=0 STR_VAR insert=first END
*/


//Add Incinerate
COPY ~%icons%/incinerateicon.BAM~  ~override/%s2%i.BAM~
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@103) STR_VAR bam=~%s2%i~ RET icon END

//COPY ~%icons%/incinerate.BAM~     ~override/%s2%b.BAM~
COPY ~%abilities%/incinerate.SPL~  ~override/%s2%.SPL~
  SAY NAME1 @103
  SAY UNIDENTIFIED_DESC @203
//  WRITE_ASCII 0x3a ~%s2%b~ #8
//  WRITE_ASCII 0x76 ~%s2%b~ #8
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%s2%a~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%s0%x~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=icon END
  LPF ALTER_EFFECT INT_VAR match_duration=12 duration=15 END

COPY ~%abilities%/hiteffectranged.SPL~  ~override/%s2%a.SPL~
  LPF ALTER_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%s2%b~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%s0%x~ END
  LPF ALTER_EFFECT INT_VAR match_duration=12 duration=15 END

COPY - ~%abilities%/incinerate.PRO~ ~%workspace%/%s2%a.PRO~
ADD_PROJECTILE ~%workspace%/%s2%a.PRO~

COPY ~%abilities%/castspelltarget.EFF~  ~override/%s2%b.EFF~
  WRITE_ASCII 0x30 ~%s2%b~ #8
COPY ~%abilities%/incineratecloud.SPL~  ~override/%s2%b.SPL~
  SAY NAME1 @103
  LPF ALTER_SPELL_HEADER INT_VAR projectile=~%s2%a~ END
//  LPF d2_safe_spell INT_VAR source=1 END    // immune to own explosions


//Remove effects
COPY ~%abilities%/castspellself.EFF~       ~override/%s0%x.EFF~
  WRITE_ASCII 0x30 ~%s0%x~ #8
COPY ~%abilities%/removespelleffects.SPL~  ~override/%s0%x.SPL~
  LPF ALTER_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%s1%~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%s1%a~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%s2%~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%s2%a~ END


//Add Incinerate to CLEARAIR.2DA
OUTER_SET incinerate = (~%%s2%a%~ - 1)    // projectl.ids number is 1 less than variable given by ADD_PROJECTILE
APPEND ~CLEARAIR.2DA~  ~d2incinerate %incinerate%~

COPY_EXISTING ~CLEARAIR.2DA~ ~override~
  PRETTY_PRINT_2DA
BUT_ONLY


//Make clab table
COPY ~inlined/d2clab_fighter~  ~override/%kitid%.2DA~
  LPF d2_kit_ability_adder INT_VAR level=5 passive=1 multi=1 increment=5 STR_VAR resource=~%e1%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e1%~ END
  LPF d2_kit_ability_adder INT_VAR level=3 passive=1 multi=1 increment=5 STR_VAR resource=~%e2%~ END
  LPF d2_kit_ability_adder INT_VAR level=4 multi=1 increment=4 limit=20 STR_VAR resource=~%s1%~ END
  LPF d2_kit_ability_adder INT_VAR level=4 multi=1 increment=4 limit=20 STR_VAR resource=~%s2%~ END
  LPF d2_kit_ability_adder END


//Kit function
LAF ADD_KIT_EX
  INT_VAR
    kit_class = 2
    mixed = RESOLVE_STR_REF (@100201)
    lower = RESOLVE_STR_REF (@100202)
    help = RESOLVE_STR_REF (@100203)
  STR_VAR
    kit_name = ~D2%kitname%~
    clab_path = ~%kitid%~
    clsrcreq = ~1 1 1 1 1 1 1~         // Usable by all races, if base class is usable
//    weapprof = ~1 1 5 1 1 1 1 5 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 5 5 5 5 5 1 1 1 1~
    clascolr = ~97 137 138 142 188~    // Totemic Druid colors
  RET 
    kit_id
END


//hla table
OUTER_TEXT_SPRINT ids_name ~D2%kitname%~
OUTER_TEXT_SPRINT abbrev   ~%kitid%~
OUTER_TEXT_SPRINT class    ~fighter~
LAF d2_hla_baseclass STR_VAR kit=~%ids_name%~ class=~%class%~ END
LAF d2_hla_tableres STR_VAR kit=~%ids_name%~ abbrev=~%abbrev%~ END


//Gains archer effects from items
OUTER_TEXT_SPRINT kit_group ~archer~
OUTER_TEXT_SPRINT ids_name  ~D2%kitname%~
INCLUDE ~%install%/beamdogitems.tph~


//Set variables for external use
OUTER_TEXT_SPRINT warhound         ~%kitid%~
OUTER_TEXT_SPRINT shockwave        ~%s1%~
OUTER_TEXT_SPRINT incinerate       ~%s2%~
OUTER_TEXT_SPRINT shockwaveblast   ~%s1%b~
OUTER_TEXT_SPRINT shockwavepush    ~%s1%c~
OUTER_TEXT_SPRINT incinerateblast  ~%s2%b~


/*
- op183 doesn't work on launchers
- can't give bonus when equipped with a launcher
*/